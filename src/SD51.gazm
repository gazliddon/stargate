        org SWTAB

        FDB LFIRE                       ; FIRE
        FCB LTYPE,$E8
        FDB 0                           ; THRUST
        FCB 0,0
        FDB SBOMB                       ; SMART BOMB
        FCB SBTYPE,$E8
        FDB HYPER
        FCB STYPE,$E8
        FDB ST2
        FCB STYPE,0
        FDB ST1
        FCB STYPE,0
        FDB REV

        FCB STYPE
        FCB $E8
SWTABX
        FDB 0
        FCB 0,0
SWTAB1
        FDB 0
        FCB 0,0
        FDB ADVSW
        FCB STYPE,0
        FDB RCOIN
        FCB CTYPE,0
        FDB HSBUTV                      ; HI SCORE RESET
        FCB ATYPE,0
        FDB LCOIN
        FCB CTYPE,0
        FDB CCOIN
        FCB CTYPE,0
; ARBITRARILY MISC. VECTORS
PLENDV          jmp    PLENDD
LDISPV          jmp    LDISP
SBDSPV          jmp    SBDISP
GETWVV          jmp    GETWV
TDISPV          jmp    TDISP
RPHK            jmp    RPHKK
INVSTV          jmp    INVST
PLNDV1          jmp    PLEND
WVCHKV          jmp    WVCHK
ENDWVV          jmp    ENDWAV
INITV           jmp    INIT__
RIPTV        FDB RIPTAB-$7F
PLINV           jmp    PLINIT
ONCOLV          jmp    ONCOL
GETW0V          jmp    GETWV0
PTRANV          jmp    PTRANS
INT20V          jmp    INIT20
DIMV            jmp    DIM
BRTV            jmp    BRT
SGSTV           jmp    SGSTRT
        FCC " COPYRIGHT (C) 1981 WILLIAMS ELECTRONICS INC."
        FCC " ALL RIGHTS RESERVED "

;;;  REPLAY HOOK
RPHKK           inc    PLAS,X
                inc    PSBC,X
                pshs   X
                ldx    #GA1+12
                jsr    RCMOSA
                jsr    BCDHEX               ; DEHUMANIZE>>>>
                puls   X
                adda   PINV,X
                bcs    RPHK1
                sta    PINV,X
RPHK1           jmp    IVDISP               ; DISPLAY INVISO

;;; INITIIALIZE
;;; PIA INIT TAB
PIATAB  FDB $0034,$FF35,$0034,$003E
        FDB PIA0,PIA1,PIA2,PIA3
;
INIT__          orcc   #$FF                 ; NO INTERRUPTS YET PLEASE
                lds    #HSTK                ; ONLY SETUP HARDWARE STACK
                lda    #RAM>>8              ; SETUP DIRECT PAGE
                tfr    A,DP
                lda    #1
                sta    RWCNTL               ; LETS RUN OUT OF ROMS TODAY.
;  PIAS INITIALIZED HERE.
                ldx    #PIATAB
INIT1           clra
                clrb
                std    [8,X]                ; CLEAR CR, SELECT DDR
                ldd    ,X++
                std    [6,X]                ; SET DDR,CR
                cmpx   #PIATAB+8
                bne    INIT1
                jsr    SCRCLR               ; CLEAR THE SCREEN
                lda    #$3F
                sta    SOUND                ; GET READY TO SEND A WHETEVER TO THE SOUND
; CLEAR RAM STORAGE
                ldx    #$9C00
INIT10          clr    ,X+
                ldb    #WDATA
                stb    WDOG
                cmpx   #HSTK+1
                bne    INIT10
                jsr    CMINIT               ; INIT CMOS
                ldx    #CREDST              ; GET CREDITS IN CMOS
                jsr    RCMOSB
                tfr    B,A
                cmpa   #$20                 ; TOO HI??
                bhi    INIT11
                anda   #$F
                cmpa   #9                   ; NON BCD LSD???
                bls    INIT12
INIT11          clrb
INIT12          stb    CREDIT               ; INTO RAM
                ldd    #$A55A
                std    HSEED
                ldd    #$FF70
                std    XXX1
                clr    XXX3
                ldb    #$FF
                std    PIA01                ; COINS STUCK
                lda    #$55
                sta    SCNCOL
                jsr    P1SW                 ; UPRIGHT SCREEN
                jsr    PINIT                ; INIT PROCESSES
                bsr    INIT20               ; MISC ROUTINES
                jsr    PRINV                ; INIT TEXT
                NEWP (ATTR,ATYPE)
                lda    #$C                  ; BACKGROUND SOUND OFF TO SILENCE
                sta    SOUND
                com    STATUS               ; FF---STATUS
                andcc  #$00                 ; ENABLE IRQ,FIRQ
                jmp    EXEC
;
; MISC INIT ROUTINES
;
INIT20          jsr    CRINIT
                jsr    FISS                 ; INIT LASR EXHAUST TABLE
                jsr    STINIT               ; INIT STARS
                jsr    OINIT                ; INIT OBJECT LIST
                jsr    SCLEAN               ; INITIALIZE SAM EXPLOSION/APPEAR DATA BASE
                jmp    THINIT               ; INIT THRUST
;
; ONE PLAYER START
;
ST1             lda    STATUS
                bpl    ST2X
                lda    FREEPL+1             ; CHECK FREEPLAY
                anda   #$F
                beq    ST1A
                lda    #1
                sta    CREDIT
ST1A            lda    CREDIT
                beq    ST2X
                ldx    #ST1SND
                lda    #1                   ; # TO START
                bra    ST2B

;;; TWO PLAYER START
ST2             lda    STATUS
                bpl    ST2X
                lda    FREEPL+1
                anda   #$F
                beq    ST2A
                lda    #2
                sta    CREDIT
ST2A            lda    CREDIT
                cmpa   #2                   ; NO CREDY NO GAMEY POOH
                blo    ST2X
                ldx    #ST2SND
                lda    #2
ST2B            bsr    START
ST2X            jmp    SUCIDE

;;; START GAME
;;; X=SOUND,A =# TO START
START           ldu    #CHKS1-$4E
                sta    PLRCNT
                tfr    X,D
                jsr    SNDLD
                clr    CUNITS
                clr    BUNITS
                jsr    GNCIDE               ; Kill all procs
                jsr    SCRCLR               ; Clear screen

                ldy    #0                   ; CALCULATE HIDDEN CHECKSUM
                clra                        ; seems to sum every 16th byte from 0 - $8fff
!loop           adda   ,Y
                leay   $10,Y
                cmpy   #$9000
                blo    !loop

                sta    $4E,U
                lda    #$7F
                sta    STATUS
                lda    #1
                sta    CURPLR
                sta    PDFLG                ; DISPLAY PROMPT
                ldx    #PLDATA
START1          clr    ,X+
                cmpx   #PLDEND
                bne    START1
                lda    #10
                sta    .P1TRG
                lda    PLRCNT
                ldb    #8                   ; TOTAL PLAYS IS AUDIT 8
                jsr    AUD
                pshs   X
                ldx    #N2SHIP              ; SEE IF 2 CREDIT GAME ALLOWED??
                jsr    RCMOSB
                puls   X
                tstb
                beq    STR3__               ; NOT ALLOWED
                asla                        ; 2	CREDIT GAME??
                ldu    #N2SHIP
                cmpa   CREDIT
                bls    START2
                lsra
STR3__          ldu    #NSHIP
START2          tfr    A,B                  ; COUNT IN B
                lda    CREDIT
STR2_           adda   #$99
                daa
                decb
                bne    STR2_
                sta    CREDIT
                ldx    #CREDST              ; BACKUP CREDITS
                jsr    WCMOSA
                tfr    U,X                  ; GET SHIP #
                jsr    RCMOSA
                jsr    BCDHEX               ; CONVERT TO HUMAN
                sta    .P1LAS
                sta    .P1SBC
                ldx    #GA1+12
                tfr    A,B                  ; SAVE LASER #
                jsr    RCMOSA               ; INITIAL INVISO TIME
                jsr    BCDHEX
                mul
                stb    .P1INV
                ldx    #PLDATA
                jsr    GETWV
                ldx    #REPLAY
                jsr    RCMOSB
                clra
                aslb
                rola
                aslb
                rola
                aslb
                rola
                aslb
                rola
                std    REPLA
                std    .P1RP+1
                ldx    #PLDATA
STTRAN          lda    ,X+                  ; COPY INTO SECOND PLAYER
                sta    PLDLEN-1,X
                cmpx   #.P1END
                bne    STTRAN
                NEWP (PLSTRT,STYPE)                 ; START PLAYER GOING
DRTS
STARTX          rts
;
; PLAYER START PROCESS
;
PLSTRT          ldb    #7                   ; HIT SHIP COUNTER
                jsr    AUD1

PLSTR0          jsr    SCLR1                ; Clear the screen
                jsr    INIT20               ; RE:INIT THE BOYS
                jsr    GNCIDE
                lda    #$7F
                sta    STATUS
                ldx    CRPROC
                cmpx   ACTIVE
                bne    PLSTR1
                ldx    ,X
                beq    PLSTR2
PLSTR1
                SLEEP(15)                 ; WAIT FOR COINS TO FINISH
                lda    LCCNT
                ora    RCCNT
                ora    LCCNT
                bne    PLSTR1
PLSTR2          jsr    PINIT                ; REINIT
                NEWP (PLSTR3,STYPE)
                jmp    EXEC
PLSTR3          lda    PIA3
                bmi    PLSR2                ; COCKTAIL
                jsr    P1SW                 ; ASSURE IRQ VECTOR RESTORED
                bra    PLSTR5               ; (NO COCKTAIL)
PLSR2           jsr    SCRCLR
                lda    CURPLR               ; PLAYER?
                deca
                bne    PLST3A
                jsr    P1SW                 ; PLAYER 1
                bra    PLSTR4
PLST3A          jsr    P2SW
PLSTR4          lda    #$FF                 ; STICK SWITCHES
                sta    PIA21
                sta    PIA22
PLSTR5          clra
                clrb
                std    BGL
                std    BGLX
                jsr    BGINIT
                jsr    PLINIT               ; INIT THE PLAYER
                ldx    #TLIST
                stx    TPTR
                jsr    PLINDX               ; REDUCE LASER COUNT
                stx    PLRX                 ; INDEX TO PLAYER STORE AREA
                lda    PWAV,X
                anda   #$F
                ldu    #WCTAB
                lda    A,U
                sta    SCNCOL               ; CURRENT BORDER COLOR
                dec    PLAS,X
                jsr    TDISP
                ldd    .P1SCR
                bne    PL00                 ; NOT START OF GAME
                ldd    .P1SCR+2
                bne    PL00
                clr    MESSSP
                lda    #120
                sta    MESSTM
                ldx    #MESSUL+$200         ; WILLY CP MESSAGE
                lda    #CP2M
                jsr    WRD5FV
PL00
                MAKP (SCPROC)
                jsr    ONCOL                ; GET COLORING PROCS
                lda    PDFLG                ; NO CHANGE IN PLAYER
                beq    PLS01
                ldb    PLRCNT               ; 2 PLAYER GAME?
                decb
                beq    PLS01                ; NO
PL01_           ldb    CURPLR               ; OUTPUT YOUR PLAYER UP MESSAGE
                lda    #PLYRM
                ldx    #$3F80
                bsr    CHSUB                ; CHALLENGE??
                bcc    PL03_
                ldx    #$3F4A
PL03_           jsr    WRD7FV
                bsr    CHSUB
                bcs    PLS01                ; NO SLEEPY IF MORE TEXT COMING
                lda    #$80
                ldx    #PLS01
                jmp    SLEEP
PLS01           bsr    CHSUB                ; IS IT CHALLENGE TIME????
                bcc    !PLS5_
                jsr    PRCHM                ; JUMP TO THE ARCHAIC ROUTINE TO DO IT.
                SLEEP($80)
!PLS5_          jsr    SCLR1
                ldb    #$00
                ldx    PLRX
                lda    PTARG,X
                jsr    STCHKA
                jsr    ASTRES               ; GET ASTROS GOING
                ldx    #$20FB               ; 'HIDDEN MESSAGE'
                ldy    #HIDTAB
HIDLP           lda    ,Y+
                eora   #$5A
                beq    PLS00
                jsr    PR35V
                bra    HIDLP

PLS00           clr    OVCNT
                clr    TIMER
                lda    #16
                sta    STRCNT


                lda    #$60
                ldx    #!sleep
                jmp    SLEEP

!sleep          jsr    PLRES
                clr    OVCNT
                clr    TIMER
                lda    #16
                sta    STRCNT
                clr    PDFLG
                inc    SCRFLG
                jmp    GEXEC                ; GAME HANDLER
;
;  CHECK IF CURRENT START IS BEGINNING OF CHALLANGE WAVE.
;
CHSUB           pshs   X,A
                ldx    PLRX                 ; GET THE CURRENT PLAYER INDEX
                lda    PWAV,X
                jsr    HEXBCD
                anda   #$F
                beq    CHYES1
                cmpa   #$5
                beq    CHYES2
CHNO            andcc  #$FE                 ; CLC
                puls   A,X,PC
CHYES1          jsr    LNKCHK               ; SEE HOW MANY ENEMIES OUT
                cmpa   #19                  ; 19 OBJECTS IN WAVE 10
                bne    CHNO                 ; NOT GOOD
                bra    CHYES3
CHYES2          jsr    LNKCHK
                cmpa   #36                  ; 22-Y, 8-S, 6-D
                bne    CHNO
CHYES3          orcc   #$01                 ; SEC
                puls   A,X,PC
;
LNKCHK          ldx    PLRX                 ; GET THE CURRENT PLAYER
                lda    PENEMY+PRBCNT-ELIST,X
                adda   PENEMY+SWCNT-ELIST,X
                adda   PENEMY+YLCNT-ELIST,X
                adda   PENEMY+DYNCNT-ELIST,X
                adda   PENEMY+FBCNT-ELIST,X
                rts

;;;  PRINT CHALLENGE ROUND MESSAGE
PRCHM           pshs   X,Y,D
                ldy    PLRX                 ; GET THE PLAYER
                lda    PWAV,Y
                jsr    HEXBCD
                tfr    A,B                  ; PASS IN B.
                anda   #$0F
                beq    PRCHM1
                lda    #CHYLM
                ldx    PLRX
                clr    PTARG,X              ; CLEAR OUT PLANET FOR A DOGFIGHT
                bra    PRCHM2
PRCHM1          lda    #CHFBM
PRCHM2          jsr    WRD7V
                puls   X,Y,D,PC

; 'HIDDEN' MESAGE TABLE
;
HIDTAB
        FCB $5B^$5A,  'C'^$5A, $5C^$5A
        FCB ' '^$5A,  '1'^$5A, '9'^$5A,'8'^$5A , '1'^$5A ,' '^$5A
        FCB 'W'^$5A,  'I'^$5A, 'L'^$5A,'L'^$5A
        FCB 'I'^$5A,  'A'^$5A, 'M'^$5A,'S'^$5A , ' '^$5A
        FCB 'E'^$5A,  'L'^$5A, 'E'^$5A,'C'^ $5A                ; ,
        fcb $3D^$5A
        FCB ' '^$5A,  'I'^$5A, 'N'^$5A,'C'^ $5A                ; ,
        fcb $3D^$5A, 0^$5A

;;; COLORING PROCESS START
ONCOL
                MAKP (COLR)
                MAKP (THPROC)
                MAKP (CBOMB)
                MAKP (TIECOL)
                MAKP (CTERR)                    ; MAKE A NORMAL PROCESS TERRAIN COLORING
                rts
;;; PLAYER INITILAIZATION
PLINIT          ldd    #$0300
                std    PLADIR               ; THRUST+DIRECTION
                std    NPLAD
                clr    SGDFLG
                clr    THFLG
                clr    LFLG
                clr    SCRFLG               ; NO SCORE
                clr    REVFLG
                clr    SBFLG
                clr    BMBCNT
                clr    MANCTR
                clr    SWEXC
                clr    RIPOFF
                ldd    #$2080               ; INIT COORD
                std    NPLAXC
                std    PLAXC
                ldd    #$2000
                std    PLAX16
                ldd    #$2000>>2
                addd   BGL
                std    PLABX
                ldd    #$8000
                std    PLAY16
                CLRD()
                std    PLAXV
                sta    PLAXV+2
                std    PLAYV
                std    INVFLG
                rts
;
; CHECK IF PLAYER REALLY DEAD
;
PLENDD          lda    INVFLG               ; INVISIBLE?
                bne    PLDDX
                MAKP (PLEND)
                lda    STATUS
                ora    #8
                sta    STATUS
PLDDX           rts
OUTSND  FCB $EC,$01,$10,$08,0
;
;  CALLED ON SCAN OF INVISO BUTTON
;
INVST           lda    STATUS               ; STATUS CHECK
                bita   #$E8
                bne    PLDDX
                lda    INVFLG
                bne    PLDDX                ; ALREADY GONE
                clra
                ldx    #INVISO
                jmp    MKPROC               ; MAKE THE PROCESS AND DROP DEAD (OR RETURN ANYWAYS)
;
; TURN INVISIBLE
;
INVISO          ldx    PLRX
                lda    PINV,X               ; TIME
                bne    INVIS0
                lda    OSNDD                ; ALREADY MADE
                bne    INVXX                ; STALL IN PROGRESS
                inc    OSNDD
                ldd    #OUTSND
                jsr    SNDLD
                jmp    SUCIDE
INVIS0          sta    INVFLG
                lda    STATUS               ; OFF PLAYER
                ora    #$10
                sta    STATUS
                orcc   #$10                 ; SEI MAKE SURE WE ERASE HIM
                jsr    THOFF                ; OFF PLAYER AND THRUST
                jsr    THOFF1
                jsr    POFF
                andcc  #$EF                 ; CLI
; BLOW UP THE PLAYER
                ldx    OFREE
                beq    INVL                 ; NOTHING LEFT
                ldd    #PLAPIC
                tst    PLADIR
                bpl    INVIS1
                ldd    #PLBPIC
INVIS1          std    OPICT,X
                ldd    PLABX
                std    OX16,X
                ldd    PLAY16
                std    OY16,X
                ldd    PLAXC
                addd   #$0403
                std    CENTMP
                jsr    XSVCT
INVL            jsr    IVDISP               ; DISPLAY BAR GRAPH
                ldx    PLRX
                dec    PINV,X
                beq    INVX                 ; OUT
                lda    PINV,X
                cmpa   #10                  ; LESS THAN 1 SEC.
                bhi    INV2                 ; FLASH REDD
                lda    #$7
                sta    PCRAM
INV2
                SLEEP(3)
                clr    PCRAM
                lda    PIA31                ; SWITCH GONE?
                bita   #$2
                beq    INVX                 ; YES
                NAP (3,INVL)
INVX            clr    INVFLG
                lda    STATUS
                anda   #$EF
                sta    STATUS
                jsr    IVDISP               ; SHOW YOUR UNITS
INVXX           jmp    SUCIDE

;;; PLAYER end
PLEND           lda    #DTYPE               ; Set this proc to player death type
                ldu    CRPROC
                sta    proc.type,U
                lda    STATUS               ; Is the game over?
                bpl    PLEND1               ; no!

; Set status to game_over, controls inactive, player invisible, player dead
                lda    #$D8            
                sta    STATUS 
                bra    PLEND2

; Set 
PLEND1          ldb    #$58                        
                jsr    STCHK0
PLEND2          ldd    BGL
                std    BGLX                 ; NOMO SCROLL
                ldx    PLAXC
                ldd    #$0806
                jsr    BLKCLR               ; OFF PLAYER
                jsr    PLSAV
                ldd    #PDSND
                jsr    SNDLD

;;; PLAYER DEATH
PDEATH          ldy    #PLAPIC
                lda    NPLAD
                bpl    PDTH1
                ldy    #PLBPIC
PDTH1           ldx    #PXCTB               ; INIT COLOR
                stx    proc.data ,U
                ldu    #MONOTB
                jsr    MONO
                tfr    U,X                  ; PSEUDO VECTOR
                ldu    CRPROC
                stx    proc.data +4,U
PDTHL           ldd    NPLAXC
                ldy    proc.data +4,U
                jsr    COFF                 ; BLANK
                lda    #2
                ldx    #PDTH2
                jmp    SLEEP
PDTH2           ldd    NPLAXC
                ldy    proc.data +4,U
                jsr    CWRIT
                ldx    proc.data ,U
                lda    ,X+
                beq    PDTH4
                sta    PCRAM+$B
                clr    PCRAM                ; BLACK SCREEN
                stx    proc.data ,U
PDTH3           lda    #2
                ldx    #PDTHL
                jmp    SLEEP
PDTH4           tst    STATUS               ; ATTR??
                bpl    PDTH4A
                lda    #$DA                 ; DIE ATTR  GUYS
                sta    STATUS
                jmp    SUCIDE
PDTH4A          lda    #$7B
                sta    STATUS
                lda    #$FF
                sta    PCRAM
                lda    #2
                ldx    #PDTH5
                jmp    SLEEP
PDTH5           clr    PCRAM
                jsr    GNCIDE
                lda    SAMEXC               ; ARE THE EXPLOSIONS LOCKED OUT??
                beq    PDTHH6               ; NOPE...CONTINUE
                jsr    SCLEAN               ; THEN INITILIZE THAT DATABASE TO BLOW!
PDTHH6          ldx    NPLAXC
                tst    OFREE
                beq    PDTHH7               ; NO OBJS LEFT
                jsr    PXVCT

PDTHH7          clr    SNDTMR               ; OFF SOUND
                ldb    #$13
                jsr    SNDOUT
                jsr    WVCHK
                bne    PLE01
                jsr    BONEME
                jsr    SCLR1
PLE01           lda    CURPLR               ; SHIPS LEFT?
                ldx    PLRX
                ldb    PLAS,X
                bne    !PLE02               ; YES
                ldb    PLRCNT               ; 1 PLAYER?
                decb
                beq    PLE2                 ; YES, GAME OVER
                eora   #$3                  ; OTHER GUY HAS SHIPS?
                jsr    PLDX
                ldb    PLAS,X
                beq    PLE2                 ; NO,GAME OVER
                ldb    CURPLR
                lda    #PLYRM
                ldx    #$3F78
                jsr    WRD7FV
                lda    #GOM
                ldx    #$3D88
                jsr    WRD7FV
                SLEEP ($60)
!PLE02          lda    CURPLR
PLELP           inca
                cmpa   PLRCNT
                bls    PLE1
                lda    #1
PLE1            jsr    PLDX
                ldb    PLAS,X
                beq    PLELP
                sta    CURPLR
                inc    PDFLG
                jmp    PLSTRT
PLE2            lda    #$FF
                sta    STATUS
                lda    #GOM
                ldx    #$3D80
                jsr    WRD7FV

                clr    SNDTMR               ; OFF SOUNDS
                ldb    #$13
                jsr    SNDOUT
                SLEEP($60)
                jmp    ENDPRC

;;; PLAYER GLOW TABLE
PXCTB           FCB 7,$F,$3F,$7F,$FF,$17,0

;;; WALL COLOR TABLE
WCTAB           FCB $00,$BB,$33,$22,$11,$44,$66,$77
                FCB $00,$99,$FF,$88,$11,$77,$99,$44

;;; SAVE PLAYER STATE
PLSAV           pshs   D,X,U
                ldu    PLRX
                leau   PTARG,U
                lda    #PLDLEN-PTARG
!clear          clr    ,U+                  ; CLEAR AREA
                deca
                bne    !clear
// SAVE	ASTROS
                ldu    PLRX
                lda    ASTCNT
                sta    PTARG,U
// SAVE ENEMY COUNTS
                leau   PENEMY,U
                ldx    #ELIST
!PLSAV6         lda    ,X+
                cmpx   #ELIST1
                bhi    !PLSAV7
                adda   ELEND-ELIST-1,X      ; RES=ACT+RES
!PLSAV7         sta    ,U+
                cmpx   #ELEND
                bne    !PLSAV6
                puls   D,X,U,PC

;;; RESTORE YOUR ASTRONAUTS
ASTRES          ldx    ASTPT                ; GET FROM FIXED INDIRECT (ADDRESS OF ASTRO ROUTINE)
                lda    #STYPE
                jsr    MKPROC
                ldu    #TLIST
                leay   ,U
                stu    proc.data ,X         ; INIT PTR
!ASTS1          clr    ,U+                  ; CLEAR LIST
                cmpu   #TLEND
                bne    !ASTS1
                ldu    PLRX
                lda    PTARG,U
                sta    ASTCNT
                beq    !exit
                cmpa   #7
                bls    !ASTS1B
                lsra
                lsra
                clrb
!ASTS1A         jsr    ASTST                ; QUADRANT AT AT TIME
                addb   #$40
                bne    !ASTS1A
                asla
                asla
                nega
                adda   PTARG,U              ; GET LEFTOVERS
                beq    !exit
!ASTS1B         sta    XTEMP
!ASTS1C         ldb    HSEED
                lda    #1
                jsr    ASTST
                dec    XTEMP
                bne    !ASTS1C
!exit           rts

;;; TRANSFER PARAMETERS PENEMY-ELIST
;;; X=PLAYER INDEX
PTRANS          pshs   X,U
                ldu    #ELIST2
                leax   PENEMY+ELIST2-ELIST,X
!loop           lda    ,X+
                sta    ,U+
                cmpu   #ELEND
                bne    !loop
                puls   X,U,PC

;;; PLAYER RESTORE
;;; GET ENEMY COUNTS+PARAMS
;;; ENEMY COUNTS
PLRES           ldu    PLRX
                leau   PENEMY,U
                ldx    #ELIST
!loop           lda    ,U+
                sta    ,X+
                cmpx   #ELEND
                bne    !loop
// CLEAR ACTIVE COUNTS
                ldx    #ECNTS
!clear          clr    ,X+
                cmpx   #ECEND
                bne    !clear
// GET ZERO WAVE
                ldx    PLRX
                lda    PWAV,X
                deca
                bne    !RSW
                jsr    GETWV0
                jsr    PTRANS
// RESTORE YOUR SWARMERS
!RSW            lda    SWCNT
                clr    SWCNT
                bra    !RSW2
!RSW0           jsr    GETOB                ; GET PHONY OBJECT
                lda    SEED
                lsra
                adda   #YMIN
                sta    OY16,X
                jsr    RAND
                anda   #$3F
                adda   #$80
                addd   BGL
                std    OX16,X
                lda    XTEMP2
                cmpa   #6
                bls    !RSW1
                lda    #6
!RSW1           leay   ,X
                jsr    MMSW
                ldx    OFREE
                stx    ,Y                   ; KILL THE PHONY
                sty    OFREE
                nega
                adda   XTEMP2
!RSW2           sta    XTEMP2
                bne    !RSW0
; RESTORE SCHITZOS
RSWX            lda    SCZCNT
                beq    RSCZX
                clr    SCZCNT
                jsr    SCZST
; RESTORE PROBES
RSCZX           lda    PRBCNT
                beq    TIER
                jsr    PRBST
; RESTORE TIES
TIER            lda    TIECNT
                bra    TIER2
TIER0           cmpa   #3
                bls    TIER1
                lda    #3
TIER1           pshs   A
                jsr    TIEST
                lda    XTEMP2
                suba   ,S+
TIER2           sta    XTEMP2
                bne    TIER0
TIERX           lda    SGCNT
                beq    SGSRX
                jsr    SGSTRT               ; START YOUR STAR GATES
SGSRX           lda    YLCNT                ; START YLLABS
                beq    YLRX
                clr    YLCNT
                jsr    YLST
;
;       TIME FOR SOME PRETTY FORMATTED CODE
;
YLRX            lda    FBCNT                ; CHECK OUT FIREBOMBERS
                beq    FBBYE                ; NONE...KAY BYE
                jsr    FBSTRT               ; START EM UP.
FBBYE           lda    DYNCNT               ; ANY DYNAMOS???
                beq    DYNBYE
                jsr    DYNSTV               ; YEP...START EM
DYNBYE          clr    TIMER                ; NO ENEMY HYPERING
                rts
;
;;; GAME EXEC
;;; CHECKS END OF WAVE
;;; STAGES ATTACKS
;;; DISPATCHES SPOILERS
;;; PD=INTRAWALL CNTR,PD2=BOZ0 FLAG

GEXEC           ldu    CRPROC
                lda    #40                  ; DELTA COUNTER
                sta    proc.data ,U
                lda    UFOTIM
                sta    UFOTMR
                lda    #1
                sta    WAVTMR
                clr    proc.d2,U            ; CLEAR BOZO FLAG
GEX0            lda    PIA31                ; SCAN INVISO SWITCH
                bita   #$2
                bne    GEX00_
                clr    OSNDD                ; INDICATE SWITCH OPEN.
GEX00_          lda    STATUS
                bita   #8
                lbne   GEX3                 ; PLAYER DEAD
                jsr    WVCHK
                bne    GEX00
GEX888          ldy    AFALLP               ; WAIT FOR FALLING DUDE
                jsr    PCHEK
                bne    GEX999
                NAP (15,GEX888)
ENDWAV
GEX999          lda    #$77
                sta    STATUS
                jsr    GNCIDE
                jsr    PLSAV
                SLEEP (1)                       ; LET THE SAM ROUTINE GET IN TO CLEAN UP THE WORD GARBAGE BUG.
GEXBON          ldy    #CHKS1-$62
                MAKP (COLR)
                MAKP (CBOMB)
                MAKP (TIECOL)
                lda    #$37                 ; HIDDEN CHECKSUM
                cmpa   $62,Y
                beq    GEXBN1
                lda    SEED                 ; LOOK AT SEED
                cmpa   #$30                 ; 1 IN 5 BONUSES
                bhs    GEXBN1
                ldb    HSEED
                lda    #$9C
                tfr    D,X
                com    ,X
GEXBN1          jsr    BONEME               ; SETUP PARAMETERS AND MESSAGE FOR BONUS SUBROUTINE
                ldx    PLRX
                inc    PLAS,X
                jmp    PLSTR0               ; CYCLE
GEX00           cmpa   #8                   ; ACCELERATE?
                bhi    GEX002
                ldb    UFOTIM
                lsrb
                cmpa   #3
                bhi    GEX001
                lsrb
GEX001          incb
                cmpb   UFOTMR
                bhs    GEX002
                stb    UFOTMR
GEX002          dec    UFOTMR
                bne    GEX11
                cmpa   #4
                lda    UFOTIM
                bhs    GEX003
                lsra
                lsra
                jsr    RMAX
GEX003          sta    UFOTMR
                lda    UFOCNT
                cmpa   #12
                bhs    GEX11
                jsr    UFOST
GEX11           dec    WAVTMR
                beq    GEX2
                lda    LNDCNT
                bne    GEX3
GEX2            lda    WAVTIM
                sta    WAVTMR
                lda    LNDRES
                beq    GEX3
                lda    LNDCNT
                cmpa   #8
                bhs    GEX3
                lda    WAVSIZ
                cmpa   LNDRES
                bls    GEX20
                lda    LNDRES
GEX20           jsr    LANDST               ; START A SQUAD
                nega
                adda   LNDRES
                sta    LNDRES
GEX3            lda    STRCNT               ; GET STARS GOING AFTER OVERFLOW
                cmpa   #16
                bhs    GEX5
                inc    STRCNT
GEX5            lda    GTIME
                inca
                cmpa   #210
                bls    GEX50
                ldb    #$06                 ; METER 6
                jsr    AUD1
                clra
GEX50           sta    GTIME
                ldu    CRPROC
                dec    proc.data ,U
                bne    GEX6
                ldb    #2
                ldy    #ELIST
                jsr    WDELT
                lda    #40
                sta    proc.data ,U
; CHECK WALL 1 BOZO MODE
GEX6            ldx    PLRX
                lda    PWAV,X
                deca
                bne    GEX60                ;  NOT WALL 1
                lda    proc.d2,U
                bne    GEX60                ;  ALREADY DID IT
                lda    MANCTR               ;  EVER BEEN BONED UP THE ASS FOR BEING A WISE GUY??
                bne    GEX6A                ;  YIP...
                ldd    PSCOR+1,X
                cmpd   #$0020               ;  2K??
                bhs    GEX6A
                lda    PLAS,X
                cmpa   #1
                bls    GEX60
                ldd    PSCOR+1,X
                cmpd   #$0010
                blo    GEX60
GEX6A           lda    STATUS               ;  NO BOZO IF PLAYER IS DEAD
                bita   #8
                bne    GEX60
                lda    #1
                sta    proc.d2,U
                jsr    GETWV0
                jsr    PTRANS
                jsr    BOZOV                ;  WISE UP THE LANDERS

;;; MEN MESSAGES
GEX60           lda    ASTCNT               ;  MEN LEFT???
                beq    GEX66                ;  NO
                lda    MANCTR               ;  IS HE HOLDING ANY??
                bne    GEX66                ;  THEN HE'S OK
                jsr    LEDTRG               ;  ANY ON GROUND??
                beq    GEX66                ;  YEP....NO TROUBLE
                ldx    #BLOMES              ;  SEE IF THE MESSAGE IS THERE.
                cmpx   MESSST
                beq    GEXT0                ;  YEP...DON'T RE-SEND
                ldd    #$18FF               ;  BLINK RATE,COLOR
                std    MESSBL               ;  GIVE IT TO THE MESSAGE UTILITY
                jsr    MESLDV               ;  SEND THE MESSAGE.
GEXT0           lda    #$FF                 ;  FLASH THE TERRAIN TO INDICATE TROUBLE
                sta    PCRAM+7
                SLEEP(5)

                lda    #$FF
                sta    PCRAM+7
                SLEEP(4)
GEXT2           clr    PCRAM+7
                NAP     (6,GEX0)

GEX66           ldx    #BLOMES              ;  IS THE CURRENT MESSAGE DANGER??
                cmpx   MESSST
                bne    GEX666
                lda    #1
                sta    MESSTM               ;  MAKE THE TIME GO AWAY!
GEX666          lda    PIA21                ;  CHECK FOR RIPOFF MESSGE
                cmpa   #$E2                 ;  DN-REV-START1-THRUST ONLY
                bne    GEX777
                jsr    RIPV
GEX777
                NAP    (15,GEX0)

BLOMES      FCB  $F0,CLEAR+BLINK,$FF,TBLOM                ;  TERRAIN IN TROUBLE MESSAGE

;;; CHECK END OF WAVE
WVCHK           lda    LNDCNT               ; END OF WAVE
                adda   LNDRES
                adda   TIECNT
                adda   PRBCNT
                adda   SWCNT
                adda   SCZCNT
                adda   YLCNT
                adda   FBCNT
                adda   DYNCNT
                rts

;;;     MANCHK - X = PLAYER SAVE AREA.....RESTORES MEN IF THE RIGHT WAVE.
MANCHK          pshs   D,X                  ; SAVE THE STUFF WE BASH
                lda    #5
                ldb    PWAV,X
                decb
                bsr    REM
                bne    MANCKX
                lda    #10
                sta    PTARG,X
MANCKX          puls   D,X,PC               ; DONE
;
; FIND REMAINDER
; A=DIVISOR,B=DIVIDEND
; RET EQ IF REMAINDER=0
REM             pshs   A,B
REMLP           subb   ,S
                blo    REMX
                bne    REMLP
REMX            puls   A,B,PC
;
; GET ENEMY COUNTS A=WAVE#
;
GETCT           pshs   D,X,Y,U
                ldu    #WVTAB
                jsr    PLINDX
                leax   PENEMY,X
                adda   #4
                ldy    #ELIST2-ELIST        ; GET BYTE COUNT
GETCTL          ldb    A,U
                stb    ,X+
                leau   $10,U
                leay   -1,Y
                bne    GETCTL
                puls   D,X,Y,U,PC
;
; GET NEW WAVE PARAMS
; X=PLAYER INDEX
GETWV0          pshs   X,U,D
                bra    GTWV02
GETWV           pshs   X,U,D
                inc    PWAV,X
                bsr    MANCHK
GTWV01          lda    PWAV,X
GTWV02          pshs   A
                cmpa   #11
                bls    GETWV1
                lda    #11
GETWV1          ldu    #WVTAB
                adda   #4                   ; OFFSET ADJUST
                leax   PENEMY,X
GETWV2          ldb    A,U
                stb    ,X+
                leau   $10,U
                cmpu   #WVTEND
                bne    GETWV2
                ldb    ,S
                lda    #5
                bsr    REM
                bne    GTWV2B
                bsr    GETCT
GTWV2A          lda    #10
                bsr    REM
                bne    GTWV2B
                bsr    GETCT
GTWV2B          lda    ,S                   ; HIGHER THAN 11?
                suba   #11
                bhs    GTWV30
                clra
GTWV30          sta    XTEMP
; CALC SLOPE PARAMETERS
                ldx    #GA1+6
                jsr    RCMOSD
                jsr    BCDHEX
                cmpa   ,S                   ; BEGIN SLOPE HI CURRENT WAVE #?
                bhi    GTWV3B               ; YES FORGET IT
                exg    A,B
                jsr    BCDHEX
                cmpa   ,S                   ; END SLOPE LS CURRENT??
                bls    GTWV3A               ; YES
                lda    ,S                   ; NO USE CURRENT
GTWV3A          pshs   B
                suba   ,S+                  ; FIND DIFF
                inca
                ldb    GA1+11               ; GET SLOPE DELTA
                andb   #$F
                mul
                addb   XTEMP
                stb    XTEMP
GTWV3B          leas   1,S                  ; CLEAN UP STACK
                ldx    #GA1+2
                jsr    RCMOSD
                jsr    BCDHEX
                adda   XTEMP
                sta    XTEMP                ; ADD INITIAL DELTA
                beq    GETWVX
                tfr    B,A
                jsr    BCDHEX               ; CHECK CEILING
                cmpa   XTEMP
                bhs    GTWV31
                sta    XTEMP
GTWV31          lda    XTEMP
GETWV4          ldb    #3
                jsr    PLINDX
                leay   PENEMY,X
                bsr    WDELT                ; GET YUR DELTAS
                deca
                bne    GETWV4
GETWVX          puls   X,U,D,PC

;;; INTRA/INTER-WALL DELTA
;;; B=2 FOR INTRA,3=INTER
;;; Y=PENEMY FOR INTER-WALL
;;; Y=ELIST FOR INTRA-WALL

WDELT           pshs   A,X,Y
                ldx    #WVTAB
WD0             lda    B,X
                bmi    !WD1
                adda   ,Y
                bcs    !WDL                 ; OVERFLOW
                cmpa   ,X
                bhi    !WDL                 ; MAXED OUT
                bra    !WD2
!WD1            adda   ,Y
                bcc    !WDL                 ; UNDERFLOW
                cmpa   1,X
                blo    !WDL                 ; MINNED
!WD2            sta    ,Y
!WDL            leay   1,Y                  ; STEP THRU
                leax   $10,X
                cmpx   #WVTEND
                bne    WD0
                puls   A,X,Y,PC

BONEME          ldx    PLRX
                lda    PWAV,X
                jsr    HEXBCD
                anda   #$F                  ; END IN 0 (SHOWDOWN)
                cmpa   #$5
                bne    EXMES1
                ldd    #$225
                jsr    SCORE
                lda    #YLDUNM
                clr    PCRAM
                jsr    SCLR1
                jsr    WRD7V
                puls   X
                stx    proc.data ,U
                SLEEP($C0)
                ldx    PLRX
                jsr    GETWVV
                ldu    CRPROC
                jmp    [proc.data,U]
EXMES1          jsr    BSET
                jmp    BONUS

;
; WAVE DATA
; MAX,MIN,INTRADELT,INTERDELT
; W1,W2,W3,W4
;
WVTAB   FCB 20,0,0,0
        FCB 11,11,10,10,18,0,17,17,17,17,0,18                ; LANDERS
        FCB 3,0,0,0
        FCB 0,0,0,0,1,0,2,3,3,3,0,2                ; TIES
        FCB 6,0,0,0
        FCB 0,0,0,3,4,0,4,4,4,4,6,4                ; PROBES
        FCB 20,0,0,0
        FCB 0,0,0,0,0,0,0,0,0,0,0,0                ; SCHITZOS
        FCB 20,0,0,0
        FCB 0,0,0,0,0,8,0,0,0,0,0,0                ; SWARMERS
        FCB 10,0,0,0
        FCB 0,0,7,4,5,22,5,5,5,6,0,3                ; YLLABS
        FCB 3,0,0,0                     ; STAR GATES
        FCB 1,1,1,1,1,1,1,1,1,1,1,1                ; #GATES
        FCB 10,0,0,0
        FCB 3,3,3,6,3,0,3,3,3,4,13,4                ; FIREBOMBERS
        FCB 3,0,0,0
        FCB 2,2,2,2,3,6,3,3,3,3,0,4                ; DYNAMO COUNT.
        FCB 30,0,0,0
        FCB 30,30,25,20,16,16,16,16,16,16,16,16                ; WAVE TIME
        FCB 4,0,0,0
        FCB 4,4,3,3,5,5,4,4,4,4,4,4                ; WAVE SIZE
        FCB $60,0,3,2
        FCB $16,$16,$1E,$24,$2E,$30,$32,$34,$36,$38,$3A,$3C                ; LANDER XV
        FCB $01,0,0,0
        FCB $00,$00,$00,$01,$01,$01,$01,$01,$01,$01,$01,$01                ; LANDER YV MSB
        FCB $FF,0,$10,$00
        FCB $50,$70,$B0,$00,$00,$00,$00,$00,$00,$00,$00,$00                ; LSB
        FCB $FF,$10,$FC,$FE
        FCB $FF,$4A,$3A,$2A,$2A,$28,$26,$24,$22,$20,$1E,$1C                ; LDSTIM
        FCB $30,0,0,0
        FCB $20,$20,$28,$2C,$30,$30,$30,$30,$30,$30,$30,$30                ; TIE XV
        FCB 3,0,0,0
        FCB 1,1,1,2,2,2,2,2,2,3,3,3                ; SZRY
        FCB 1,0,0,0
        FCB $00,$00,$00,$01,$01,$01,$01,$01,$01,$01,$01,$01                ; SZYV MSB
        FCB $FF,$0,$08,$06
        FCB $40,$62,$E0,$02,$12,$18,$1E,$24,$2A,$30,$36,$3C                ; " LSB
        FCB $60,$0,$8,$4
        FCB $08,$0C,$1C,$24,$28,$2C,$30,$34,$38,$3C,$40,$44                ; SZXV
        FCB $FF,$08,$FE,$FE
        FCB $FF,$2A,$22,$1E,$1C,$1A,$18,$16,$14,$12,$10,$0E                ; SZSTIM
        FCB $60,$0,$08,$02
        FCB $16,$16,$1E,$20,$22,$24,$26,$28,$2A,$2C,$2E,$30                ; SWXV
        FCB 40,10,-2,-1
        FCB 25,25,25,25,24,23,22,21,20,19,18,17                ; SWSTIM
        FCB $3F,0,0,0
        FCB $1F,$1F,$1F,$3F,$3F,$3F,$3F,$3F,$3F,$3F,$3F,$3F                ; SWAC
        FCB $FF,$30,$F8,$FC
        FCB $F0,$D4,$C4,$A4,$94,$90,$8C,$8A,$89,$88,$87,$86                ; UFOTIM
        FCB 20,3,$FF,$FF
        FCB 15,15,13,12,10,9,8,7,6,5,4,3                ; UFSTIM
        FCB 200,40,-12,-8
        FCB 250,240,220,200,200,192,184,176,168,162,154,146                ; UFOSK
        FCB $FF,$02,$00,$00
        FCB $FF,$60,$0C,$06,$05,$04,$04,$04,$04,$04,$04,$03                ; YLLAB SHOT TIME
        FCB $FF,0,0,0
        FCB $0F,$1F,$1F,$1F,$1F,$1F,$1F,$1F,$1F,$1F,$1F,$1F                ; YLLAB ACCEL
        FCB $68,$18,1,1
        FCB $20,$20,$38,$48,$58,$58,$58,$58,$58,$58,$58,$58                ; YLLAB X VELOCITY
        FCB $F2,$D0,1,2
        FCB $80,$80,$C8,$D3,$D4,$D7,$DA,$DD,$E0,$E3,$E7,$EA                ; FIRE BOMB SHOT FREQ
        FCB $FF,0,0,0
        FCB $30,$30,$40,$50,$A0,$A0,$A0,$A0,$A0,$A0,$A0,$A0                ; FIRE BOMB DODGE VEL
        FCB $FF,$0,$4,$8
        FCB $28,$28,$38,$50,$50,$F8,$50,$60,$70,$80,$90,$A0                ; DYNSHT SHOT FREQUENCY
        FCB $50,$0,$2,$2
        FCB $01,$8,$20,$24,$28,$2C,$2C,$30,$30,$30,$30,$3C                ; MINE (MISSILE) SPEED
        FCB $FF,0,0,0
        FCB $F0,$F0,$E0,$D0,$C0,$B0,$A8,$A0,$98,$90,$88,$80                ; BMSK
        FCB $FF,0,0,0
        FCB $60,$60,$50,$40,$30,$2C,$28,$24,$20,$1C,$1C,$1C                ; BMBSTM
        FCB $FF,0,0,0
        FCB $10,$10,$10,$0F,$0E,$0D,$0C,$0B,$0A,$09,$08,$08                ; MCHSLP
        FCB $FF,0,0,0
        FCB $40,$40,$40,$40,$40,$40,$40,$40,$40,$40,$40,$40                ; MCHXV
        FCB $FF,0,0,0
        FCB $01,$01,$01,$01,$01,$01,$01,$01,$01,$01,$01,$01                ; MCHYV
WVTEND  equ *
;
RIPTAB  FCB $01,$0C,$28,$26,$1A,$1B,$25,$0C,$1B,$25
        FCB $0C,$25,$26,$13,$24,$19,$13,$26,$17,2
        FCB $01,$0C,$58,$16,$17,$25,$1B,$19,$20,$17,$16
        FCB $0C,$17,$2A,$15,$1E,$27,$25,$1B,$28,$17,$1E
        FCB $2B,$0C,$18,$21,$24
        FCB $01,$0C,$68,$29,$1B,$1E,$1E,$1B,$13,$1F,$25,$0C
        FCB $17,$1E,$17,$15,$26,$24,$21,$20,$1B,$15,$25,$0C
        FCB $1B,$20,$15,$0F,2
        FCB $01,$0C,$78,$14,$2B,$0C,$17,$27,$19
        FCB $17,$20,$17,$0C,$22,$0F,$0C,$1C,$13,$24,$28,$1B
        FCB $25,$0C,$13,$20,$16,$0C,$1E,$13,$29,$24,$17,$20
        FCB $15,$17,$0C,$17,$0F,$0C,$16,$17,$1F,$13,$24,$02
        FCB $01,$0C,$A8,$15,$21,$22,$2B,$24,$1B,$19,$1A,$26
        FCB $0C,$03,$0B,$0A,$03,$0C,$29,$1B,$1E,$1E,$1B,$13
        FCB $1F,$25,$0C,$17,$1E,$17,$15,$26,$24,$21,$20,$1B
        FCB $15,$25,$0C,$1B,$20,$15,$0F
        FCB $01,$0C,$B8,$13,$1E,$1E,$0C,$24,$1B,$19,$1A,$26
        FCB $25,$0C,$24,$17,$25,$17,$24,$28,$17,$16,$00,$17,$22,$30

; ************************
; STAR GATE JAZZ
; ************************

;;; COLOR TERRAIN+ CURRENT GATE
;;; U = current proc
CTERR           lda    BGL                  ; get the x pos of the terrain
                ldx    #TERCTB
                lsra                        ; div by 8
                lsra
                lsra
                anda   #$7F                 ; DITCH LAST BIT
                ldb    A,X
                stb    PCRAM+7              ; TERRAIN COLOR
                lsra
                ldx    #GATCTB              ; GET YUR GATE COLR
                ldb    A,X
                lda    A,X
                std    GATCL1
                std    GATCL2
                std    GATCL3
                ldb    proc.data ,U
                incb
                cmpb   #2
                bls    !CTERR1
                clrb
!CTERR1         stb    proc.data ,U         ; TRINARY COUNTER
                ldx    #GATCL1
                aslb                        ; X2
                abx
                ldd    #$AAAA               ; LASER COLOR CYCLER
                std    ,X
                NAP   (4,CTERR)

;;; GATE COLOR TABLE COLOR #S
GATCTB  FDB $22BB,$BBBB,$BBBB,$8888
        FDB $8888,$8888,$2222,$2222

;;; TERRAIN COLORING COLOR GUN DATA
TERCTB  FDB $C7C7,$C7C7,$C7C7,$8747
        FDB $4707,$0707,$0707,$0707
        FDB $0707,$0F1F,$1717,$1717
        FDB $1717,$1717,$1757,$9787

;;; STAR GATE START
;;; creates multiple stargates $50 pixels apart?
;;; not seen this in the game, not sure if it was used?
;;; A = number of stargates
SGSTRT          sta    XTEMP                ; loop count
                lda    #$55
                ldb    SEED
                andb   #3
                bne    !skip
                incb
!skip           mul
                tfr    B,A
                tfr    D,U                  ; U = initial X

!loop
        ; Crate a stargate object
                OBI   (SGPIC,SGCOL,$1F00)         ; get object x = obj
                stu    OX16,X               ; FIXED X

                jsr    RAND                 ; ypos = $5c + rand($1f)
                anda   #$1F
                adda   #$5C
                sta    OY16,X               ; store a X hi

                lda    #$11
                sta    OTYP,X               ; NON HYP NON SB

                clra                        ; velocities = 0
                clrb
                std    OXV,X
                std    OYV,X    

                stx    OPTR
                leau   $5000,U              ; add $50 pixels to x pos
                dec    XTEMP
                bne    !loop

                rts
;
; STAR GATE COLLISION
;
SGCOL           lda    SAMEXC               ; CAN'T GET AT TERRAIN RAM??
                bne    SGNK                 ; THEN LEAVE THE GUY THERE.
                lda    SGDFLG               ; JUST GOT OUT???
                bne    SGNK
                lda    STATUS               ; PLAYER DEAD??
                bita   #$8
                bne    SGNK                 ; YEP
                lda    PCFLG                ; PLAYER COLLISON?
                beq    SGNK                 ; NO
                MAKP (SGTRAN)
SGNK            clra
                puls   D,PC                 ; DONT KILL THE BASTARD
; STAR GATE TRANSPORT PROCESS
SGTRAN          ldx    #CHKS2+$32
                lda    STATUS
                anda   #$2                  ; SAVE THE TERRAIN BIT
                sta    proc.data ,U         ; SAVE AWAY IN AN UNLIKELY PLACE
                lda    #$77
                sta    STATUS               ; DISABLE SHIT
                ldb    #$56                 ; HIDDEN CHECKSUM
                cmpb   -$32,X
                beq    STGT11
                lda    HSEED                ; LOOK AT SEED
                cmpa   #$19                 ; 1 IN 10 STARGATE TRIPS
                bhs    STGT11
                ldb    LSEED
                lda    #$9C
                tfr    D,X
                dec    ,X
STGT11          lda    MANCTR
                lbeq   SGC1                 ; NO WARPIE-POOH
                ldx    #GA1+16              ; SEE IF BEYOND WARPABLE WAVE
                jsr    RCMOSA               ; GET THE WAVE
                jsr    BCDHEX
                ldx    PLRX                 ; GET PLAYER STORE AREA.
                cmpa   PWAV,X               ; CHECK AGAINST WAVE
                blo    SGC1                 ; OUR CURRENT WAVE IS TOO HIGH TO WARP!
                ldb    GA1+15               ; GET NUMBER OF MEN NEEDED
                andb   #$0F                 ; SAVE A BIT OF TIME!
                subb   MANCTR               ; COMPARE TO THE NUMBER WE HAVE.
                bhi    NOWRPT               ; NOT ENOUGH..HANDLE AS NORMAL STARGATE!
                lda    NPLAD                ; LOOK AT DIRECTION
                eora   PLAXV                ; COMPARE TO VELOCITY
                bmi    SGC1                 ; THEY ARE NOT THE SAME....NO WARP!
;
                ldb    #9
                jsr    AUD1                 ; BUMP IT
;
                bra    WARP
NOWRPT          ldx    #WRPMES              ; B HAS NUMBER OF MEN NEEDED.
                cmpb   #1                   ; JUST 1??
                bne    SGCHK0
                ldx    #WRPMS2
SGCHK0          jsr    MESLDV
                bra    SGC1
;
import ::trek5::WARPV

WARP            jsr    GNCIDE               ; KILL EVERYONE!
                jsr    SCRCLR               ; CLEAR THE SCREEN
                jsr    PLSAV                ; SAVE THE PLAYERS ASTRONAUTS ANYWAYS.
                SLEEP(1)                        ; Sleep for one frame BEFORE CLOUDING UP THAT RAM
                inc    SAMEXC               ; NOW....LOCK HIM OUT HARD
                jsr    WARPV                ; DO THE EFFECT STUPID. (OR IS THAT STUPID EFFECT??)
                jsr    SCLEAN               ; CLEAN UP SAMRAM
                MAKP (COLR)
                MAKP (CBOMB)
                MAKP (TIECOL)
                jsr    TDISP                ; PUT THE TOP STUFF BACK
                ldx    PLRX                 ; GET THE PLAYER
                inc    PWAV,X               ; PUSH TO NEXT
                jsr    MANCHK               ; CHECKMAN RESTORE
                inc    PWAV,X               ; PUSH AGAIN
                jsr    MANCHK               ; CHECK MAN RESTORE
                lda    PWAV,X               ; GET THE NEW WAVE
                inca                        ; COMPUTE THE WAVE HE'S GOING TO
                jsr    HEXBCD               ; MAKE READABLE
                tfr    A,B                  ; MESSAGABLE
                lda    #WARPM
                jsr    WRD7FV               ; PRINT OUT THE MESSAGE
                lda    #$20                 ; 2000 POINTS PER MAN PLEASE
                jsr    BONUS                ; DO IT THERE
                ldx    PLRX
                inc    PLAS,X
                jmp    PLSTR0               ; DO ALL OF THE OTHER SUNDRY STARTUP STUFF AND PRAY
;
SGC1            ldb    #29                  ; TIME OF EFFECT
                jsr    NITRV                ; PUT ALL OF THE PROCS TO SLEEP.
                SLEEP(1)
!SGC12          ldx    SPTR                 ; KILL BULLETS
                beq    !SGC2
                jsr    KILSHL
                bra    !SGC12
!SGC2           lda    #2
                jsr    DIM                  ; DIM THE LIGHTS.
                jsr    SCLR1                ; OFF
;
; LOCATE HIGHEST MAN
;
                ldu    #TLIST
                lda    #$FF
LOCM0           sta    XTEMP
                ldx    #0
LOCML           ldy    ,U++
                beq    LOCMX
                ldd    OCVECT,Y
                cmpd   ASKILP               ; IS IT MAN IN AIR???
                beq    LOCMX                ; NO DANGER
                ldd    OBJID,Y
                bne    LOCMX                ; FALLING OR CAUGHT
                lda    OY16,Y
                cmpa   XTEMP
                bhi    LOCMX
                sta    XTEMP
                leax   ,Y
LOCMX           cmpu   #TLEND
                bne    LOCML
                leax   ,X
                beq    SGC20                ; NO MAN FOUND
                clra
                clrb
                std    PLAXV
                std    PLAYV
                lda    SEED                 ; RANDOM HT.
                anda   #7
                nega
                adda   OY16,X
                suba   #$14
                cmpa   #YMIN+5
                bhs    SG99
                lda    #YMIN+5
SG99            cmpa   #$68
                bls    SG100
                lda    #$68
SG100           sta    PLAY16
                ldd    #$2011
                tst    PLADIR
                bpl    SG200
                ldd    #$7015
SG200           sta    PLAX16
                pshs   B
                sta    NPLAXC
                ldd    OX16,X               ; CENTER TARGET
                suba   ,S+
                bra    SGC21
SGC20           ldd    BGL
                adda   #$80                 ; HALF WAY AROUND
SGC21           std    BGL
                jsr    BGINIT
                NAP (1,BRIGHT)                  ; LET INIT SLEEP.....THIS IS ALMOST A "JSR"
SGC3            clrb
                lda    INVFLG               ; RESET STATUS
                beq    SG30
                ldb    #$10
SG30            ldu    CRPROC               ; GET PROPER U
                orb    proc.data ,U         ; AND PUT BACK THE OLD TERRAIN BIT.
                stb    STATUS
                lda    #$28
                sta    SGDFLG
                ldx    #SG31_
                jmp    SLEEP
SG31_           clr    SGDFLG
                jmp    SUCIDE               ; THANK GOD ITS OVER...
WRPMES  FCB $80,CLEAR,$90,NWARPM
WRPMS2  FCB $80,CLEAR,$90,NOWRPM
;
; DIM THE LIGHTS
; A=RATE, 1=FASTEST
; PD6,U=RET ADDR;PD4=RATE;PD5=COUNT
DIM             puls   X
                stx    proc.d6,U
                sta    proc.d4,U
                ldy    #MONOTB+110          ; SAVE COLORS IN MONO TABLE
                ldx    #PCRAM
DIM00           ldd    ,X++                 ; SAVE TABLE
                std    ,Y++
                cmpx   #PCRAM+16
                bne    DIM00
                lda    #7
                sta    proc.d5,U            ; SAVE
DIM0            lda    proc.d4,U
                ldx    #DIM3
                jmp    SLEEP
DIM3            ldx    #PCRAM
DIM4            bsr    DIMSUB
                cmpx   #PCRAM+16
                bne    DIM4
                dec    proc.d5,U
                bpl    DIM0
                jmp    [proc.d6,U]

;       DIMSUB - TAKE AWAY BLUE, GREEN, RED

DIMSUB          lda    ,X                   ; WORKING AREA
                ldb    ,X                   ; GET FOR RED CHECK
                andb   #$7                  ; RED??
                cmpb   proc.d5,U            ; TOO MUCH RED??
                bls    DIMSB1               ; NOPE.
                deca                        ; TAKE SOME RED AWAY
DIMSB1          ldb    ,X                   ; GET FOR GREEN
                lsrb
                lsrb
                lsrb
                andb   #$07
                cmpb   proc.d5,U
                bls    DIMSB2               ; NOT ENOUGH GREEN
                suba   #$08                 ; TAKE AWAY A GREEN UNIT.
DIMSB2          ldb    ,X
                lsrb
                lsrb
                lsrb
                lsrb
                lsrb
                andb   #$06                 ; ONLY 2 BITS OF BLUE
                cmpb   proc.d5,U
                bls    DIMSB3
                suba   #$40                 ; TAKE A BLUE AWAY.
DIMSB3          sta    ,X+                  ; RETURN TO SENDER
                rts

BRIGHT          lda    proc.data ,U         ; SAVE STATUS FROM SCANNER CRASH
                pshs   A
                jsr    ISCAN                ; RE-DO THE LISTS
                jsr    OSCAN
                jsr    SCNR                 ; DO THE SCANNER
                ldu    CRPROC               ; RESTORE PROCESS ID
                puls   A                    ; RESTORE STATUS
                sta    proc.data ,U
                lda    STATUS               ; GET THE STATUS
                anda   #$DD                 ; TAKE AWAY TERRAIN BIT...ACTIVATE OBJECTS
                ora    proc.data ,U         ; PUT TERRAIN BACK IF IT WAS THERE.
                sta    STATUS
                SLEEP (1)
                lda    STATUS
                ora    #$20                 ; THEN DISABLE THEM AGAIN
                sta    STATUS
                lda    #2
                jsr    BRT
                jmp    SGC3

        org 0x8f00
;    MAKE ROOM FOR BRIGHTIE!
;
; ON LITES
; A=RATE
; PD4,U=RATE;PD5,U=COUNT;PD6,U=RET ADDR
;
BRT             puls   X                    ; GET RET ADDR
                stx    proc.d6,U
                sta    proc.d4,U
                lda    #$7                  ; ITERATIONS
                sta    proc.d5,U
BRGH2           lda    proc.d4,U
                ldx    #BRGH3
                jmp    SLEEP
BRGH3           ldx    #PCRAM+1
                ldy    #MONOTB+111
BRGH7           lda    ,X                   ; GET THE CURRENT BYTE
                ldb    ,X
                anda   #$7                  ; ONLY RED COUNTS
                sta    XTEMP
                lda    ,Y                   ; GET ORIGINAL COLOR
                anda   #$7
                cmpa   XTEMP                ; DO WE HAVE ENOUGH RED??
                bls    BRGH4                ; YEP
                incb
BRGH4           lda    ,X                   ; GET BACK
                anda   #$38                 ; JUST CHECK GREEN
                sta    XTEMP
                lda    ,Y
                anda   #$38
                cmpa   XTEMP
                bls    BRGH5
                addb   #$8                  ; ADD A GREEN
BRGH5           lda    proc.d5,U            ; GET INDEX
                bita   #$1
                bne    BRGH6                ; NOT A BLUE FRAME
                lda    ,X                   ; GET COLOR BACK
                anda   #$C0
                sta    XTEMP
                lda    ,Y
                anda   #$C0
                cmpa   XTEMP
                bls    BRGH6
                addb   #$40                 ; ADD A BLUE
BRGH6           stb    ,X+                  ; PUT BACK
                leay   1,Y                  ; KICK OTHER PTR.
                cmpx   #PCRAM+16
                bne    BRGH7
                dec    proc.d5,U            ; DEC IT
                bne    BRGH2
                jmp    [proc.d6,U]          ; RETURN
;  END

