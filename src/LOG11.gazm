
;  NLIST
;  INCLUDE SPHR56.SRC
;  INCLUDE EQ4.SRC
;  LIST
;  TTL <<<<< S T A R G A T E  >>>>>
            org LOGORG

ATTR        JMP ATTR_           ;;; ATTRACT MODE
GOV         JMP ATTR_           ;;; ATTRACT MODE
HALVCT      JMP HALL            ;;; Hall of fame
GNCDAV      JMP GNCIDA          ;;; Kill everything

;;; ATTRACT MODE
ATTR_       LDX #CHKS2+78                   ; HIDDEN CHECKSUM
            LDY #$D004
            CLRB
ATTR0_      ADDB ,Y
            LEAY $9,Y
            CMPY #$F000
            BLO ATTR0_
            STB -78,X
            NOP
            JSR GNCIDE

            // Create a new ATYPE proc
            NEWP(CRDSCN,ATYPE)
            LDA ATRFLG
            BNE HALLV                       ; LET HIM SEE THE HALL
            LDA CREDIT
            LBNE CRDPAG
HALLV       JMP HALL

;;; __Function:__ GNCIDA
;;;
;;; Kill all procs apart from CTYPE or ATYPE
;;;
;;; * CTYPE are coin monitoring procs
;;; * ATYPE are attract mode proc
GNCIDA      PSHS A,X
            LDX #ACTIVE
!loop       LDX ,X
            BEQ !exit           ; exit if end of list
            CMPX CRPROC         ; next if this is currentproc
            BEQ !loop

            ; Do not kill procs of CTYPE or ATYPE
            LDA proc.type,X
            CMPA #CTYPE
            BEQ !loop
            CMPA #ATYPE
            BEQ !loop

            JSR KILL            ; Woo kill it and loop
            BRA !loop
!exit       PULS A,X,PC         ; Return

////////////////////////////////////////////////////////////////////////////////
;;; __Function:__ CRDSCN
;;; Proc to scan for new coins being inserted
;;; Runs every other frame
CRDSCN      LDA CREDIT
            STA proc.data,U
            CLR proc.d1,U
            CLR proc.d2,U
CRDSLP      LDA CREDIT
            CMPA proc.data,U
            BEQ CRDSLX
            STA proc.data,U
            LDA ATRFLG
            CMPA #$50                       ; CREDIT PAGE??
            BNE CRDSL1
            JSR OCRDM
            BRA CRDSLX
CRDSL1      JSR GNCIDE
            LDA STYPE
            LDX #CRDPAG
            JSR MSPROC
CRDSLX      LDA PIA21
            ANDA #$8
            CMPA proc.d1,U
            BEQ HALSX
            STA proc.d1,U
            BEQ HALSX
            LDA ATRFLG
            CMPA #$10
            BEQ HALSX
            JSR GNCIDE
            LDA STYPE
            LDX #HALL
            JSR MSPROC
HALSX       LDA PIA21
            ANDA #$40
            CMPA proc.d2,U
            BEQ SECSX
            STA proc.d2,U
            BEQ SECSX
            LDA ATRFLG
            CMPA #$40
            BEQ SECSX
            JSR GNCIDE
            LDA STYPE
            LDX #SECRET
            JSR MSPROC
SECSX       NAP (2,CRDSLP)

;;; CREDIT PAGE DISPLAY
CRDPAG  
        ; Storing $50 into ATRFLG means we're in the credits
            LDA #$50
            STA ATRFLG

            LDA #$FF
            STA STATUS
            JSR GNCIDA
            JSR SCRCLR
            JSR CRINIT
            JSR BORDER
            JSR ONCOLV
            MAKP (OPMCOL)
            JSR OCRDM
            LDU CRPROC
            LDA CREDIT
            STA proc.d4,U
            LDA #2
            STA PLRCNT
            LDA #8
            STA proc.d3,U                       ; TIMER
            LDX #$14AC
            JSR OSEXM                       ; OUTPUT EXTRA SHIP MESSAGE
            LDX #$3EE0
            LDA #CPM                        ; COPYRIGHT MESSAGE
            JSR WRD5FV
            LDX #$35E8
            LDA #WEIM
            JSR WRD5FV
            LDA #HSCRM                      ; HI SCOR AND SECRETS
            LDX #$2BCC
            JSR WRD5FV
            LDX #$33C0
            LDA #SECM
            JSR WRD5FV
CRDP0       LDX #NSHIP
            JSR RCMOSD
            STD proc.d1,U
            LDB FREEPL+1
            ANDB #$F
            BEQ CRDP00                      ; NO FREE PLAY
            CLR proc.d2,U
CRDP00      LDB proc.d2,U
            BNE CRDP1                       ; 2 CREDIT OPTION SELECTED
            LDX #$3858
            BRA CRDP2
CRDP1       LDX #$5958
            TFR B,A
            LDB #2
            JSR CRDSB
            LDA proc.d1,U
            LDX #$1958
CRDP2       LDB #1
            JSR CRDSB
CRDP3       LDA CREDIT
            CMPA #1
            BEQ CRDP3A                      ; NO TWO PLAYER, DONT ERASE
            JSR CRDWT
            NAP (10,CRDP3A)
CRDP3A      LDB #1
            LDX #$3514
            LDA #NPSM
            JSR WRD7FV
            LDA CREDIT
            CMPA #2
            BLO CRDP4
            LDA proc.d2,U
            BNE CRDP4A
CRDP4       LDA proc.d1,U
CRDP4A      CLRB
            BSR CRDSB1
            NAP ($80,CRDP5)
CRDP5       LDA CREDIT
            CMPA #2
            BLO CRDP6
            JSR CRDWT
            NAP (10,CRDP50)
CRDP50      LDB #2
            LDX #$3514
            LDA #NPSM
            JSR WRD7FV
            LDB proc.d1,U
            LDA CREDIT
            CMPA #4
            BLO CRDP5A
            TST proc.d2,U
            BEQ CRDP5A
            LDB proc.d2,U
CRDP5A      TFR B,A                         ; SAME FOR BOTH PLAYERS
            BSR CRDSB1
CRDP6       DEC proc.d3,U                       ; TIMER
            LBEQ HALL                       ; TIME OUT
            LDA CREDIT
            CMPA proc.d4,U
            BEQ CRDP7
            STA proc.d4,U
            LDA #08
            STA proc.d3,U
CRDP7
        NAP ($80,CRDP3)

;;; CREDIT PAGE DISPLAY SUBROUTINE
;;; A=#SHIPS,B=#CREDIT GAME
CRDSB       PSHS A,X
            LDA #NCGM
            JSR WRD7FV
            LDB ,S
            LDX 1,S
            LEAX $18,X
            LDA #NSHM
            JSR WRD7FV
            LDX 1,S
            LEAX $28,X
            LDA #NSBM
            JSR WRD7FV
            LDX 1,S
            LEAX $38,X
            LDA #NIUM
            JSR WRD7FV
            PULS A,X,PC

;;; PLAYER OBJECT DISPLAY SUBROUINTE
;;; A=PLAYER 1 SHIP #,B=PLAYER 2
CRDSB1      STA .P1LAS
            STA .P1SBC
            PSHS B
            LDB #10
            MUL
            STB .P1INV
            PULS B
            STB .P2LAS
            STB .P2SBC
            LDA #10
            MUL
            STB .P2INV
            JSR LDISP
            JSR SBDISP
            JMP IVDISP

;;; CREDIT START MESS OFF
CRDWT       LDD #0
            BSR CRDSB1
            LDX #$2E13
            LDD #$3D08
            JMP BLKCLR

;;; OUTPUT EXTRA SHIP MESSAGE
;;; X=ADDR
OSEXM       PSHS X
            LDX #REPLAY
            JSR RCMOSB
            TSTB
            // NO REPLAYS
            BEQ !exit            
            LDX ,S
            LDA #SEXM
            JSR WRD5FV
!exit       PULS X,PC

;;; OUTPUT CREDIT MESSAGE SUBROUTINE
OCRDM       LDX #$3E40
OCRDM1      LDB CREDIT
            LDA #CREDM
            JMP WRD7FV

;;; SECRETS PAGE
SECRET      LDA #$40
            STA ATRFLG
            LDA #$FF
            STA STATUS
            JSR GNCIDA
            LDA #1
            JSR DIMV
            JSR SCRCLR
            LDX #$0E10
            LDD #$0801
            JSR OTSGM
            LDU CRPROC
            LDA #SECSM
            LDX #$4044
            JSR WRD7FV
            LDX #GA1+14
            JSR RCMOSB                             ; GET WARPING PARAMS
            CLRA
            TFR D,Y
            JSR RCMOSB
            LDA #SWRPM
            JSR WRD7FV
            LDD #$1111
            STD GATCL1
            LDD #$4444
            STD GATCL2
            LDD #$7777
            STD GATCL3
            LDD #$338F
            JSR ONSG
            LDX #$14CA
            JSR OSEXM
            LDX #$3EE0
            JSR OCRDM1
            MAKP (OPMCOL)
            MAKP (LOGCOL)
            MAKP (CBOMB)
            LDU CRPROC
            LDD #$1040
            STD proc.data,U
SECR0       DEC proc.data+1,U
            BEQ SECX                               ; MAXIMUM TIMEOUT
            DEC proc.data,U
            BEQ SECR1
SEC55
            NAP (60,SECR0)
SECR1       LDA PIA21
            ANDA #$40
            BEQ SECX
            LDA #1
            STA proc.data,U
            BRA SEC55
SECX        LDA CREDIT
            BEQ HALL
            JMP CRDPAG

;;; STARGATE IMMORTALS HALL OF FAME
HALL        LDA #$FF
            STA STATUS
            LDA #$66
            STA SCNCOL
            LDA #$10
            STA ATRFLG
            JSR GNCIDA
            CLRA                                   ; BECOME A SUPER PROCESS
            LDX #HALL1
            JSR MSPROC
            JMP SUCIDE

// ***********************
// ***********************BEGINING OF LINKY INSERTION
// ***********************

HSY     equ $28                                ; Y OFFSET OF ENTIRE TABLE
HCOL1   equ $8
             
;;; __Proc:__ HALL1
;;; Proc that oversees the hall of fame
HALL1       LDX #PCRAM                             ; CLEAR OUT COLORS
!loop       CLR ,X+
            CMPX #PCRAM+16
            BLO !loop

            JSR PRINV
            JSR SCRCLR
            JSR SCLEAN                             ; KILL SAM STUFF
            MAKP (OPMCOL)
            LDA #HSTABM                            ; PRINT BORING STUFF ON THE SCREEN
            JSR WRD7V
            LDX #SGMSP                             ; MAKE A PROCESS TO OUTPUT THE STARGATE MESSAGE
            CLRA
            JSR MSPROC                             ; DONE
            LDA #$AA
            STA TEXCOL                             ; MAKE GOD NAME VISIBLE

            LDX #GODSCR+38                         ; POINT AT LAST LETTER
!loop2      JSR RCMOSA                             ; FETCH A LETTER
            LEAX -4,X                              ; UNDO INC, AND BACKUP 1
            CMPA #$3A                              ; SPACE??
            BNE !LHAL2
            CMPX #GODSCR
            BHS !loop2

!LHAL2      LEAX 2,X                               ; POINT AT LAST
            PSHS X                                 ; PUT IT ON STACK
            LDX #$0E47                             ; GODS PLACE IN THE WORLD
            LDA #$31
            JSR PR57V
            LDA #$5C
            JSR PR57V
            LEAX $300,X
            LDY #GODSCR
!LHAL3      EXG X,Y
            JSR RCMOSA                             ; GET A CHARACTER
            EXG X,Y
            JSR PR57V                              ; PRINT IT
            CMPY ,S                                ; COMPARE TO LAST
            BLS !LHAL3                              ; GOD NAME HAS BEEN PRINTED
            LEAX $200,X                            ; SLIDE OVER 8 PIXELS
            LDY #GODSCR+40                         ; POINT AT SCORE
            LDU #WRD7V                             ; PRINT IN 7 HIGH
            JSR PRSCOR                             ; PRINT GOD SCORE
            LEAX $500,X
            LDA #$5B
            JSR PR57V
            LDY #GODINT                            ; POINT AT INITIALS
            LDB #3
!LHA3__     EXG X,Y
            JSR RCMOSA
            EXG X,Y
            JSR PR57V
            DECB
            BNE !LHA3__
            LDA #$5C
            JSR PR57V

//  PD = CURRENT INDEX
//  PD1= ENTRIES LEFT IN THIS COLUMN
//  PD2= COLUMNS LEFT
//  PD3= CURRENT X
//  PD4= CURRENT Y
//  PD5= POINTER TO CURRENT GUY IN CMOS
//  PD7= # OF ENTRIES IN COLUMN
            ldu     CRPROC
            lda     #$2                                ; START WITH INDEX 2
            sta     proc.data,U
            ldd     #$0C56
            std     proc.data+3,U
            ldy     #CMSCOR
            sty     proc.data+5,U
            lda     #13
            sta     proc.data+7,U
            jmp     HSCOLM                             ; PRINT THIS MATRIX OF THE TABLE
PRTODY      ldu     CRPROC
            lda     #1
            sta     proc.data,U                        ; START WITH 1 FOR TODAYS
            ldd     #$0CD0
            std     proc.data+3,U
            ldy     #TODAYS
            sty     proc.data+5,U
            lda     #2
            sta     proc.data+7,U
            jmp     HSCOLM
HSCOLM      lda     #3                                 ; 3 COLUMNS
            sta     proc.data+2,U                      ; SET IT HARD
            clr     TXFLAV
HSCOL0      lda     proc.data+7,U                      ; NUMBER PER COLUMN
            sta     proc.data+1,U                      ; MAKE THAT NUMBER LEFT IN THIS COLUMN
HSCOL3
            NAP (1,!sleep)                         ; TIME BREAK HERE

!sleep
            // Update the text colour
            LDA TEXCOL                             ; GET CURRENT COLOR
            ADDA #$11                              ; PUSH TO NEXT
            CMPA #$88
            BLS !nowrap
            LDA #$11
!nowrap     STA TEXCOL

            LDX proc.data+3,U                      ; GET ADDRESS
            LDB proc.data,U                        ; GET PLAYERS INDEX
            LDA #HSINDX                            ; INDEX MESSAGE
            JSR WRD5V                              ; PRINT IT

            LDY proc.data+5,U                      ; POINT AT PLAYERS INITIALS
            LDB #3                                 ; ITERATE 3 TIMES
!hscol2     EXG X,Y
            JSR RCMOSA                             ; GET AN INITIAL
            EXG X,Y
            JSR PR35V                              ; PRINT IT OUT
            DECB
            BNE !hscol2                             ; INITIALS ARE OUT THERE

            LDX proc.data+3,U                      ; GET CURSOR POINTER AGAIN
            LEAX $E00,X                            ; POSITION FOR SCORE
            LDU #WRD5V                             ; USE 3 BY 5 SCORE ROUTINE
            JSR PRSCOR                             ; PRINT OUT THE GUYS SCORE
            LDU CRPROC
            LDY proc.data+5,U                      ; GET CMOS POINTER
            LEAY SCRSIZ,Y
            STY proc.data+5,U
            LDX proc.data+3,U                      ; GET CURRENT CURSOR
            LEAX 7,X                               ; MOVE 7 PIXELS
            STX proc.data+3,U
            LDA proc.data,U                        ; KICK INDEX
            ADDA #1
            DAA
            STA proc.data,U
            DEC proc.data+1,U                      ; ONE LESS GUY IN THIS COLUMN
            BNE HSCOL3
            LEAX $3000,X                           ; MOVE TO NEXT COLUMN
            LDA #7                                 ; HEIGHT PER ENTRY
            LDB proc.data+7,U                      ; ENTRIES PER COLUMN
            MUL
            NEGB                                   ; OPPOSITE OF TOTAL HEIGHT
            LEAX B,X                               ; TO RESTORE THAT HEIGHT
            STX proc.data+3,U                      ; RESTORED
            DEC proc.data+2,U                      ; ONE LESS COLUMN
            BNE HSCOL0                             ; DO ANOTHER
            CMPY #TODAYS+SCRSIZ                    ; ARE WE IN TODAYS
            LBLS PRTODY                            ; NOPE...GO AND DO TODAYS
                                               ;
            MAKP (LOGCOL)                          ; DONE...START THE COLORS
            LDU CRPROC
            LDA #8
            STA proc.data,U
!HALX1      DEC proc.data,U
            BEQ !HALX
            NAP (60,!HALX1)
!HALX       LDA #120
            STA proc.data,U
!halx2      LDA PIA21
            ANDA #$8
            BEQ LOGO
            DEC proc.data,U
            BEQ LOGO
            NAP (30,!halx2)

SGMSP       LDX #$0E10                             ; TOP OF SCREEN
            LDD #$0A01
            JSR OTSGM                              ; PUT IT OUT
            JMP SUCIDE                             ; AND DIE
PRSCOR      PSHS Y                                 ; SAVE Y FOR THE HELL OF IT
            EXG X,Y                                ; SWAP EM
            JSR RCMOSA
            JSR RCMOSB
            EXG X,Y
            ANDA #$0F
            BNE PR2SC                              ; THIS WILL BE A TWO CALL JOB
            TSTB
            BNE PR2SC                              ; THIS WILL BE A ONE CALL JOB
            LDA #HIGH3                             ; USE OTHER ROUTINE
            BRA PR3SC
PR2SC       PSHS Y                                 ; SAVE NEW Y
            TFR D,Y                                ; DATA TO PRINT
            LDA #HIGH1                             ; USE FIRST PRINT TEXT
            JSR ,U                                 ; CALL PASSED ROUTINE TO DO IT
            PULS Y
            LDA #AUDM2                             ; USE AUDIT TEXT TO PUT OUT LOW HALF
PR3SC       PSHS A
            EXG X,Y
            JSR RCMOSA
            JSR RCMOSB
            EXG X,Y
            TFR D,Y
            PULS A
            JSR ,U
            PULS Y,PC                              ; AND RETURN

// ***********************
// ***********************END OF LINKY INSERTION
// ***********************
LOGO        JSR GNCIDA
            LDA #$20
            STA ATRFLG                             ; LOGO PROGRAM
            LDA #1
            JSR DIMV                               ; DIM DOWN, CHAPS
            JSR SCRCLR
            MAKP (OPMCOL)
                                               ;
                                               ; OUTPUT OPERATOR MESSAGE
                                               ; PD=MESS PTR,PD2=SCREEN ADDR,PD4=BYTE CNT.,PD5=PASS FLAG
OOM         CLR proc.d6,U
            JSR ATMCK                              ; MESSAGE O.K.???
            BEQ OOM0
            LDX #OMST
            INC proc.d6,U                          ; ERROR FLAG
            BRA OOM00
OOM0        LDX #OPMESS
OOM00       STX proc.data,U
            LDX #OPL1                              ; LOCATION OF LINE 1
            LDB #$20
            JSR GETMEX
            STX proc.d2,U
            LDA #25
            STA proc.d4,U
            STA proc.d5,U
            LDA #$AA
            STA TEXCOL
            CLR TXFLAV
OOML        LDX proc.data,U
            LDA proc.d6,U                          ; ERROR?
            BEQ OOML0                              ; NO
            LDA ,X+
            BRA OOML00
OOML0       JSR RCMOSA
OOML00      STX proc.data,U
            LDX proc.d2,U
            JSR PR57V
            STX proc.d2,U
            DEC proc.d4,U
            BEQ OOFIN
OOML1
            NAP (3,OOML)
OOFIN       LDA proc.d5,U
            BEQ OOFX
            CLR proc.d5,U
            LDA #25
            STA proc.d4,U
            LDX #OPL2
            LDB #$30
            JSR GETMEX
            STX proc.d2,U
            CLR TXFLAV
            BRA OOML1
OOFX
            MAKP (COLSHK)
            LDX #$0E58
            LDD #$0102
            JSR OTSGM
            NAP (40,LOG20)
LOG20       JSR GNCIDA
            CLR PCRAM
            MAKP (OPMCOL)
            MAKP (LOGCOL)
            LDA #CPM
            LDX #$3498
            JSR WRD7FV
            LDA #WEIM
            LDX #$25A8
            JSR WRD7FV
            LDX #$3ABC
            JSR OCRDM1                             ; OUTPUT CREDIT MESSAGE
            LDA #HSCRM
            LDX #$28CC
            JSR WRD5FV
            LDA #SECM
            LDX #$30D4
            JSR WRD5FV
            NAP (40,LOG2)
LOG2
            LDX proc.data+10,U                     ; GET START ADDRESS
            LEAY $8824,X
            LDA #7                                 ; SET COLOR 9 TO RED
            STA PCRAM+9
            LDA #$99                               ; AND DO HALO IN COLOR 9
            JSR HALO
            NAP ($FF,PICKUP)                       ; DELAY 4 SECS AND GO TO LOOP

; OPERATOR DEFAULT MESSAGE

OMST        FCC "WILLIAMS ELECTRONICS INC"
            FCB $3D                         ; PERIOD
            FCC "           PRESENTS"
            FCB $3F                         ; COLON
            FCC "     "
;
GETMEX      LDA #$25                        ; ASSUME ERROR
            TST proc.data+6,U
            BNE GETME3
            JSR RCMOSA                      ; GET THE BYTE
            CMPA #$10
            BHS GETME2
            LDA #$10
GETME2      CMPA #$3C
            BLS GETME3
            LDA #$3C
GETME3      TFR D,X
            RTS

; OPERATOR MESS COLORER
OPMCOL      INC proc.data,U
            LDA proc.data,U
            ANDA #$7
            LDX #OPMTAB
            LDA A,X
            STA PCRAM+$A
            NAP (5,OPMCOL)
OPMTAB      FCB $7,$87,$FF,$FF,$C7,$C7,$FF,$FF

;;; LOGO CYCLER
;;; PD,U=COLOR INDEX
LOGCOL      LDX #LCTAB
            STX proc.data,U
LOGLP       LDX proc.data,U
            LDY #PCRAM+1
LOGL0       LDA ,X+
            BNE LOGL1
            LDX #LCTAB
            BRA LOGL0
LOGL1       STA ,Y+
            CMPY #PCRAM+9
            BNE LOGL0
            LDX proc.data,U
            LEAX 1,X
            LDA ,X
            BNE LOGL2
            LDX #LCTAB
LOGL2       STX proc.data,U
            NAP (3,LOGLP)
;
; LOGO COLORING TABLE
;
LCTAB       FCB $37,$2F,$27,$1F,$17,$47,$47,$87
            FCB $87,$C7,$C7,$C6,$C5,$CC,$CB,$CA
            FCB $C0,$D0,$98,$38,$33,0,0,0
            FCB 0,0,0,0,0,0,0,0
            FCB 0,0,0,0,0,0,0,0
;
; COLOR SHAKER
;
COLSHK      LDX #SHKTAB
            JSR RAND
            ANDA #$F
            LDA A,X
            STA PCRAM
            LDA #5
            LDX #COLSH1
            JMP SLEEP
COLSH1      CLR PCRAM
            NAP (3,COLSHK)
SHKTAB      FCB $7,$F,$17,$1E,$25,$2C,$2B,$2A
            FCB $28,$60,$D0,$C0,$C3,$85,$47,$87

;;; OUTPUT STARGATE
;;; SUPER PROCESS
;;; PD+8=TABLE X,PD+40=RET ADD,PD10=SADDR
;;; A=COLS/DELAY;X=SADD;B=DELAY/SLEEP
OTSGM       STX proc.data+10,U                     ; START ADDR
            STA proc.d1,U                       ; COLS/DELAY
            STB proc.data+13,U
            PULS X
            STX proc.data+14,U                     ; RET ADDR
            LDX #SGMTAB
            STX proc.data+8,U
            CLR proc.data+12,U
OTSGM1      LDX proc.data+8,U
            LDY ,X++                        ; LETTER ADDR
            BEQ OTSGMX                      ; THE END
            LDA ,X+
            STD proc.d2,U                       ; COLOR
            LDA ,X+
            STA proc.data,U                        ; FLAVOR MASK
            LDD ,X++                        ; SCREEN ADDR OFFSET
            ADDD proc.data+10,U
            STX proc.data+8,U
            TFR D,X
            JMP OUTLET
OTSGMX      JMP [proc.data+14,U]                   ; RETURN

;;; OUTPUT LETTER
;;; X=SCREEN ADDR TOP LEFT
;;; Y=LETTER DATA TABLE
;;; PD=FLAVOR MASKL;PD1=DELAY;PD3=REPCNT
;;; PD12=DELAY TEMP
OUTLET      CLR proc.d3,U
            STX proc.d4,U
OUTL0       LDA ,Y
            BEQ OUTLX                       ; END
            BPL OUTL1
            BITA #$40
            BNE OUTL1
            ANDA #$3F
            STA proc.d3,U
            LEAY 1,Y
            BRA OUTL0
OUTL1       PSHS Y
            JSR OUTCOL
            LDA proc.d3,U
            BEQ OUTL2
            DEC proc.d3,U
            BEQ OUTL2
            LDY ,S
OUTL2       LEAS 2,S
            COM proc.data,U
            BPL OUTL2A
            INC proc.d4,U
OUTL2A      STY proc.d6,U
            DEC proc.data+12,U
            BPL OUTL3
            LDA proc.d1,U
            STA proc.data+12,U
            LDX #OUTL3
            LDA proc.data+13,U
            JMP SLEEP
OUTL3       LDX proc.d4,U
            LDY proc.d6,U
            BRA OUTL0
OUTLX       JMP OTSGM1

;;; OUTPUT COLUMN
;;; X=ADDR TOP LEFT
;;; Y=DATA ADDR
;;; PD,U=FLAVOR MASK ;PD2,U=COLOR
OUTCOL      PSHS D
OUTC1       LDA ,Y+
            PSHS A                          ; END OF COLUMN FLAG
            BITA #$40
            BNE OUTC2
            ANDA #$3F
            LEAX A,X
            BRA OUTCX
OUTC2       ANDA #$3F
            BEQ OUTCX
OUTC3       LDB proc.data,U
            ANDB proc.d2,U
            ORCC #$10                       ; SEI
            CLR RWCNTL                      ; ENABLE RAM READ
            ORB ,X
            STB ,X+
            LDB #1
            STB RWCNTL
            ANDCC #$EF                      ; (CLI)
            DECA
            BNE OUTC3
OUTCX       LDA ,S+
            BPL OUTC1
            PULS D,PC

;;;  HALO ROUTINE---THIS ROUTINE PUTS OUT A HALO AROUND THE EDGES OF
;;;  ALL SOLID REGIONS IN A DEFINED AREA.
;;;
;;;  X = UPPER LEFT CORNER OF REGION
;;;  Y = LOWER RIGHT CORNER (JUST OUTSIDE OF REGION)
;;;  A = COLOR OF HALO
HALO        STY XTEMP                       ; SAVE BOTTOM RIGHT IN MEMORY
            PULS Y                          ; GET RETURN ADDRESS
            LDU CRPROC
            STX proc.data,U                        ; SAVE UPPER LEFT
            STY proc.data+6,U                      ; RETURN ADDRESS
            CLRB                            ; START ON EVEN FLAVOR
HALO0       BSR VFILL                       ; FILL A VERTICAL LINE
            TSTB                            ; MOVE TO NEXT FLAVOR
            BEQ HALO1                       ; EVEN FLAVOR..MOVE TO ODD
            LDU CRPROC
            STD proc.data+2,U
            STX proc.data+4,U
            NAP (1,HALO50)
HALO50      LDD proc.data+2,U
            LDX proc.data+4,U
            LEAX $100,X                     ; ODD FLAVOR MOVE TO NEXT BYTE
            LDB #$FF                        ; SETUP FOR EVEN FLAVOR
HALO1       INCB                            ; TO NEXT FLAVOR
            EXG X,D                         ; SEE IF DUN
            CMPA XTEMP                      ; DONE IF X BEYOND OR EQUAL TO PASSED Y
            EXG X,D
            BLO HALO0                       ; WE STILL IN RANGE...DO ANOTHER COLUMN

            LDU CRPROC
            LDX proc.data,U                        ; RESTORE ORIGINAL CURSOR
HALO2       LDA #$4B                        ; HALF A LINE DOWN
            STA XTEMP
            JSR HFILL                       ; DO A HORIZONTAL ROW
            STX proc.data+4,U
            NAP (1,HAL_55)
HAL_55      LDA #$8C
            STA XTEMP
            LDB proc.data+5,U
            LDA #$4B
            TFR D,X
            JSR HFILL
            NAP (1,HALO55)
HALO55      LDX proc.data+4,U
            LEAX 1,X                        ; MOVE TO NEXT ROW
            EXG X,D
            LDA #$0E
            CMPB XTEMP+1                    ; TOO MUCH Y?
            EXG X,D
            BLO HALO2                       ; NOT THIS PASS
            LDU CRPROC
            JMP [proc.data+6,U]                    ; RETURN
VFILL       PSHS X,D                        ; SAVE POINTER, COLOR, FLAVOR
VFILL1      BSR WCHK                        ; SEE IF THERE IS WHITE OR BLACK
            BNE VFILL2                      ; ITS WHITE....BOUNDARY FOUND
            BSR VADV                        ; DO AN ADVANCE TO NEXT PIXEL
            BRA VFILL1
VFILL2      BSR VON                         ; TURN ON THE CURRENT PIXEL
            BSR VADV                        ; PUSH TO NEXT ONE
            BSR VON                         ; TURN IT ON TOO
VFILL3      BSR WCHK                        ; LOOK FOR BLACK NOW...WE IN A WHITE REGION
            BEQ VFILL4                      ; FOUND SOME BLACK
            BSR VADV                        ; NEXT PIX
            BRA VFILL3
VFILL4      LEAX -2,X                       ; MOVE BACK 2 TO WHITEN
            BSR VON                         ; TURN EM ON
            LEAX 1,X
            BSR VON
            LEAX 1,X                        ; BACK TO BLACK PIX THAT TRIGGERED IT
            BRA VFILL1
WCHK        PSHS D                          ; SEE IF CURRENT POINTED TO IS ON OR OFF
            ORCC #$10                       ; (SEI) WE HAVE TO READ THE SCREEN
            CLR RWCNTL
            BSR GETMSK                      ; GET THE RIGHT MASK
            ANDA ,X
            LDB #1
            STB RWCNTL                      ; UNCONTROL
            ANDCC #$EF                      ; (CLI)
            TSTA                            ; SET FLAGS FOR RETURN
            PULS D,PC
GETMSK      LDA #$F0                        ; ASSUME EVEN
            TSTB
            BEQ GETMX                       ; YEP
            LDA #$0F                        ; ODD
GETMX       RTS
VADV        LEAX 1,X                        ; PUSH TO NEXT PIXEL
            EXG X,D                         ; COMPARE
            CMPB XTEMP+1                    ; IS OUR Y ENOUGH??
            EXG X,D
            BLO GETMX                       ; DONE..IN RANGE...RETURN
HADV2       LEAS 2,S                        ; REMOVE CALL TO US
            PULS X,D,PC                     ; AND RETURN FOR VFILL
VON         PSHS D                          ; SAVE
            ORCC #$10                       ; (SEI) READ SCREEN
            CLR RWCNTL
            BSR GETMSK                      ; GET THE CURRENT MASK
            ANDA ,X                         ; READ THE SCREEN
            BEQ VBYE                        ; NOTHING THERE....DONT SHADE.
            BSR GETMSK                      ; GET MASK BACK
            ANDA #$99                       ; MASK COLOR ON
            PSHS A                          ; PUT THAT HALF OF BYTE UP
            BSR GETMSK                      ; ANOTHER MASK PLEASE
            COMA                            ; OTHER HALF
            ANDA ,X                         ; GET WHATS ON SCREEN.
            ORA ,S+                         ; OR IN OTHER HALF
            STA ,X                          ; AND STORE
VBYE        LDA #1
            STA RWCNTL
            ANDCC #$EF                      ; (CLI)
            PULS D,PC
HFILL       PSHS X,D
            CLRB                            ; START ON EVEN FLAVOR
HFILL1      BSR WCHK
            BNE HFILL2                      ; STILL LOOKING FOR WHITE
            BSR HADV                        ; MOVE TO NEXT PIX
            BRA HFILL1
HFILL2      BSR VON                         ; TURN ONE ON
            BSR HADV                        ; MOVE TO NEXT
            BSR VON                         ; TURN ONE ON
HFILL3      BSR WCHK
            BEQ HFILL4
            BSR HADV
            BRA HFILL3
HFILL4      LEAX -$100,X                    ; MOVE BACK 2 PIXELS
            BSR VON
            BSR HADV                        ; MOVE 1 PIX FWD
            BSR VON
            BSR HADV
            BRA HFILL1
HADV        TSTB
            BEQ HADV1
            LEAX $100,X                     ; TO NEXT BYTE
            EXG X,D
            CMPA XTEMP                      ; SEE IF TOO MUCH
            EXG X,D
            BHS HADV2
            LDB #$FF
HADV1       INCB
            RTS


;;; STARGATE MESSAGE TABLE
;;; LETTER,COLOR,DELAY,FLAVOR,X16
SGMTAB
; LETTER S
        FDB SGS
        FCB $11,$F0
        FDB $0000
; LETTER T
        FDB SGT
        FCB $22,$F0
        FDB $1000
; LETTER A
        FDB SGA
        FCB $33,$F0
        FDB $1F00
; LETTER R
        FDB SGR
        FCB $44,$F0
        FDB $2F00
; LETTER G
        FDB SGG
        FCB $55,$F0
        FDB $3F00
; LETTER A
        FDB SGA
        FCB $66,$F0
        FDB $4F00
; LETTER T
        FDB SGT
        FCB $77,$F0
        FDB $5F00
; LETTER E
        FDB SGE
        FCB $88,$F0
        FDB $6F00
        FDB 0                           ; END

;;; STARGATE LETTER DATA
SGS     FCB 6,$44,$9,$D0
        FCB 4,$48,7,$D0
        FCB 3,$4A,$6,$D0
        FCB 2,$4C,$5,$D0
        FCB $82                         ; REPEAT TWICE
        FCB 1,$4E,$4,$D0
        FCB $8D                         ; REPEAT 13
        FCB $50,$3,$D0
        FCB $82                         ; REPEAT TWICE
        FCB $50,4,$CE
        FCB $50,5,$CC
        FCB $50,6,$CA
        FCB $50,7,$C8
        FCB $50,9,$C4
        FCB 0                           ; END OF LETTER
; TTTTTTTTTTTTTTTTTTTTTTTT
SGT     FCB $87                         ; REPEAT 7
        FCB $CA
        FCB $8A                         ; REPEAT 10
        FCB $4A,$2,$D7
        FCB $87
        FCB $CA
        FCB 0
; LETTER A
SGA     FCB 15,$40+2,2,$C0+16
        FCB 13,$40+4,2,$C0+16
        FCB 11,$40+6,2,$C0+16
        FCB 9,$40+8,2,$C0+16
        FCB 7,$40+10,2,$C0+16
        FCB 5,$40+12,2,$C0+16
        FCB 3,$40+14,2,$C0+16
        FCB 1,$40+16,2,$C0+16
        FCB $80+2                       ; REPEAT 2 TIMES
        FCB $40+17,2,$C0+16
        FCB $80+5                       ; REPEAT 5 TIMES
        FCB $40+12,7,$C0+5
        FCB $80+2                       ; REPEAT 2 TIMES
        FCB $40+17,2,$C0+16
        FCB 1,$40+16,2,$C0+16
        FCB 3,$40+14,2,$C0+16
        FCB 5,$40+12,2,$C0+16
        FCB 7,$40+10,2,$C0+16
        FCB 9,$40+8,2,$C0+16
        FCB 11,$40+6,2,$C0+16
        FCB 13,$40+4,2,$C0+16
        FCB 15,$40+2,2,$C0+16
        FCB 0                           ; END OF LETTER A
;  START OF LETTER R
SGR         FCB $80+10                      ; REPEAT 10 TIMES
            FCB $C0+35
            FCB $C0+0                       ; BLANK COLUMN
            FCB $C0+0                       ; BLANK COLUMN
            FCB $80+4                       ; REPEAT 4 TIMES
            FCB $40+22,2,$C0+11
            FCB $80+2                       ; REPEAT 2 TIMES
            FCB 1,$40+21,2,$C0+11
            FCB $80+2                       ; REPEAT 2 TIMES
            FCB 2,$40+20,2,$C0+11
            FCB 3,$40+19,4,$C0+9
            FCB 4,$40+17,7,$C0+7
            FCB 5,$40+15,10,$C0+5
            FCB 7,$40+11,14,$C0+3
            FCB 9,$40+7,18,$C0+1
            FCB 0                           ; END OF LETTER R
;  START OF LETTER G
SGG         FCB 12,$C0+11
            FCB 10,$C0+15
            FCB 8,$C0+19
            FCB 7,$C0+21
            FCB 5,$C0+24
            FCB 4,$C0+26
            FCB 3,$C0+28
            FCB 2,$C0+30
            FCB 2,$C0+31
            FCB 1,$40+15,3,$C0+14
            FCB 1,$40+13,7,$C0+13
            FCB $40+12,11,$C0+12
            FCB $80+2                       ; REPEAT 2 TIMES
            FCB $40+11,13,$C0+11
            FCB $80+2                       ; REPEAT 2 TIMES
            FCB $40+10,15,$C0+10
            FCB $80+9                       ; REPEAT 9 TIMES
            FCB $40+10,2,$C0+23
            FCB 0                           ; END OF LETTER G
;  START OF LETTER E
SGE         FCB $80+9                       ; REPEAT 9 TIMES
            FCB $40+10,2,$C0+23
            FCB $80+15                      ; REPEAT 15 TIMES
            FCB $40+10,2,$40+10,3,$C0+10
            FCB 0                           ; END OF LETTER E

