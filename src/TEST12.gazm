;  NLIST
;  LIST
;  TTL    <<<<<  S T A R G A T E  >>>>>

        SETDP RAM>>8

        org RAMALS                      ;  USE SAMS RAM

SW0ST   RMB    1
SW0SCN  RMB    1
SW2ST   RMB    1
SW2SCN  RMB    1
SW3ST   RMB    1
SW3SCN  RMB    1
SW2STI  RMB    1
SW2SCI  RMB    1
SW3STI  RMB    1
SW3SCI  RMB    1

            org TSTORG
AUD             jmp    AUD_
AUD1            jmp    AUD1_
PWRUPV          jmp    PWRUP
ADVSW           jmp    ADVSW_
AUTOCY          jmp    AUTCYC

;;;  AUDIT: COUNT=A, COUNTER=B (1-7)
AUD1_
                pshs   D,X                   ;  SAVE STATE
                lda    #$01                  ;  BUMP BY COUNT OF 1
                bra    AUD2

AUD_            pshs   D,X                   ;  SAVE STATE

AUD2            andb   #$0F                  ;  LIMIT TO 0-F
                aslb                        ;  MAKE    INTO 4X
                pshs   B
                aslb
                addb   ,S+                    ;  6X
                ldx    #CMOS-4+$100            ;  POINT TO START-6    (CREDITS TAKES UP 2)
                abx                         ;  GET THE CORRECT DESTINATION
                jsr    RCMOSB
                pshs   B
                jsr    RCMOSB
                pshs   B
                jsr    RCMOSB
                pshs   B
                adda   ,S
                daa
                sta    ,S
                lda    1,S
                adca   #0
                daa
                sta    1,S
                lda    2,S
                adca   #0
                daa
                leax   -6,X
                jsr    WCMOSA
                puls   B
                puls   A
                jsr    WCMOSD
                puls   A
AUDX            puls   D,X,PC                 ;  CLEAN UP

;;; Game entru point on power up
PWRUP           orcc   #$FF                 ;  NO INTERRUPTS ETC.
                lds    #HSTK                ;  IF AND WHEN WE DECIDE TO USE THE STACK
                clr    PIA0_ctrl
                clr    PIA0
                lda    #$3C                 ;  SET LED 3 TO 1 (WE'RE MAKING AN F TO BLANK)
                sta    PIA0_ctrl
                clr    PIA1_ctrl
                lda    #$C0                 ;  BITS 6,7 TO OUTPUTS (LEDS)
                sta    PIA1
                lda    #$3C                 ;  SET BIT 2 TO 1
                sta    PIA1_ctrl            ;  CLEAR CB2 (LED)
                lda    #$C0                 ;  CLEAR LEDS
                sta    PIA1                 ;  SET BITS 0,1
                lda    #1
                sta    RWCNTL               ;  MAKE SURE WE COPY FROM ROM!
                ldx    #CRTAB
                ldy    #CRAM
!loop           ldd    ,X++
                std    ,Y++
                cmpx   #CRTAB+16
                blo    !loop
                lda    #2
                ldy    #PWRUP1
                ldx    #0
                jmp    RAMTST               ;  DO A RAM TEST.

PWRUP1          ldy    #PWRUP4
                jmp    ROMTST
PWRUP4          lda    #$34                 ;  PUT A ZERO IN THE LED.
                sta    PIA0_ctrl
                sta    PIA1_ctrl
                clr    PIA1
                lda    #RAM>>8
                tfr    A,DP
                lds    #HSTK
                jsr    SCRCLR
                lda    #OKM
                jsr    WRD7V                ;  INDICATE OK
                ldy    #INITV               ;  DELAY TO JUMP INTO GAME.
                lda    #7
                jmp    DELA1

;
;     ADVANCE SWITCH
;

ADVSW_          lda    INDIAG               ;  ALREADY HERE??
                bne    SUCDV
                NAP    (2,ADVSW2)                               ;  MAKE SURE THAT ITS NOT NOISE
ADVSW2          lda    PIA0                 ;  IS IT STILL PRESSED??
                bita   #$02                 ;  WELL??
                bne    ADVSW3               ;  YEP
SUCDV           jmp    SUCIDE               ;  NOPE...DIE
ADVSW3          jsr    GNCIDE               ;  CAN EVERYBODY
                jsr    P1SW                 ;  MAKE SURE PROPER DIRECTION FACED
                lda    #$FF
                sta    INDIAG
                sta    STATUS               ;  KEEP EM ALL OUT
                jsr    INITCM               ;  INITIALIZE COLOR MATRIX
                lda    PIA0                 ;  CHECK WHICH TYPE OF TEST
                rora                        ;  AUTO UP??
                lbcs   BOOK                ;  YEP...GO TO BOOKKEEPING
                jsr    SCRCLR
                orcc   #$BF                ;  FIRQ ENABLED INDICATES NORMAL AGNOSTICS RUNNING.
                ldy    #DAG999
                jmp    BLKLED               ;  BLANK THE LEDS

DAG999          lda    #WDATA
                sta    WDOG
                lda    PIA0                 ;  WAIT FOR ADVANCE SWITCH LET GO
                bita   #2
                bne    DAG999
                ldy    #DIAG0
                jmp    ROMTST               ;  DO THE ROM TEST

DIAG0           lda    #RAM>>8
                tfr    A,DP
                jsr    SCRCLR               ;  CLEAR FOR ROMS MESSAGE.
                lda    #ROMOK
                jsr    WRD7V                ;  PRINT THE MESSAGE
                ldb    #3
DIG111          ldx    #$7000
DIAG1           lda    #WDATA
                sta    WDOG
                lda    PIA0
                bita   #2
                bne    DIAG2                ;  CONTINUE IF ADVANCE PRESSED.
                leax   -1,X
                cmpx   #0
                bne    DIAG1
                decb
                bne    DIG111
DIG11_          ldy    #DIAG2
                ldx    #0
                lda    #$FF                 ;  INDICATE FRONT DOOR
                jmp    RAMTST               ;   DO THE RAM TEST.
DIAG2           lda    #1
                sta    RWCNTL
                lda    #RAM>>8
                tfr    A,DP
                jsr    SCRCLR
                lda    #RAMOK
                jsr    WRD7V                ;  PRINT RAM OK MESSAGE
DIAG22          lda    #WDATA
                sta    WDOG
                lda    PIA0
                bita   #2
                bne    DIAG22               ;  HOLD CONTROL UNTIL LET GO.
RAMERJ          ldx    #$9C00               ;  RAM ERROR MESSAGE COMES HERE.
DIAG3           clr    ,X+                  ;  CLR USABLE MEMORY
                ldb    #WDATA
                stb    WDOG
                cmpx   #HSTK+1
                bne    DIAG3
                ldd    #$A55A
                std    HSEED
                sta    INDIAG               ;  INDICATE THAT WE'RE AGNOSTICS
                jsr    PINIT                ;  TIME TO LET THE SYSTEM TAKE OVER
                jsr    INT20V
                jsr    P1SW
                com    STATUS
                MAKP    ( DIAG4 )                          ;  START A PROCESS TO CONTINUE
                bsr    ZLED                 ;  ZERO IN LED
                andcc  #0                 ;  LET THE INTERRUPTS IN
                jmp    EXEC                 ;  GO!
DIAG4           jsr    AVWAIT               ;  WAIT FOR ADVANCE SWITCH.
                jsr    CMTEST               ;  TEST CMOS.
                andcc  #1                 ;  INTERRUPTS BACK ON    LEAVE CARRY
                lda    #CMOK
                bcc    DIAG66
                ldb    #$2F                 ;  INDICATE THAT ITS IN R/W HALF
                cmpx   #CMOS+$100
                bhi    DIAG5                ;  IT WAS IN WRITABLE HALF
                ldb    #$1F
DIAG5           orcc   #$10                ;  LOCK OUT THE INTERRUPTS
                lds    #DIAG55
                lda    #$3
                jmp    PULSE
DIAG55          lds    #HSTK
                bsr    ZLED
                lda    #RAM>>8
                tfr    A,DP
                andcc  #$EF               ;  LET THE INTERRUPTS BACK
                lda    #CMERR
                cmpb   #$1F
                bhi    DIAG66
                lda    #CMDOOR
DIAG66          jsr    SCRCLR
                jsr    WRD7V                ;  PRINT ONE OF THE TWO MESSAGES.
                ldu    CRPROC
                jsr    AVWAIT
                clr    proc.data  +2,U      ;  INDICATE FRONT DOOR TEST CALLING
                jsr    SNDSRT               ;  START THE SOUND TEST
DIAG6           jsr    SNDCYC               ;  CYCLE ONE SOUND
                jsr    AVCHK                ;  CHECK ADVANCE SWITCH.
                bcc    DIAG6                ;  NOT PRESSED, DO ANOTHER SOUND
                lda    #$3F
                sta    SOUND
                NAP    ( 1,DIAG77 )
DIAG77          lda    #$C                  ;  SILENCE
                sta    SOUND
                jsr    AVWAIT               ;  NOW WAIT FOR RELEASE.
                jsr    SWTEST               ;  DO A SWITCH TEST.
                jsr    AVWAIT               ;  WAIT FOR RELESE
                jsr    CRTEST               ;  DO THE COLOR MATRIX TEST
                jsr    AVCHK                ;  HOLDING BUTTON??
                bcc    DIAG78               ;  NOPE...CONTINUE
                jsr    AVWAIT               ;  WAIT FOR LET GO TIME.
DIAG78          jmp    TSTPAT               ;  DO SOME TEST PATTERNS    THEN GO TO BOOK.

ZLED            clr    PIA1                 ;  ZERO IN LED INDICATES ON LAST TEST OR DUN
                lda    #$34
                sta    PIA0_ctrl
                inca
                sta    PIA1_ctrl
                rts

INITCM          ldx    #CRTAB
                ldy    #PCRAM
ADVS90          ldd    ,X++
                std    ,Y++
                cmpx   #CRTAB+16
                blo    ADVS90
                rts
;
;       AUTO CYCLE
;

AUTCYC          lda    #$3F                 ;  MAKE LOOK LIKE FRONT DOOR TEST THANX TO MOTOROLA
                tfr    A,CC                 ;  SET TO AUTOCYCLE NO INTS PLEASE ETC.
                lda    #$8F                 ;  FIFTEEN RAM PASSES, IN AUTOCYCLE
                ldx    RAMALS+$600          ;  TAKE A BYTE FROM THE RAM TEST
                leax   $1234,X             ;  ADD A STUPID NUMBER TO VARY TEST
                ldy    #AUTO1
                jmp    RAMTST               ;  TEST THE RAM
AUTO1           ldy    #AUTO2
                jmp    ROMTST
AUTO2           lda    #RAM>>8
                tfr    A,DP
                lds    #HSTK                ;  JUST IN CASE
                jsr    CMTEST
                bcc    AUTO4
                lda    #CMDOOR
                cmpx   #CMOS+$100          ;  WRITE PROTECT??
                bls    AUTO8
                lda    #CMERR
AUTO8           jsr    SCRCLR
                jsr    WRD7V                ;  PRINT THE ERROR
AUTO5           lda    #WDATA
                sta    WDOG
                bra    AUTO5
AUTO4           jsr    BARS
                ldy    #AUTCYC
                lda    #4
                jmp    DELA1

;
;     TEST PATTERNS
;

TSTPAT          jsr    CROSS                ;  DO FANCY HASSLER PAT.
                jsr    AVWAIT               ;  WAIT FOR ADVANCE
                jsr    SCRCLR
                lda    #$7                  ;  RED
                sta    PCRAM
                jsr    AVWAIT
                lda    #$38                 ;  GREEN
                sta    PCRAM
                jsr    AVWAIT
                lda    #$C0
                sta    PCRAM                ;  BLUE
                jsr    AVWAIT
                jsr    BARS
                jsr    AVWAIT
                jmp    BOOK

BARS            ldx    #PCRAM
                ldy    #CBARCL
                ldu    #CRAM
BARS0           ldd    ,Y++                 ;  GET A COLOR
                std    ,X++
                std    ,U++                 ;  IN CASE INTERRUPTS OFF
                lda    #WDATA
                sta    WDOG
                cmpx   #PCRAM+16
                blo    BARS0
                ldd    #0
                ldx    #0
COLOR1          stx    XTEMP
                leax   $F00,X
COLOR2          std    ,--X
                pshs   A
                lda    #WDATA
                sta    WDOG
                puls   A
                cmpx   XTEMP
                bne    COLOR2
                leax   $900,X
                tsta
                bne    COLOR3
                ldx    #$D00
COLOR3          addd   #$1111
                bcc    COLOR1
                rts

CBARCL  FCB    $5,$5,$28,$28,$80,$80,$0,$0
        FCB    $AD,$AD,$2D,$2D,$A8,$A8,$85,$85

;
;  DON HASSLER CROSSHATCH
;
;     RAM0 = BLACK - 00
;     RAM1 = WHITE - FF
;     RAM2 = BLUE - C0
;     RAM3 = GREEN - 38
;     RAM4 = RED - 07
;

CROSS           jsr    SCRCLR               ;  CLEAR SCREEN
                clra
                jsr    SETPCR               ;  ZERO COLOR MATRIX
                lda    #$FF
                sta    PCRAM+1
                lda    #$C0
                sta    PCRAM+2
                lda    #$38
                sta    PCRAM+3
                lda    #$7
                sta    PCRAM+4
                ldy    #WVERT               ;  MOVE WHITE VERTICALS
                ldd    #$0101
CROSS4          ldx    0,Y                  ;  GET START PTR
CROSS5          std    ,X++                 ;  MOVE IN WHITE
                cmpx   2,Y                 ;  CK IF LINE DONE
                bne    CROSS5               ;  NO, KEEP GOING
                leay   4,Y                 ;  INCR Y BY 4
                cmpy   #WVERTX             ;  CK IF ALL DONE
                bne    CROSS4               ;  NO, DO NEXT LINE
                lda    #$11                 ;  MOVE WHITE HORIZ
                ldy    #WHORZ
CROSS2          ldx    0,Y                  ;  GET START PTR
                stx    XTEMP
CROSS3          sta    0,X                  ;  MOVE IN WHITE
                inc    XTEMP                ;  UPDATE PTR
                ldx    XTEMP
                cmpx   2,Y                 ;  CK IF DONE
                bne    CROSS3               ;  NO, KEEP GOING
                leay   4,Y                 ;  INCR Y BY 4
                cmpy   #WHORZX             ;  CK IF ALL DONE
                bne    CROSS2               ;  NO, DO NEXT LINE
                ldy    #HORIZ               ;  MOVE IN COLOR HORIZ.
CROSS6          ldx    0,Y                  ;  GET START PTR
                stx    XTEMP
                lda    4,Y                  ;  GET COLOR
CROSS7          sta    0,X                  ;  MOVE IN COLOR
                inc    XTEMP
                ldx    XTEMP
                cmpx   2,Y                 ;  CK IF LINE DONE
                bne    CROSS7               ;  NO, KEEP GOING
                leay   5,Y                 ;  INCR Y BY 5
                cmpy   #HORIZX             ;  CK IF ALL DONE
                bne    CROSS6               ;  NO, DO NEXT LINE
                ldy    #VERT                ;  MOVE IN COLOR VERTICAL
CROSS8          ldx    0,Y                  ;  GET START PTR
                lda    4,Y                  ;  GET COLOR
CROSS9          sta    ,X+                  ;  MOVE IN COLOR
                cmpx   2,Y                 ;  CK IF LINE DONE
                bne    CROSS9               ;  NO, KEEP GOING
                leay   5,Y                 ;  INCR Y BY 5
                cmpy   #VERTX              ;  CK IF ALL DONE
                bne    CROSS8               ;  NO, DO NEXT LINE
                lda    #$21                 ;  BLUE TOUCH UP
                sta    $437E
                lda    #$20
                sta    $937E
                ldx    #$4B0A
CROSSA          orcc   #$10                ;  SEI
                clr    RWCNTL
                lda    0,X
                ldb    #1
                stb    RWCNTL
                andcc  #$EF               ;  CLI
                anda   #$F0
                ora    #$02
                sta    ,X+
                cmpx   #$4B6D
                bne    CROSSA
                ldx    #$4B90
CROSSB          orcc   #$10                ;  SEI
                clr    RWCNTL
                lda    0,X
                ldb    #1
                stb    RWCNTL
                andcc  #$EF               ;  CLI
                anda   #$F0
                ora    #$02
                sta    ,X+
                cmpx   #$4BF3
                bne    CROSSB
                ldx    #$0B18
                stx    XTEMP                ;  MOVE IN DOTS
CROSSC          ldx    XTEMP
                lda    0,X                  ;  OR IN DOT
                anda   #$F0
                ora    #$01
                sta    0,X                  ;  STORE DOT
                ldb    XTEMP+1              ;  UPDATE VERTICAL PTR
                addb   #$22
                bcs    CROSSD
                stb    XTEMP+1
                bra    CROSSC
CROSSD          ldb    #$18
                stb    XTEMP+1              ;  RE-INIT VERT. PTR
                ldb    XTEMP
                addb   #$10                ;  UPDATE HORIZ PTR
                stb    XTEMP
                cmpb   #$9B                ;  CK IF DONE
                bne    CROSSC               ;  NO, KEEP GOING
                rts
;
;  CROSS HATCH DATA
;
WHORZ   FDB    $0407
        FDB    $9407
        FDB    $0429
        FDB    $9429
        FDB    $044B
        FDB    $944B
        FDB    $046D
        FDB    $946D
        FDB    $048F
        FDB    $948F
        FDB    $04B1
        FDB    $94B1
        FDB    $04D3
        FDB    $94D3
        FDB    $04F5
        FDB    $94F5
WHORZX

WVERT   FDB    $0307
        FDB    $03F5
        FDB    $1307
        FDB    $13F5
        FDB    $2307
        FDB    $23F5
        FDB    $3307
        FDB    $33F5
        FDB    $4307
        FDB    $43F5
        FDB    $5307
        FDB    $53F5
        FDB    $6307
        FDB    $63F5
        FDB    $7307
        FDB    $73F5
        FDB    $8307
        FDB    $83F5
        FDB    $9307
        FDB    $93F5
WVERTX
HORIZ   FDB    $4505
        FDB    $5205
        FCB    $44
        FDB    $4506
        FDB    $5206
        FCB    $44
        FDB    $4507
        FDB    $5207
        FCB    $00
        FDB    $4508
        FDB    $5208
        FCB    $33
        FDB    $4509
        FDB    $5209
        FCB    $33
        FDB    $45F3
        FDB    $52F3
        FCB    $33
        FDB    $45F4
        FDB    $52F4
        FCB    $33
        FDB    $45F5
        FDB    $52F5
        FCB    $00
        FDB    $45F6
        FDB    $52F6
        FCB    $44
        FDB    $45F7
        FDB    $52F7
        FCB    $44
        FDB    $047E
        FDB    $437E
        FCB    $22
        FDB    $547E
        FDB    $937E
        FCB    $22
HORIZX

VERT    FDB    $026F
        FDB    $028E
        FCB    $04
        FDB    $036F
        FDB    $038E
        FCB    $30
        FDB    $936F
        FDB    $938E
        FCB    $00
        FDB    $946F
        FDB    $948E
        FCB    $34
VERTX

;
;     COLOR    MATRIX    TEST
;

CRTEST          puls   D                   ;  GET RETURN
                ldu    CRPROC
                std    proc.data  +6,U      ;  SAVE
                jsr    SCRCLR               ;  CLEAR OUT BABY
                lda    #CMTSTM              ;  PRINT THE MESSAGE
                jsr    WRD7V
                lda    #$80
                sta    proc.data  ,U
CRTST1
                NAP    ( 1,CRTST2 )
CRTST2          jsr    AVCHK                ;  PRESS??
                bcs    CMTBYE               ;  NOPE
                dec    proc.data  ,U        ;  DONE WITH DELAY??
                bne    CRTST1
                lda    COLRMT               ;  GET FIRST COLOR.
                bsr    SETPCR               ;  STORE IT DOWN
                bsr    RAMBAR               ;  SET UP MEMORY.
CRTT__          ldx    #COLRMT              ;  TABLE TO STORE
CRTST3          lda    ,X+                  ;  GET A COLOR
                ldu    CRPROC
                stx    proc.data  +2,U
                bsr    SETPCR               ;  STORE IT THROUGH
                lda    #$80
                sta    proc.data  ,U
CRTST4
                NAP(1, CRTST5)

CRTST5          jsr    AVCHK                ;  ADVANCE??
                bcs    CMTBYE               ;  YEP
                dec    proc.data  ,U
                bne    CRTST4
                ldx    proc.data+2,U        ;  RESTORE POINTER
                cmpx   #COLRMT+8
                blo    CRTST3
                bra    CRTT__               ;  CONTINUE THROUGH
CMTBYE          ldu    CRPROC
                jmp    [proc.data+6,U]

RAMBAR          ldx    #$0
                ldy    #COLRMD              ;  NUMBERS TO PUT IN BARS
RAMBA1          stx    XTEMP                ;  SAVE OUR X
                leax   $F00,X
                lda    ,Y+
                tfr    A,B
RAMBA2          std    ,--X
                cmpx   XTEMP
                bne    RAMBA2
                leax   $900,X
                tsta
                bne    RAMBA3
                ldx    #$D00
RAMBA3          cmpy   #COLRMD+16
                bne    RAMBA1
                rts

COLRMT  FCB    $02,$03,$04,$10,$18,$20,$40,$80
COLRMD  FCB    $00,$FF,$11,$EE,$22,$DD,$33,$CC
        FCB    $44,$BB,$55,$AA,$66,$99,$77,$88

SETPCR          ldx    #PCRAM
SETPC1          sta    ,X+
                cmpx   #PCRAM+16
                blo    SETPC1
                rts
;
;     SWITCH TEST
;

SWMH    equ 44                          ;  HEIGHT OF FIRST MESSAGE

SWTEST          puls   D                   ;  GET RETURN ADDR
                ldu    CRPROC
                std    proc.data+6,U
                lda    #10
                sta    proc.data+4,U        ;  DELAY COUNTER FOR EXIT
                jsr    SCRCLR               ;  CLEAR IT PLEASE
                lda    #SWTESM              ;  SWITCH TEST MESSAGE.
                jsr    WRD7V
                ldu    #RAMALS
SWTES0          clr    ,U+
                cmpu   #SW3SCI+1
                bls    SWTES0
SWTES2          ldu    #STABP
                bsr    SWSCN0
SWTES1          lda    #$34
                sta    PIA3_ctrl
                bsr    SWSCN0               ;  SCAN THE COCKTAIL SIDE
                lda    #$3C
                sta    PIA3_ctrl
                bsr    SWDISP
                jsr    AVCHK                ;  ADVANCE PRESSED?
                bcc    SWT999               ;  NOPE.
                ldu    CRPROC
                dec    proc.data+4,U
                beq    SWTBYE
SWT999
                NAP    (1,SWTES2)

SWTBYE          ldu    CRPROC
                jmp    [proc.data+6,U]      ;  "RETURN"

SWSCN0          ldx    ,U++                 ;  GET ADDRESS OF PORT
                beq    SWSCN5               ;  DONE
                ldy    ,U++                 ;  GET ADDRESS TO STORE STATE
                lda    ,X                   ;  GET NEW STATE
                eora   ,Y                   ;  SEE WHERE DIFFERENT
                sta    1,Y                  ;  AND SAVE AS DIFFERENCE BYTE.
                bra    SWSCN0
SWSCN5          rts

SWDISP          ldu    #SPHTAB              ;  WE'LL WALK THROUGH SWITCH PHRASE TABLE
                ldy    #RAMALS              ;  WALK THROUGH THE SCANNED STUFF.
SWDIS4          ldb    #1                   ;  WALK FROM LSB
SWDIS0          bitb   1,Y                  ;  SEE IF THIS BIT CHANGED.
                beq    SWDIS2
                bsr    SWACT                ;  TAKE SOME ACTION
SWDIS2          leau   3,U                  ;  MOVE TO NEXT ENTRY
                aslb                        ;  SHIFT ONE DOWN
                bcc    SWDIS0
                leay   2,Y                  ;  TO NEXT BYTE
                cmpy   #SW3SCI              ;  PAST TABLE
                bhi    SWDIS9               ;  YEP..DONE
                lda    PIA3                 ;  COCKTAIL
                bmi    SWDIS4               ;  YEP...FULL SCAN
                cmpy   #SW3SCN              ;  NOPE..DONE WITH NORMAL SWITCHES??
                bls    SWDIS4               ;  NOT DONE
SWDIS9          rts                         ;  RETURN BABY.

SWACT           pshs   B,X                  ;  SAVE THE MASK
                lda    #$3F
                sta    SOUND                ;  BRING SOUND LINES HIGH IN ANTICIPATION
                eorb   ,Y                   ;  INVERT THE BIT
                stb    ,Y                   ;  AND MAKE THAT THE NEW STATE
                ldb    ,S                   ;  RESTORE MASK
                bitb   ,Y                   ;  WHICH WAY DID IT GO
                bne    SWPRNT               ;  ON....PRINT A MESSAGE.
                ldb    2,U                  ;  HEIGHT
                beq    SWNOAC
                lda    #$40                 ;  X
                tfr    D,X                  ;  MAKE IT CURSOR
                ldd    #$3006
                jsr    BLKCLR               ;  CLEAR THE BLOCK
                puls   B,X,PC
SWPRNT          ldb    2,U                  ;  GET HEIGHT
                beq    SWNOAC
                lda    #$40                 ;  CURSOR
                tfr    D,X
                ldb    #$BB
                stb    TEXCOL
                ldd    ,U                   ;  GET MESSAGE NUMBER,B (PLAYER NUMBER)
                jsr    WRD5FV               ;  AND PRINT IT.
                lda    #$17
                sta    SOUND                ;  MAKE A SOUND
SWNOAC          puls   B,X,PC              ;  AND RETURN.

STABP   FDB    PIA0,SW0ST
        FDB    PIA2,SW2ST
        FDB    PIA3,SW3ST
        FDB    0
        FDB    PIA2,SW2STI
        FDB    PIA3,SW3STI
        FDB    0

SPHTAB  FCB    SWMES1,0,SWMH                ;  AUTO UP
        FCB    SWMES1+1,0,SWMH+7                ;  ADVANCE
        FCB    SWMES1+2,0,SWMH+14                ;  RIGHT COIN
        FCB    SWMES1+3,0,SWMH+21                ;  HIGH SCORE RESET
        FCB    SWMES1+4,0,SWMH+28                ;  LEFT COIN
        FCB    SWMES1+5,0,SWMH+35                ;  CENTER COIN
        FCB    SWMES1+6,0,SWMH+42                ;  SLAM SWITCH
        FCB    0,0,0                    ;  UNUSED (SOUND HANDSHAKE)

        FCB    SWMES1+7,1,SWMH+49                ;  FIRE (1)
        FCB    SWMES1+8,1,SWMH+56                ;  THRUST (1)
        FCB    SWMES1+9,1,SWMH+63                ;  SMARTBOMB (1)
        FCB    SWMES1+10,1,SWMH+70                ;  HYPERSPACE (1)
        FCB    SWMES1+11,0,SWMH+77                ;  START 2
        FCB    SWMES1+12,0,SWMH+84                ;  START 1
        FCB    SWMES1+13,1,SWMH+91                ;  REVERSE (1)
        FCB    SWMES1+14,1,SWMH+98                ;  DOWN (1)

        FCB    SWMES1+15,1,SWMH+105                ;  UP (1)
        FCB    SWMES1+16,1,SWMH+112                ;  INVISO (1)
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED

        FCB    SWMES1+7,2,SWMH+119                ;  FIRE (2)
        FCB    SWMES1+8,2,SWMH+126                ;  THRUST (2)
        FCB    SWMES1+9,2,SWMH+133                ;  SMART BOMB(2)
        FCB    SWMES1+10,2,SWMH+140                ;  HYPERSPACE (2)
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED
        FCB    SWMES1+13,2,SWMH+147                ;  REVERSE (2)
        FCB    SWMES1+14,2,SWMH+154                ;  DOWN (2)

        FCB    SWMES1+15,2,SWMH+161                ;  UP (2)
        FCB    SWMES1+16,2,SWMH+168                ;  INVISO (2)
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED
        FCB    0,0,0                    ;  UNUSED

FDRET           ldu    #RAMERJ              ;  RAM ERROR RETURN
                bra    FDROM0

FDROM           ldu    #DIG11_
FDROM0          lds    #HSTK
FDROM5          ldy    #FDROM6
                lda    #1
                jmp    DELA1
FDROM6          lda    PIA0
                bita   #$2
                bne    FDROM5
FDROM1          ldy    #FDROM7
                lda    #1
                jmp    DELA1
FDROM7          lda    PIA0
                bita   #$2
                beq    FDROM1
FDROM2          ldy    #FDROM8
                lda    #1
                jmp    DELA1
FDROM8          lda    PIA0
                bita   #2
                bne    FDROM2
                jmp    ,U

AVWAIT          puls   D
                ldu    CRPROC
                std    proc.data+6,U
AVWAI_          lda    PIA0                 ;  SEE IF ADVANCE PRESSED
                bita   #$2                 ;  WELL??
                bne    AVWAT2
                NAP    (1,AVWAI_)                                                   ;  WELL, WAIT FOR IT.
AVWAT2          clr    PCRAM
                jsr    SCRCLR
                bra    AVWAT5
AVWAT4          lda    PIA0
                bita   #$2                 ;  WAIT FOR IT TO BE RELEASED
                beq    AVWAT3
AVWAT5
                NAP    (2,AVWAT4)
AVWAT3          jmp    [proc.data+6,U]

AVCHK           lda    PIA0
                bita   #$2
                beq    AVCHK1
                orcc   #$01                ;  SEC
                rts
AVCHK1          andcc  #$FE               ;  CLC
                rts

;
;     DISPLAY BOOKKEEPPING
;

BOOK            lda    #$FF
                sta    STATUS
                jsr    INITCM               ;  COLOR MATRIX PROPER.
                jsr    AVCHK
                bcc    BOOK3
                jsr    AVWAIT               ;  WAIT FOR HIM TO LET GO.
BOOK3           lda    #BOOKM
                jsr    WRD7V                ;  PRINT THE BOOKKEEPING MESSAGE
                ldu    #SLOT1
                lda    #AUDMES              ;  FIRST OF THE AUDIT MESSAGES
BOOK0           pshs   A                   ;  SAVE MESSAGE NUMBER
                jsr    WRD7V                ;  PRINT IT.
                exg    U,X
                jsr    RCMOSB               ;  GET THE TOP BYTE
                pshs   B
                jsr    RCMOSD
                tfr    D,Y
                puls   B
                exg    U,X
                tstb
                beq    BOOK1
                lda    #AUDM1
                jsr    WRD7V                ;  FULL SIX DIGITS USED
                lda    #AUDM2
                jsr    WRD7V
                bra    BOOK2
BOOK1           lda    #AUDM3
                jsr    WRD7V
BOOK2           puls   A                   ;  RESTORE A
                inca
                cmpu   #WARPS
                bls    BOOK0                ;  THANK YOU SIR, MAY I HAVE ANOTHER??
                jsr    AVWAIT
                jmp    ADJV                 ;  NOW GO DO ADJUSTMENT MODE

;
;     SOUND TEST
;

SNDSRT          jsr    SCRCLR               ;  CLEAR THE SCREEN
SNDSR1          ldd    #$FE01               ;  SET UP FOR SOUND LINE 1
                std    proc.data,U
                rts

SNDCYC          puls   D
                std    proc.data+6,U
                lda    #$3F
                sta    SOUND
                NAP    (1,SNDC55)
SNDC55          lda    #$C
                sta    SOUND
                NAP    (1,SNDCY6)
SNDCY6          lda    #$3F
                sta    SOUND
                NAP    (1,SNDCY1)
SNDCY1          ldd    proc.data,U
                anda   #$3F
                sta    SOUND
                lda    #SNDTEX
                jsr    WRD7V
                lda    #$40
                sta    proc.data+4,U
SNDCY4
                NAP (1,SNDCY5)
SNDCY5          jsr    AVCHK                ;  ADVANCE???
                bcs    SNDCY3               ;  THEN GO
                dec    proc.data+4,U
                bne    SNDCY4
SNDCY3          lda    proc.data+2,U        ;  SEE WHO SET THIS UP??
                bne    SNDC90               ;  AUTOCY DID....ALWAYS ADVANCE
                lda    PIA0                 ;  CHECK MANUAL/AUTO
                rora
                bcc    SNDC91               ;  MANUAL...DON'T MOVE ON.
SNDC90          ldd    proc.data,U
                orcc   #$01                ;  SEC
                rola                        ;  MOVE TO NEXT LINE
                incb
                cmpb   #6                  ;  UP TO LINE 6??
                blo    SNDCY2
                bsr    SNDSR1
SNDCY2          std    proc.data,U
SNDC91          jmp    [proc.data+6,U]

;;; CMOS RAM TEST
CMTEST          ldx    #CMOS
                ldy    #RAMALS              ;  USE SAMRAM
!loop           lda    ,X+                  ;  DON'T DO DOUBLES BECAUSE OF HARDWARE BRAIN DAM.
                sta    ,Y+
                cmpx   #CMOS+$400          ;  DONE??
                bne    !loop

                ldb    #6                   ;  ENOUGH ITERATIONS TO ASSURE
                orcc   #$3F                 ;  STOP INTERRUPTS
!outer          ldu    HSEED
                ldy    SEED
                ldx    #CMOS
!loop3          jsr    RAND
                sta    ,X+
                lda    #WDATA
                sta    WDOG
                cmpx   #CMOS+$400           ;  DONE??
                bne    !loop3
                sty    SEED                 ;  RESTORE SEED
                stu    HSEED
                ldx    #CMOS
!loop4          jsr    RAND
                eora   ,X+
                anda   #$F
                bne    !CMEROR
                lda    #WDATA
                sta    WDOG
                cmpx   #CMOS+$400
                bne    !loop4
                decb                        ;  ANOTHER PASS...DONE??
                bne    !outer
                bsr    !RAMBAK
                andcc  #$FE                 ;  CLC CLEAR IT FOR REAL
                rts                         ;  AND RETURN

!RAMBAK         ldu    #RAMALS
                ldy    #CMOS
!rbloop         lda    ,U+
                sta    ,Y+
                cmpy   #CMOS+$400
                bne    !rbloop
                rts

!CMEROR         bsr    !RAMBAK
                orcc   #$01                 ;  SEC
                rts

;;;     RAM TEST....Y = PLACE TO RETURN IF NO ERROR
;;;     X = SEED TO START WITH
;;;     A = ITERATIONS TO MAKE
RAMTST          orcc   #$3F                ;  NO INTERRUPTS DURING TEST
                clr    RWCNTL               ;  SET TO RAM READ
                tfr    A,DP                 ;  COUNT AT DP.
                tfr    X,D                  ;  START WITH A PASSED SEED.
RAM2            tfr    D,U                  ;  SAVE THE SEED
RAM0            ldx    #0                   ;  MEMORY POINTER
RAM3            comb                        ;  DONT ASK
                bitb   #9
                bne    RAM4
                comb
                rora
                rorb
                bra    RAM6
RAM4            comb
                bitb   #9
                bne    RAM5
                rora
                rorb
                bra    RAM6
RAM5            lsra
                rorb
RAM6            std    ,X++
                exg    X,D                  ;  SINCE IRQ RUNS OUT OF RAM, STROKE ROVER
                tstb
                bne    RAM6B                ;  EVERY 256!
                ldb    #WDATA
                stb    WDOG                 ;  HAVE A BONE..EVER BEEN BONED.....
                tfr    DP,B                 ;  CHECK IS NORMAL DIAGS
                cmpb   #$FF                ;  FF MEANS FRONT DOOR
                bne    RAM6C
                ldb    PIA0                 ;  CHECK FOR ADV.
                bitb   #2
                beq    RAM6C                ;  NOT PRESSED.
                jmp    ,Y                   ;  TIME TO RETURN
RAM6C           clrb                        ;  RETURN B TO ZERO
RAM6B           exg    X,D                  ;  TRADE BACK
                cmpx   #$C000              ;  DONE??
                bne    RAM3                 ;  NOPE..CONTINUE

                tfr    U,D                  ;  RESTORE SEED.
                ldx    #0
RAM7            comb
                bitb   #9
                bne    RAM8
                comb
                rora
                rorb
                bra    RAM10
RAM8            comb
                bitb   #9
                bne    RAM9
                rora
                rorb
                bra    RAM10
RAM9            lsra
                rorb
RAM10           cmpd   ,X++
                bne    RERROR               ;  RAM ERROR!
RAM25           exg    X,D                  ;  CHECK FOR END OF PAGE
                tstb
                bne    RAM17
                ldb    #WDATA
                stb    WDOG
                tfr    DP,B                 ;  SEE IF NORMAL RUN
                cmpb   #$FF                ;  FRONT DOOR??
                bne    RAM17C               ;  NOPE
                ldb    PIA0                 ;  CHECK ADV.
                bitb   #2
                beq    RAM17C
                jmp    ,Y                   ;  JUST RETURN (NO ERRORS)
RAM17C          clrb
RAM17           exg    X,D                  ;  TRADE BACK
                cmpx   #$C000              ;  DONE??
                bne    RAM7
                tfr    D,U                  ;  SHOVE NEW SEED OVER
                tfr    DP,A
                cmpa   #$FF                ;  FF INDICATES FRONT DOOR
                bne    RAM99
                tfr    U,D
                jmp    RAM0                 ;  DO ANOTHER ITERATION IN THIS TEST.
RAM99           deca                        ;  TAKE ONE AWAY
                tfr    A,DP                 ;  RETURN
                cmpa   #$80                ;  ZERO OUT IN AUTO CYCLE???
                beq    RAM99_               ;  YEP....
                tsta                        ;  SET FOR BELOW BRANCH
                tfr    U,D                  ;  MAKE SEED AND SAVED SEED LOOK ALIKE
                lbne   RAM0                ;  DO ANOTHER ITERATION
RAM99_          ldb    #1                   ;  SET BACK TO ROM
                stb    RWCNTL
                jmp    ,Y                   ;  RETURN
RERROR          leax   -2,X                ;  BACK TO ERROR POINT
                eora   ,X                  ;  FIND DIFFERENCE.
                eorb   1,X
                tsta                        ;  NO DIFFERENCE?
                bne    RERR2                ;  DIFF.
                tstb
                bne    RERR2
                leax   2,X                 ;  UNDO THIS BULLSHIT...MAYBE ALPHA BABY.
                bra    RAM25                ;  AND CONTINUE
RERR2           ldu    #$30                 ;  FIND BANK NUMBER
                exg    X,D
                clrb
                exg    X,D
RERR0           cmpx   #0
                beq    RERR1                ;  DONE.
                leax   -$100,X
                leau   $10,U
                cmpu   #$30
                bls    RERR0
                ldu    #$10
                bra    RERR0
RERR1           leau   1,U
                asra
                bcs    RERR5                ;  THIS IS THE BIT
                asrb
                bcs    RERR5
                bra    RERR1
RERR5           tfr    U,D                  ;  BRING THE BANK,BIT INTO B
                lda    #1                   ;  INDICATE RAM ERROR
                sta    RWCNTL               ;  BACK TO THAT JAZZ
                lds    #RERR6               ;  RETURN HERE
                jmp    PULSE                ;  PULSE THE LEDS
RERR6           lda    #RAM>>8
                tfr    A,DP
                tfr    CC,A
                coma
                bita   #$C0                ;  INITIAL TEST???
                beq    RER098               ;  YEP
                lda    #FDRAMM
                bra    RER099
RER098          lda    #RERRM
RER099          lds    #HSTK                ;  SET UP THE STACK
                jsr    SCRCLR
                jsr    WRD7V                ;  PRINT THE MESSAGE.
                tfr    CC,A                 ;  SEE WHICH TEST
                bita   #$40                ;  FIRQ SAYS FRONT DOOR
                bne    REROR7               ;  NOPE
                jmp    FDRET                ;  DO THE FRONT DOOR RETURN
REROR7          ldy    #INITV               ;  OTHERWISE START...TRY AND RUN THE GAME.
                bra    DELAY

DELAY           lda    #32
DELA1           ldx    #$5800
DELA2           leax   -1,X
                ldb    #WDATA
                stb    WDOG
                cmpx   #0
                bne    DELA2
                deca
                bne    DELA1
                jmp    ,Y                   ;  RETURN

;
;     PULSE......PUT SOME CRAP IN LED'S
;     LOW HALF OF A IS ERROR CODE...
;     BOTH HALVES OF B ARE THE RELEVANT DATA.
;

PULSE           tfr    D,U                  ;  SAVE A,B
                lda    #2
                tfr    A,DP
PULSE0          tfr    U,D
                ldy    #PULSE1
                jmp    PSSUB
PULSE1          lda    #2
                ldy    #PULSE2
                jmp    DELA1
PULSE2          ldy    #PULSE3
                jmp    BLKLED
PULSE3          lda    #1
                ldy    #PULSE4
                jmp    DELA1
PULSE4          tfr    U,D                  ;  GET DATA BACK
                tfr    B,A                  ;  SHIFT B DOWN
                lsra
                lsra
                lsra
                lsra                        ;  PROPER HALF
                ldy    #PULSE5
                bra    PSSUB                ;  PULSE IT.
PULSE5          lda    #2
                ldy    #PULSE6
                bra    DELA1
PULSE6          ldy    #PULSE7
                bra    BLKLED
PULSE7          lda    #1
                ldy    #PULSE8
                bra    DELA1
PULSE8          tfr    U,D
                tfr    B,A
                ldy    #PULSE9
                bra    PSSUB
PULSE9          lda    #2
                ldy    #PULS10
                bra    DELA1
PULS10          ldy    #PULS11
                bra    BLKLED
PULS11          lda    #5
                ldy    #PULS12
                jmp    DELA1
PULS12          tfr    DP,A                 ;  SEE IF FIRST PASS??
                deca
                tfr    A,DP
                bne    PULSE0
                ldy    #PULS95
                bra    BLKLED               ;  BLANK EM.
PULS95          tfr    U,D                  ;  RESTORE D
                jmp    ,S                   ;  AND RETURN

BLKLED          lda    #$3C
                sta    PIA0_ctrl
                inca
                sta    PIA1_ctrl
                lda    #$C0
                sta    PIA1
                jmp    ,Y                   ;  AND RETURN

;     PSSUB - PUT LOW HALF OF A TO LEDS

PSSUB           tfr    A,B                  ;  SAVE A COPY.
                rora                        ;  BIT 0 - CARRY
                rora                        ;  BIT 0 - BIT 7
                rora                        ;  BIT 0 - BIT 6
                anda   #$C0
                sta    PIA1                 ;  THOSE BITS OUT.
                lda    #$34                 ;  ASSUME ZERO
                bitb   #$4                 ;  SEE IF 1
                beq    PSSUB1               ;  NOPE
                lda    #$3C
PSSUB1          sta    PIA1_ctrl            ;  THATS ALL FOR THAT BIT
                lda    #$34
                bitb   #$8
                beq    PSSUB2
                lda    #$3C
PSSUB2          sta    PIA0_ctrl
                jmp    ,Y                   ;  AND RETURN

ROMTST          orcc   #$3F                ;  NO INTERRUPTS WHILE THIS RUNS
                ldx    #ROMTAB              ;  INDEX INTO TABLE
ROM0            cmpx   #ROMEND             ;  DONE??
                beq    ROMDUN               ;  YEP
                lda    1,X                  ;  LOOK TO SEE IF PART STUFFED??
                beq    ROMBOT               ;  NOPE...MOVE TO NEXT
                lda    ,X                   ;  GET BASE
                clrb
                tfr    D,U                  ;  USE U TO POINT THROUGH
                lda    #WDATA               ;  FOR ROVER
ROM1            addb   ,U+                 ;  ADD A BYTE
                sta    WDOG
                exg    D,U                  ;  SEE IF DUN
                cmpa   2,X                 ;  ARE WE TO NEXT PART
                exg    D,U                  ;  CHANGE BACK
                bne    ROM1                 ;  NOPE
                cmpb   1,X                 ;  EXPECTED SUM???
                bne    ROMERR               ;  NOPE....LETS SEND OUT PROBLEM WARNING.
ROMBOT          leax   2,X                 ;  MOVE TO NEXT PART
                bra    ROM0
ROMERR          lda    ,X                   ;  GET THE ADDRESS OF THE BAD PART
                lsra
                lsra
                lsra
                lsra
                cmpa   #$D                 ;  ONE OF TOP 3 PARTS??
                blo    ROMER1               ;  NOPE
                suba   #4                  ;  GROUP THEM WITH THE LOWER PARTS
ROMER1          adda   #1                  ;  MAKE ERRORS FROM 1-12
                daa                         ;  AND MAKE BCK
                tfr    A,B
                lda    #2                   ;  ROM ERROR
                lds    #RERR3               ;  RETURN
                jmp    PULSE                ;  AND SEND OUT THE CODE
RERR3           lda    #RAM>>8
                tfr    A,DP
                lda    #WDATA
                sta    WDOG
                lds    #HSTK
                jsr    SCRCLR
                tfr    CC,A
                coma
                bita   #$C0                ;  INITIAL TEST??
                beq    RERR94
                lda    #FDROMM
                bra    RERR95
RERR94          lda    #ROMERM
RERR95          jsr    WRD7V                ;  PUT THE MESSAGE ON THE SCREEN
                tfr    CC,B                 ;  SEE WHERE WE CAM FROM
                bitb   #$40                ;  FIRQ SAYS FRONT DOOR
                bne    ROMER3
                jmp    FDROM
ROMER3          ldy    #INITV               ;  NOW TRY AND RUN THE GAME
                jmp    DELAY                ;  BUT NOT UNTIL A SHORT DELAY.

ROMDUN          jmp    ,Y                   ;  ALL ROMS OK...RETURN.

;     ROMTAB    - THIS TABLE INDICATES THE BASE OF THE PART, AND THE
;     RESULTING SUM.    IF SUM=0 THEN PART IS NOT STUFFED.

ROMTAB  FCB    $00,$BC                  ;  0000
        FCB    $10,$0B                  ;  1000
        FCB    $20,$CA                  ;  2000
        FCB    $30,$21                  ;  3000
        FCB    $40,$FB                  ;  4000
        FCB    $50,$97                  ;  5000
        FCB    $60,$43                  ;  6000
        FCB    $70,$B3                  ;  7000
        FCB    $80,$15                  ;  8000
        FCB    $90,0                    ;  9000
        FCB    $A0,0                    ;  A000
        FCB    $B0,0                    ;  B000
        FCB    $C0,0                    ;  C000
        FCB    $D0,$2F                  ;  D000
        FCB    $E0,$1F                  ;  E000
        FCB    $F0,$01                  ;  F000

ROMEND  FCB    0
        FCB    $3C                      ;  CHECK BYTE (FUDGER) FOR THIS PART ($F000)


;; Hardware Vectors
        org $fff0
        fdb    PWRUPV   ; reserved by motorola
        fdb    PWRUPV   ; sw3
        fdb    PWRUPV   ; sw2
        fdb    PWRUPV   ; firq
        fdb    IRQHK    ; irq
        fdb    PWRUPV   ; swi
        fdb    PWRUPV   ; nmi
        fdb    PWRUPV   ; reset



