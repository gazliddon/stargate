;  TTL	<<<<<  S T A R G A T E  >>>>>

;;; Moves objects between active and inactive lists depending if the object
;;; on screen or not
;;; Also draws the scanner
;;;
;;; __Frame Sequencing__
;;; * 0 - ISCAN - Moves off screen objs to innactive list
;;; * 1 - 
;;; * 2 - OSCAN / SHSCAN
;;; * 3 -
;;; * 4 - SCNR
;;; * 5 -
;;; * 6 -
;;; * 7 -

SCPROC
// Move any offscreen objs to the innactive list
                jsr    ISCAN
                SLEEP(2)

// move any on-acreen inactive objs to the active list
                jsr    OSCAN
                jsr    SHSCAN
                SLEEP(2)
                jsr    SCNR
                NAP   (4,SCPROC)

;;; Scan all active objects
;;; move any offscreen into an inactive list
;;; called every 8 frames
OSCAN           ldd    BGL                  ; get screen left
                subd   #100*32
                std    XTEMP

                ldx    #OPTR                ; X points to obj list ptr
                bra    !next

!doit           ldd    OX16,X
                subd   XTEMP
                cmpd   #500*32
                blo    !next                ; IN SCREEN AREA
                ldu    OLINK,X              ; NO,MOVE TO INACTIVE LIST
                stu    ,Y
                ldu    IPTR                 ; insert at head of innactive obj list
                stu    OLINK,X
                stx    IPTR
                leax   ,Y                   ; X TO NEXT

!next           leay   ,X                   ; Y = X
                ldx    ,X                   ; get ptr to next obj
                bne    !doit                ; process next object
                rts

;;; Scan innactive objects for any that are now on screen
;;; and move to the active objects list
ISCAN           ldd    BGL                  ; get screen left
                subd   #100*32              ; 100 PIXEL LEFT BUFFER
                std    XTEMP
                ldx    #IPTR
                bra    ISCLP

ISC1            lda    STATUS               ; OBJECTS ACTIVE??
                bita   #$20
                bne    SCYV5                ; NOPE....NO VELOCITY STUFF...JUST CHECK IF ACTIVE

// Objects are active so
// Add 8 times velocity to the inactive object
// as we're only updating inacctive objects every 8 frames
                ldd    OYV,X
                aslb
                rola
                aslb
                rola
                aslb
                rola
                addd   OY16,X
                cmpa   #YMIN                ; WRAP AROUND
                bhs    SCYV3
                cmpa   #(YMIN/2)+(YMAX/2)-$80
                bls    SCYV6
                lda    #YMAX
SCYV3           cmpa   #YMAX
                bls    SCYV4

SCYV6           lda    #YMIN

SCYV4           std    OY16,X               ; ADD 8 TIMES VELOCITY
                ldd    OXV,X                ; UPDATE X POSITION
                aslb
                rola
                aslb
                rola
                aslb
                rola
                addd   OX16,X
                std    OX16,X

; now check if this object is onscreen
SCYV5           ldd    OX16,X               ; RELOAD IN CASE WE GET HERE BY BRANCH
                subd   XTEMP
                cmpd   #500*32              ; IN SCREEN AREA
                bhs    ISCLP                ; NO

; Move object active list
; where's the delink?
                ldu    OLINK,X              ; YES, TRANSFER TO ACTIVE
                stu    ,Y
                ldu    OPTR
                stu    OLINK,X
                stx    OPTR
                clra                        ; MAKE SURE OFF SCREEN AFTER HYPER
                clrb
                std    OBJX,X
                leax   ,Y

ISCLP           leay   ,X                   ; get the next object and loop
                ldx    ,X
                bne    ISC1
                rts

;;; SCANNER
;;; OFF OLDE BOZOS+PLAYER
SCNR            ldu    #0
                ldb    #8
                ldx    #SETAB
!loop           stu    [,X]
                stu    [2,X]
                stu    [4,X]
                stu    [6,X]
                abx
                cmpx   SETEND
                blo    !loop
// OFF PLAYER +
                ldx    [SETEND]
                beq    !skip
                stu    ,X
                clr    2,X
                stu    -$100,X
; OUTPUT TERRAIN
!skip           ldd    BGL                  ; CALC SCANL
                subd   #$8000-(150*32)
                std    XTEMP
                lsra
                lsra                        ; GET 6 BITS
                ldu    #MTERR
                ldb    #3
                mul
                leau   D,U                  ; GET ADDR
                lda    STATUS
                bita   #2                   ; NO TERRAIN???
                bne    !MTX                 ; NONE
                lda    #SCANER>>8
                ldy    #STETAB              ; ERASE TABLE
!mloop          ldx    #0
                stx    [,Y]                 ; OFF OLD
                pulu   B,X
                std    ,Y                   ; SAVE NEW ADDR
                stx    [,Y++]
                inca
                ldx    #0
                stx    [,Y]
                pulu   B,X
                std    ,Y
                stx    [,Y++]
                inca
                cmpa   #(SCANER>>8)+64
                bne    !mloop
; SCANNER BEZEL
!MTX            ldx    #SCANER+$1C00
                ldd    #$9090
                std    ,X
                std    26,X
                ldx    #SCANER+$2300
                ldd    #$0909
                std    ,X
                std    26,X
; OUTPUT BOZOS
                jmp    PACH1                ;  ****************THIS IS A PATCH*********
; LDX #OPTR THIS USED TO BE HERE
BOZ1            ldu    #SETAB               ; ERASE TABLE
                bsr    SCNR3
                ldx    #IPTR
                bsr    SCNR3
BOZ2            stu    SETEND               ; END OF ERASE TABLE
; PLAYER BLIP OUTPUT
                ldd    PLAXC
                lsra
                lsra
                lsra
                lsra
                lsrb
                lsrb
                lsrb
                addd   #$4700+SCANH-5       ; PLAYER BASE
                std    ,U
                ldx    ,U
                ldd    #$9099
                std    ,X
                sta    2,X
                lda    #$09
                sta    -$FF,X
                rts
; BLIP SCAN SUBROUTINE
SCNR10          ldd    OX16,X               ; FIND OFFSET
                subd   XTEMP                ; SUBTRACT SCANNER LEFT
SCNR2           lsra                        ; SHIFT DOWN TO 6 BITS
                lsra
                ldb    OY16,X
                lsrb
                lsrb
                lsrb
                addd   #SCANER-5
                std    ,U
                ldd    OBJCOL,X             ; STORE BLIP
                cmpa   #$1F                 ; STAR GATE?
                bne    SCNR20               ; NO
                ldd    ,U
                cmpa   #( SCANER>>8 )+$3F   ; CLIP GATE AT LIMIT
                bne    SCNR2A
                deca
                sta    ,U
SCNR2A          ldy    #$FFF0
                sty    [,U++]
                incb
                std    ,U
                ldy    #$F0FF
                sty    [,U++]
                inca
                std    ,U
                ldy    #$F0F0
                sty    [,U++]
                decb
                std    ,U
                sty    [,U++]
                bra    SCNR3
SCNR20          std    [,U++]
SCNR3           ldx    ,X                   ; DO THE REST
                bne    SCNR10
                rts

;;; MINI TERRAIN DATA TABLE
MTERR           FCB 27,$08,$80,26,$80,$08,28,$80,$08,30,$80,$08
                FCB 31,$88,$00,31,$88,$00,30,$88,$00,28,$88,$00
                FCB 26,$88,$00,28,$88,$00,30,$88,$00,31,$80,$08
                FCB 32,$88,$00,32,$88,$00,31,$88,$00,30,$88,$00
                FCB 28,$08,$80,28,$88,$00,29,$80,$08,31,$88,$00
                FCB 31,$88,$00,31,$02,$20,31,$22,$00,31,$02,$20
                FCB 30,$22,$00,31,$22,$00,30,$02,$20,28,$02,$20
                FCB 26,$02,$20,28,$20,$02,29,$02,$20,28,$02,$20
                FCB 25,$02,$20,22,$02,$20,24,$20,$02,27,$20,$02
                FCB 29,$20,$02,31,$20,$02,31,$22,$00,31,$22,$00
                FCB 31,$20,$02,32,$22,$00,31,$0B,$B0,29,$0B,$B0
                FCB 28,$BB,$00,30,$BB,$00,30,$0B,$B0,27,$0B,$B0
                FCB 29,$B0,$0B,31,$0B,$B0,32,$BB,$00,31,$0B,$B0
                FCB 31,$B0,$0B,31,$BB,$00,31,$B0,$0B,32,$BB,$00
                FCB 31,$0B,$B0,31,$B0,$0B,31,$BB,$00,31,$0B,$B0
                FCB 31,$BB,$00,31,$BB,$00,30,$0B,$B0,29,$0B,$B0
                FCB 27,$08,$80,26,$80,$08,28,$80,$08,30,$80,$08                ; REPEAT
                FCB 31,$88,$00,31,$88,$00,30,$88,$00,28,$88,$00
                FCB 26,$88,$00,28,$88,$00,30,$88,$00,31,$80,$08
                FCB 32,$88,$00,32,$88,$00,31,$88,$00,30,$88,$00
                FCB 28,$08,$80,28,$88,$00,29,$80,$08,31,$88,$00
                FCB 31,$88,$00,31,$02,$20,31,$22,$00,31,$02,$20
                FCB 30,$22,$00,31,$22,$00,30,$02,$20,28,$02,$20
                FCB 26,$02,$20,28,$20,$02,29,$02,$20,28,$02,$20
                FCB 25,$02,$20,22,$02,$20,24,$20,$02,27,$20,$02
                FCB 29,$20,$02,31,$20,$02,31,$22,$00,31,$22,$00
                FCB 31,$20,$02,32,$22,$00,31,$0B,$B0,29,$0B,$B0
                FCB 28,$BB,$00,30,$BB,$00,30,$0B,$B0,27,$0B,$B0
                FCB 29,$B0,$0B,31,$0B,$B0,32,$BB,$00,31,$0B,$B0
                FCB 31,$B0,$0B,31,$BB,$00,31,$B0,$0B,32,$BB,$00
                FCB 31,$0B,$B0,31,$B0,$0B,31,$BB,$00,31,$0B,$B0
                FCB 31,$BB,$00,31,$BB,$00,30,$0B,$B0,29,$0B,$B0

;;; WRITE AN OBJECT TO SCREEN
;;; Y = OBJECT DESCRIPTOR ADDR
;;; D = SCREEN ADDR
;;; ALL SAVED
;;; REENTRANT
CWRIT           pshs   A,B,X,Y,U
                tfr    D,X
                ldd    OBJW,Y               ; GET WIDTH,LENGTH
                ldy    OBJP0,Y              ; GET DATA
                pshs   D                    ; SAVE WIDTH,HEIGHT
                bitb   #1                   ; ODD LENGTH?
                bne    CWRT1
; EVEN BYTE COUNT
CWRT00          subb   #2
CWRT0           ldu    B,Y
                stu    B,X
                subb   #2
                bpl    CWRT0
                ldb    1,S
                leax   $100,X
                leay   B,Y
                deca
                bne    CWRT00
                bra    CWRTX
// ODD BYTE COUNT
CWRT1           decb
                lda    B,Y
                sta    B,X
                subb   #2
                bmi    CWRT3
CWRT2           ldu    B,Y
                stu    B,X
                subb   #2
                bpl    CWRT2
CWRT3           ldb    1,S                  ; GET HEIGHT
                leax   $100,X
                leay   B,Y
                dec    ,S                   ; DEC WIDTH
                bne    CWRT1
CWRTX           leas   2,S                  ; GET MAP +2 PHONIES
                puls   A,B,X,Y,U,PC

;;; __Func:__ COFF
;;; Y = Object descriptor addr
;;; D = Screen addr
COFF            pshs   A,B,X,U
                tfr    D,X
                ldd    OBJW,Y               ; GET WIDTH ,HEIGHT

;;; Clear block
;;; called by other functions and assumes
;;; A,B,X,U already on stack
COFFA           ldu    #0                   ; clear col
                pshs   B                    ; SAVE HT
                bitb   #1                   ; ODD LENGTH?
                bne    !coff1               ; YES

// EVEN BYTE COUNT
!coff00         subb   #2
!coff0          stu    B,X
                subb   #2
                bpl    !coff0
                ldb    ,S                   ; GET HT
                leax   $100,X               ; Next column
                deca
                bne    !coff00
                bra    !COFFX

// ODD BYTE COUNT
!coff1          decb
                clr    B,X
                subb   #2
                bmi    !COFF3
!COFF2          stu    B,X
                subb   #2
                bpl    !COFF2
!COFF3          ldb    ,S                   ; GET HT
                leax   $100,X
                deca
                bne    !coff1
!COFFX          leas   1,S                  ; PULL HEIGHT
                puls   A,B,X,U,PC

;;; __Func:__ BLKCLR
;;; Clear a block on the screen
;;;     * X-> Screen
;;;     * D = W,H
BLKCLR          pshs   A,B,X,U
                bra    COFFA

;;; SCREEN CLEAR
SCRCLR          pshs   D,X,Y,U
                ldu    #$9C00
                ldx    #0
                tfr    X,Y
                tfr    X,D
SCLRLP          pshu   X,Y,D
                pshu   X,Y,D
                pshu   X,Y,D
                pshu   X,Y,D
                pshu   X,Y,D
                pshu   X                    ; 32 AT A CRACK
                cmpu   #0
                bne    SCLRLP
                puls   D,X,Y,U,PC

;;; CLEAR ACTIVE SCREEN
SCLR1           pshs   D,X,Y,U,DP
                ldu    #$9C00
                ldx    #0
                tfr    X,Y
                tfr    X,D
                tfr    A,DP
SC0LP           ldb    #8
SC1LP           pshu   X,Y,A,DP
                pshu   X,Y,A,DP
                pshu   X,Y,A,DP
                pshu   X,Y,A,DP
                decb
                bne    SC1LP
                pshu   X,Y,A,DP
                pshu   X,Y,A,DP
                pshu   X,Y,A,DP
                pshu   X
                leau   -YMIN,U
                cmpu   #0
                bne    SC0LP
                puls   D,X,Y,U,DP,PC

;;;  CMOS PRIMITIVE FOR READING
RCMOSA          lda    1,X                  ; GET LSB
                anda   #$0F                 ; LEAVE ONLY LS
                pshs   A                    ; SAVE IT
                lda    ,X++                 ; GET MSB + AUTO INC
                asla
                asla
                asla
                asla                        ; SHIFT LS TO MSB
                adda   ,S+                  ; GET LSB + FIX STACK
DRTS            rts                         ; BYE

;;;  READ CMOS INTO D POINTED TO BY X: A=X,X+1; B=X+2,X+3
RCMOSD          bsr    RCMOSA               ; GET THE FIRST BYTE+FALL THRU FOR 2ND

;;;  READ CMOS INTO B POINTED TO BY X
RCMOSB          pshs   A                    ; SAVE A
                bsr    RCMOSA               ; GET IN A
                tfr    A,B                  ; PUT IT IN B
                puls   A,PC                 ; DONE

;;;  WRITE TO CMOS PRIMITIVE
WCMOSA
                pshs   A                    ; SAVE WHATS TO BE WRITTEN
                sta    1,X                  ; SAVE LSB
                lsra                        ; SHIFT	MS TO LS
                lsra
                lsra
                lsra
                sta    ,X++                 ; SAVE MSB AND AUTO INC
                puls   A,PC                 ; DONE

;;;  WRITE CMOS FROM D TO X: A=X,X+1; B=X+2,X+3
WCMOSD          bsr    WCMOSA               ; DO IT AND FALL THRU FOR 2ND

;;;  WRITE CMOS FROM B TO X
WCMOSB          pshs   A                    ; SAVE A
                tfr    B,A                  ; MOVE B TO A
                bsr    WCMOSA               ; FAKE IT
                puls   A,PC                 ; DONE

;;; SCORE ROUTINE
;;; A=0-7EXP,B=0-99
;;;
;;; SCORE PSHS A,B,X,U,Y
;;; ORCC #1 SET CARRY
SCORE           jmp    SCRPCH               ;  *****************NO SCORE IN ATTR
                nop                         ;  ***********************************************

SCORE2          rol    SCRFLG               ; SET FLAG
                lsra
                pshs   A
                lda    #0
                bcc    !SCR1
                aslb
                rola
                aslb
                rola
                aslb
                rola
                aslb
                rola
!SCR1           jsr    PLINDX
                std    XTEMP
                ldb    #3
                subb   ,S+
                lda    B,X
                adda   XTEMP+1
                daa
                sta    B,X
                decb
                bmi    !SCRX
!SCR2           lda    B,X
                adca   XTEMP
                daa
                sta    B,X
                lda    #0
                sta    XTEMP
                decb
                bpl    !SCR2
// CHECK REPLAY
!SCRX           ldd    REPLA                ; REPLAYS AVAILABLE
                beq    SCRXX                ; NO
                leay   4,X
                bsr    RCHK
                blo    SCRXX                ; NO
                lda    2,Y                  ; GET NEXT LEVEL
                adda   REPLA+1
                daa
                sta    2,Y
                lda    1,Y
                adca   REPLA
                daa
                sta    1,Y
                lda    ,Y
                adca   #0
                daa                         ; DUMB	ASSHOLE ASSOCIATION
                sta    ,Y
                jsr    RPHK                 ; GO TO YOUR REPLAY HOOK
                jsr    LDISPV               ; REWARD
                jsr    SBDSPV
                ldd    #RPSND
                jsr    SNDLD
                ldb    #5
                jsr    AUD1
SCRXX           bsr    SCRTRN
                puls   A,B,X,U,Y
                rts

;;; REPLAY CHECK
;;; X=PLAYER SCORE MSB 8 DIGITS
;;; Y=REPLAY LEVEL MSB 8 DIGITS
;;; RETURNS CC FOR PSCOR-RLEVL
RCHK            pshs   D
                ldd    ,X
                cmpd   ,Y
                bne    !skip
                ldd    2,X
                cmpd   2,Y
!skip           puls   D,PC

;;; SCORE TRANSFER
SCRTRN          lda    SAMEXC               ; Skip if TERRAIN BLOW UP OR WARP IN EFFECT??
                beq    !no_sploding
                rts

!no_sploding
                lda    CURPLR
SCRTR0          ldx    #N0V
                ldb    STATUS
                bmi    SCT0
                cmpa   CURPLR
                bne    SCT0
                ldx    #N0AV
SCT0            pshs   A,X
                deca
                bne    SCT1
                ldx    #P1DISP
                ldu    #.P1SCR
                bra    SCT2
SCT1            ldx    #P2DISP
                ldu    #.P2SCR
SCT2            clr    XTEMP                ; BLANKING FLAG
                ldb    #7
SCTLP           lda    ,U+                  ; GET
                ldy    1,S                  ; GET BASE OF CHARSTORAGE
                bitb   #1                   ; PHASE
                bne    SCT3
                leau   -1,U
                lsra
                lsra
                lsra
                lsra
SCT3            anda   #$F
                bne    SCT3A
                cmpb   #2                   ; LAST 2
                bls    SCT3A
                tst    XTEMP                ; BLANKING?
                beq    SCT4
SCT3A           inc    XTEMP
                asla
                asla
                leay   A,Y
                exg    X,D
                jsr    CWRIT                ; ON DIGIT PICTURE
SCT40           exg    X,D
SCT4            leax   DIGLTH,X
                decb
                bne    SCTLP                ; DO THE REST
                puls   A,Y,PC

;;; SCREEN SWITCHING
P1SW            pshs   D,X                  ; SAVE REGISTERS
P1SW0           ldx    #IRQ
                ldd    #$013C
                bra    PSW1
P2SW            pshs   D,X                  ; SAVE REGISTERS
                lda    PIA3                 ; COCKTAIL?
                bpl    P1SW0                ; NO
                ldx    #IRQB                ; SWITCH UPSIDE DOWN GUYS
                ldd    #$0334
PSW1            stx    IRQHK+1              ; IRQ SELECT
                stb    PIA3+1
                sta    RWCNTL
                lda    #$7E
                sta    IRQHK
                puls   D,X,PC

;;; CHECK FOR PROCESS EXISTANCE
;;; Y=PADDR OF SUSPECT
;;; RET EQ IF FOUND, NE IF NOT THERE
PCHEK           pshs   X
                ldx    ACTIVE
                beq    PCHK2
PCHK1           cmpy   proc.addr,X
                beq    PCKBYE
                ldx    ,X
                bne    PCHK1
PCHK2           andcc  #$FB                 ; RET NE
PCKBYE          puls   X,PC

BSET            jsr    SCLR1                ; CLEAR THE SCREEN FOR THE BONE
                ldx    PLRX                 ; GET PLAYER
                lda    PWAV,X               ; GET HIS WAVE NUMBER
                jsr    HEXBCD               ; IN BCD PLEASE (HUMAN NUMBER??)
                tfr    A,B                  ; PUT IN B FOR MESSAGE
                cmpa   #5
                bls    BSET1
                lda    #5                   ; NO MORE THAN 500 THOUGH FOR BONUS
BSET1           pshs   A                    ; SAVE FOR A MOMENT
                lda    #ATTAKM              ; MAKE THE ATTACK MESSAGE
                jsr    WRD7FV
                puls   A,PC                 ; RESTORE A (HUNDRED VALUE) AND RETURN


;;; BONUS COLLECT PROCESS
;;; A GETS NUMBER OF HUNDREDS OF BONUS MULTIPLIER (HEX)
BONUS           clr    PCRAM
                ldu    CRPROC
                puls   X
                stx    proc.data +6,U       ; SAVE RETURN
BON1            clrb
                tfr    D,Y                  ; SAVE FOR TEXT
                tfr    A,B                  ; MOVE COUNT TO COUNT
                lda    #2                   ; PUT IN POWER
                std    proc.d4,U            ; AND SAVE FOR SCORE
                lda    #ATWVM               ; WRITE IT OUT
                jsr    WRD7FV
                ldx    #$38A0               ; INITIAL ADDR
                lda    ASTCNT
                sta    proc.d2,U
                beq    BC2
BCLP            tfr    X,D
                ldy    #ASTP3
                jsr    CWRIT
                leax   $400,X
                ldd    proc.d4,U            ; GET SCORE
                jsr    SCORE
                stx    proc.data ,U
                NAP (4,BC1)
BC1             ldx    proc.data ,U
                dec    proc.d2,U            ; ALL DONE?
                bne    BCLP                 ; NO
BC2             ldx    PLRX
                jsr    GETWVV
                NAP ($80,BC3)                   ; HANG OUT
BC3             jmp    [proc.data+6,U]      ; RETURN

;;; Firq INTERRUPT ROUTINE
;;; dispatches to the correct routine
;;; Firq triggers 
IRQ             lda    #RAM>>8
                tfr    A,DP
                lda    #$34
                sta    PIA1_ctrl
                lda    PIA1
                lda    VERTCT               ; PAST 128
                cmpa   #128
                blo    I0                   ; NO

                lda    IFLG                 ; ALREADY DONE ?
                bne    IRQX                 ; NO
                inc    IFLG
                jsr    SNDSEQ               ;  SOUNDS+SWITCHES
                jsr    PLAYER
                jsr    STOUT                ;  OUTPUT STARS
                lda    VERTCT
                suba   #8
                cmpa   #$A8
                bls    IRQ1

                lda    #$A8
IRQ1            sta    XXX2
                ldd    XXX2
                jsr    OPROC
                ldd    XXX2
                jsr    PRDISP
                jsr    SHELL
                bra    IRQX                 ; exit irq

;;; I/o scanning
I0              ldb    IFLG
                beq    IRQX                 ; exit if being serviced
                clr    IFLG
                inc    TIMER
                ldb    #WDATA
                stb    WDOG
                cmpa   #4                   ; TOO LATE?
                bhi    I01

; COLOR MAPPING
                ldu    #CRAM+16
                ldd    PCRAM+10
                ldx    PCRAM+12
                ldy    PCRAM+14
                pshu   D,X,Y
                ldd    PCRAM+4
                ldx    PCRAM+6
                ldy    PCRAM+8
                pshu   D,X,Y
                ldd    PCRAM
                ldx    PCRAM+2
                pshu   D,X

I01             jsr    CSCAN                ; COIN SWITCHES
                lda    STATUS
                bita   #$02
                bne    IRQ2
                jsr    BGOUT                ; Update terrain
IRQ2            ldd    XXX1
                jsr    PRDISP               ; Draw the player
                ldd    XXX1
                jsr    OPROC
                jsr    VELO                 ; UPDATE VELOCITIES
IRQX            orcc   #$FF
                lda    #$35
                sta    PIA1_ctrl
                lda    ,S                   ; E BIT FIX
                anda   #$6F
                sta    ,S
                puls   X,Y,D,U,PC,CC,DP

;;; INVERTED IRQ FOR SCREEN FLIP
;;; Block copy of IRQ but handles flipped
;;; screen for cocktail cabs
IRQB            lda    #RAM>>8
                tfr    A,DP
                lda    #$34
                sta    PIA1_ctrl
                lda    PIA1
                lda    VERTCT
                cmpa   #$60                 ; CHECK A0 (COUNT RUNS BACKWARDS)
                blo    IB0
                ldb    IFLG
                bne    IRQX
                inc    IFLG
                coma
                sta    XXX2                 ; WRITING BOUNDARY
                jsr    CSCAN
                lda    STATUS
                bita   #$02
                bne    IRQB2
                jsr    BGOUT
IRQB2           ldd    XXX1
                jsr    PRDISP
                ldd    XXX1
                jsr    OPROC
                bra    IRQX
IB0             ldb    IFLG
                beq    IRQX
                clr    IFLG
                inc    TIMER
                ldb    #WDATA               ; THROW ROVER A BONE
                stb    WDOG                 ; BONE HIM
                cmpa   #4
                bhi    IB01
; COLOR MAPPING
                ldu    #CRAM+16
                ldd    PCRAM+10
                ldx    PCRAM+12
                ldy    PCRAM+14
                pshu   D,X,Y
                ldd    PCRAM+4
                ldx    PCRAM+6
                ldy    PCRAM+8
                pshu   D,X,Y
                ldd    PCRAM
                ldx    PCRAM+2
                pshu   D,X
IB01            jsr    SNDSEQ
                jsr    PLAYER
                jsr    STOUT
                ldd    XXX2
                jsr    PRDISP
                ldd    XXX2
                jsr    OPROC
                jsr    SHELL
                jsr    VELO
                jmp    IRQX

;;; SHELL OUTPUT
;;; OBJCOL=OUTPUT ROUTINE
;;; ODATA=TIMER
;;; ODATA+1=DEAD OR NOT
SHELL           lda    STATUS
                bita   #$20
                bne    SHELLX
                ldd    BGL                  ; CLAC SCROLL
                andb   #$E0
                std    SHTEMP
                ldd    BGLX
                andb   #$E0
                subd   SHTEMP
                aslb
                rola
                aslb
                rola
                std    SHTEMP
                ldx    #SPTR                ; PROC BUGGERS
                bra    SHELLP
SHELL1          ldy    OBJX,X               ; ERASE ADDR
                ldd    OYV,X
                addd   OY16,X
                cmpa   #YMIN
                bls    SHBD                 ; DEAD
                std    OY16,X
                ldd    OXV,X
                addd   SHTEMP
                addd   OX16,X
                cmpa   #$97
                bhs    SHBD                 ; DEAD
                std    OX16,X
                aslb                        ; FLAVOR
                ldb    OY16,X
                std    OBJX,X               ; NEW X
                jmp    [OBJCOL,X]           ; OUTPUT
SHELLP          ldx    ,X
                bne    SHELL1
SHELLX          rts

;;; BOMB OUTPUT
BMBOUT          ldu    BAX
                bcc    SHB1                 ; ODD?
                leau   6,U
SHB1            ldd    #0                   ; CLEAR IT
                std    ,Y
                sta    2,Y
                std    $100,Y
                sta    $102,Y
                ldy    OBJX,X
                ldd    ,U
                std    ,Y
                ldd    2,U
                sta    2,Y
                stb    $100,Y
                ldd    4,U
                std    $101,Y
                bra    SHELLP
SHBD
                CLRD()                          ; KILL AND ERASE
                sta    ODATA+1,X
                std    ,Y
                sta    2,Y
                std    $100,Y
                sta    $102,Y
                bra    SHELLP

;;; FIREBALL MINI
FBOUT           ldd    #0
                std    ,Y
                sta    2,Y
                std    $100,Y
                sta    $102,Y
                ldu    OBJX,X
                ldd    #$9999
                bcs    SHFB1                ; ODD
                anda   #$0F
                std    ,U
                sta    2,U
                andb   #$F0
                stb    $101,U
                bra    SHELLP
SHFB1           anda   #$F0
                std    $100,U
                sta    $102,U
                andb   #$0F
                stb    1,U
                bra    SHELLP

;;; OUTPUT YLLAB BOMB
YLBOUT          ldu    OBJX,X
                ldd    #$0099
                sta    ,Y                   ; ERASE OLDIE
                sta    $100,Y
                sta    $200,Y
                bcs    !skip
                stb    ,U
                stb    $100,U
                jmp    SHELLP
!skip           lda    #$09
                sta    ,U
                stb    $100,U
                lda    #$90
                sta    $200,U
                jmp    SHELLP

;;; Ylabbian GFX?
YLBP1   FCB 2,1
        FDB YLBD10,YLBD10,ON34,OFF34
YLBD10  FDB $FFFF

;;; BOMB COLLISION
BKIL            ldd    #$25
                jsr    SCORE
                dec    BMBCNT
                jsr    KILSHL
                jsr    OFSHIT
                ldd    OX16,X
                lsra
                rorb
                lsra
                rorb
                addd   BGL
                std    OX16,X
                lda    OY16,X
                suba   #2
                sta    OY16,X
                ldd    #BXPIC
                std    OPICT,X
                jsr    XSVCT
                ldd    #AHSND
                jmp    SNDLD
;;; REVERSE
REV             lda    REVFLG
                bne    REVX
                inc    REVFLG
                ldd    PLADIR
                comb
                coma
                addd   #1
                std    NPLAD
REV1
                NAP (2,REV2)
REV2            lda    PIA21
                bita   #$40
                bne    REV1
                NAP (5,REVX1)
REVX1           clr    REVFLG
REVX            jmp    SUCIDE

;;; SMART BOMB
SBOMB           lda    SBFLG
                bne    SBMBX2
                ldx    PLRX
                lda    PSBC,X
                beq    SBMBX2
                inc    SBFLG
                dec    PSBC,X
                jsr    SBDSPV
                ldd    #SBSND
                jsr    SNDLD
SBMB00          ldx    OPTR
SBMB0           beq    SBMBX
                ldd    OX16,X
                subd   BGL
                cmpd   #$9C*64
                bhs    SBMB2
                lda    OTYP,X
                cmpa   #$02
                bhs    SBMB2
; 	JSR	[OCVECT,X]
                jmp    SBPCH                ;  *****************DONT BLOW A NULLOB **********
                bra    SBMB00
SBMB2           ldx    ,X
                bra    SBMB0
SBMBX           ldu    CRPROC
                lda    #4                   ; SCREEN FLASHES/2
                sta    proc.data ,U
SBMBX0          com    PCRAM
                NAP (2,SBMBX1)
SBMBX1          dec    proc.data ,U
                bne    SBMBX0
                clr    PCRAM
SBX10
                NAP (10,SBX1A)                  ; SWITCH DEBOUNCE
SBX1A           lda    PIA21
                bita   #$4
                bne    SBX10
                NAP (10,SBX2A)
SBX2A           clr    SBFLG
SBMBX2          jmp    SUCIDE

;;; HYPERSPACE
HYPER           lda    STATUS
                bita   #$FD
                lbne   HYPX                 ; NO GO
                anda   #2
                sta    proc.d4,U            ; SAVE TERRAIN BIT
                lda    #$77
                sta    STATUS
                jsr    SCLR1
                NAP (15,HYP02)
HYP02           ldx    SPTR                 ; KILL BULLETS
                beq    KB2
                jsr    KILSHL
                bra    HYP02
KB2             clr    BMBCNT
                ldd    SEED
                std    BGL
                std    BGLX
; RANDOM HYPER DIRECTION
                lsrb
                bcc    HYP0
                ldd    #$2000
                ldx    #$0300
                bra    HYP00
HYP0            ldx    #-$0300
                ldd    #$7000
HYP00           std    PLAX16
                stx    NPLAD
                ldb    HSEED
                lsrb
                addb   #YMIN
                stb    PLAY16
                std    NPLAXC
                CLRD()
                sta    PLAXV+2
                std    PLAXV
                std    PLAYV
                jsr    BGINIT
; APPEAR PLAYER
                ldb    #$50
                bsr    STCHK0
                OBI (PLAPIC,KILLNO,$0000)                 ; CREATE PHONY OBJECT
                ldd    #0
                std    OXV,X
                std    OYV,X
                ldd    PLAY16
                std    OY16,X
                ldd    PLAX16
                lsra
                rorb
                lsra
                rorb
                addd   BGL
                std    OX16,X
                lda    NPLAD
                bpl    HYP1
                ldu    #PLBPIC              ; REV IMAGE
                stu    OPICT,X
HYP1            ldu    CRPROC
                stx    proc.data ,U
                jsr    APVCT
                NAP ($28,HYP2)
HYP2
                MAKP (DPKIL)                    ; DELAYED KILL PROCESS
                ldd    proc.data ,U
                std    proc.data ,X
                lda    proc.d4,U
                sta    STATUS
                lda    LSEED                ; DEAD?
                cmpa   #192
                lbhi   HYPACH               ; YAHMAHN
HYPX            jmp    SUCIDE
;
; DELAYED PLAYER IMAGE KILL
;
DPKIL           ldx    proc.data ,U
                jsr    KILOFF
                jsr    GETOB
                NAP (10,DPK1)
DPK1            ldx    proc.data ,U
                ldd    OFREE                ; BACK TO FREE LIST
                std    ,X
                stx    OFREE
                jmp    SUCIDE
;
;  DUPLICATE NOKILL ROUTINE
;
KILLNO          clra
                puls   D,PC

;;; TERRAIN STATUS UPDATE
;;; with status defaulting to zero
STCHK           clrb                        ; CHECK TERRAIN ACTIVE

;;; TERRAIN STATUS UPDATE
;;; B = base for status
;;; Set terrain to innactive if ASTCNT == 0
STCHK0          lda    ASTCNT

;;; Store A to status
;;; if Z flag is set then innactivate terrain drawing
STCHKA          bne    !skip
                orb    #2
!skip           stb    STATUS
                rts

;;; MAKE A REG PROCESS
;;; X RET WITH PROC PTR
;;; [RET]=SADDR, TYPE=STYPE, RET=RET+2
MAKEP           pshs   U,A
                ldu    3,S                  ; GET X
                pulu   X
                stu    3,S                  ; NEW RETURN
                lda    #STYPE
                jsr    MKPROC
                puls   U,A,PC

;;; GATE RING OFFSET TABLE OUTER-INNER,INNER-CENT; TL-TL
GOFFTB  FDB $0805,$0805,$0705,$0604
        FDB $0503,$0403,$0302,$0201
        FDB $0101,$0001,$0001,$0001
        FDB $0001,$0001,$0001,$0001

;;; Clean SAM RAM
SCLEAN          ldx    #RAMALS
                stx    FREEP
SCLEN1          leax   $40,X
                stx    -$40,X
                cmpx   #RAMEND-$40
                bne    SCLEN1
                clra
                clrb
                std    ,X
                std    APPTR
                std    EXPTR
                std    TRPTR
                sta    SAMEXC
                rts

;;; STAR GATE OUTPUT
;;; OUTER RING
ONSG            pshs   X,CC
                addb   #8
                tfr    D,U
                lsra
                lsra
                lsra
                anda   #$FE                 ; CALC RING OFFSETS
                ldx    #GOFFTB
                ldd    A,X
                std    GATOF1
                puls   CC
                ldd    GATCL1
                bcs    ONSG1
                anda   #$F0
                andb   #$F0
                bra    ONSG2
ONSG1           anda   #$0F                 ; FLAV1
                andb   #$0F
ONSG2           tfr    D,X
                leay   ,X
                pshu   D,X,Y                ; LEFT SIDE
                pshu   D,X,Y
                leau   $70C,U
                pshu   D,X,Y                ; RT. SIDE
                pshu   D,X,Y
                bcs    ONSG3                ; ODD GUY
                leau   -$100,U
ONSG3           lda    GATCL1
                sta    ,U
                sta    -$100,U
                sta    -$200,U
                sta    -$300,U
                sta    -$400,U
                sta    -$500,U
                sta    -$600,U
                sta    11,U
                sta    -$100+11,U
                sta    -$200+11,U
                sta    -$300+11,U
                sta    -$400+11,U
                sta    -$500+11,U
                sta    -$600+11,U
                lda    GATOF1
                bcc    ONSG20
                inca                        ; ODD CASE
                lsra
                rorb                        ; SAVE CARRY
                adda   #-7
                bra    ONSG22
ONSG20          lsra
                rorb
                adda   #-6
ONSG22          rolb                        ; RESTORE CARRY
                ldb    #3                   ; VERT OFFSET
                leau   D,U                  ; STARTING ADDR
                ldd    GATCL2               ; NEXT COLOR
                bcs    ONSG21               ; ODD FLAV
                sta    ,U
                sta    5,U
                sta    $100,U
                sta    $105,U
                sta    $200,U
                sta    $205,U
                anda   #$F0
                andb   #$F0
                std    1,U
                std    3,U
                std    $300,U
                std    $302,U
                std    $304,U
                bra    ONSG30
; OUTPUT INNER RING ODD FLAVOR
ONSG21          sta    $100,U
                sta    $105,U
                sta    $200,U
                sta    $205,U
                sta    $300,U
                sta    $305,U
                anda   #$0F
                andb   #$0F
                std    $301,U
                std    $303,U
                std    ,U
                std    2,U
                std    4,U
; CENTER
ONSG30          lda    GATOF2
                ldb    #2                   ; VERT OFFSET
                adca   #0
                lsra
                leau   D,U                  ; ADDR
                ldd    GATCL3
                bcs    ONSG31
                anda   #$F0
                andb   #$F0
                std    ,U
                puls   X,PC
ONSG31          anda   #$0F
                andb   #$0F
                std    ,U
                puls   X,PC
;
; ERASE STAR GATE
;
OF812           pshs   X
                addb   #08
                tfr    D,U
                ldd    #0
                ldx    #0
                leay   ,X
                pshu   X,Y,D
                pshu   X,Y,D
                leau   268,U
                pshu   X,Y,D
                pshu   X,Y,D
                leau   268,U
                pshu   X,Y,D
                pshu   X,Y,D
                leau   268,U
                pshu   X,Y,D
                pshu   X,Y,D
                leau   268,U
                pshu   X,Y,D
                pshu   X,Y,D
                leau   268,U
                pshu   X,Y,D
                pshu   X,Y,D
                leau   268,U
                pshu   X,Y,D
                pshu   X,Y,D
                leau   268,U
                pshu   X,Y,D
                pshu   X,Y,D
                puls   X,PC
;
; STAR GATE PIX
;
SGPIC   FCB 8,4
        FDB SGD10,SGD10,ONSG,OF812
SGD10   FDB $0000,$0000,$0000,$0000
        FDB $0000,$0000,$1111,$1111
        FDB $1111,$1111,$0000,$0000
        FDB $0000,$0000,$0000,$0000
;
; GET ALTITUDE
; A=ALT,X=OBJ
;
GETALT          pshs   B,X
                ldd    OX16,X
                lsra
                rorb
                lsra
                rorb
                lsra
                rorb
                lsra
                rorb
                lsra
                rorb
                lsra
                rorb
                ldx    #ALTBL
                lda    D,X
                puls   B,X,PC
;
; TIEST
; A=# IN SQUAD 1-4
;
TIEST           sta    XTEMP
                ldb    TIEXV
                com    TFLG                 ; ALTERNATE DIRECTIONS
                bmi    TIEST1
                negb
TIEST1          stb    XTEMP+1              ; X VELOCITY
                ldx    #TIE
                lda    #STYPE
                jsr    MSPROC               ; MAKE A SUPER PROCESS
                leau   ,X                   ; SAVE INDEX
                lda    XTEMP
                sta    proc.data +8,U
                CLRD()
                std    proc.data ,U
                std    proc.d2,U
                std    proc.d4,U
                std    proc.d6,U
TIEST2
                OBI (TIEP1,TIEKIL,$8888)
                ldb    XTEMP+1
                sex
                std    OXV,X
                CLRD()
                std    OYV,X
                lda    XTEMP                ; X SPACING
                lsra
                rorb
                adda   XTEMP
                addd   PLABX
                adda   #$80
                std    OX16,X
                lda    #$50
                sta    OY16,X
                sta    proc.data +9,U       ; CRUISE ALT
                stu    OBJID,X
                stx    OPTR
                lda    XTEMP
                asla
                adda   #proc.data-2
                stx    A,U
                dec    XTEMP
                bne    TIEST2
TIESX           rts
;
; TIE PROCESS
; PD,PD2,PD4,PD6=TIES,0000=DEAD
; PD8=#IN SQUAD,PD+9=CRUISE ALT
;
TIE             lda    SEED                 ; ADD RANDOM Y
                anda   #$6
                adda   #proc.data
                ldx    A,U
                lbeq   TIEX                 ; NONE THERE
                ldb    SEED
                lda    #OBJL
                andb   #$3F
                addb   #-$20
                bmi    TIE0
                nega
TIE0            ldy    OPICT,X              ; NEW IMAGE
                leay   A,Y
                cmpy   #TIEP1
                bhs    TIE01
                ldy    #TIEP1
TIE01           cmpy   #TIEP4
                bls    TIE02
                ldy    #TIEP4
TIE02           sty    OPICT,X
                sex
                addd   OYV,X                ; NEW Y VEL
                std    OYV,X
                aslb
                rola
                aslb
                rola
                aslb
                rola
                tfr    A,B
                negb
                sex
                addd   OYV,X
                std    OYV,X
                lda    OBJY,X               ; ON SCREEN?
                bne    TIE09                ; YES
; OFF SCREEN CASE
; CRUISE ALT ADJUST
                lda    SEED
                cmpa   #$40
                bhi    TIE06                ; BYPASS THIS TIME
                anda   #$3
                adda   #-2
                adda   proc.data+9,U
                cmpa   #$40                 ; LIMIT CHECKING
                bhs    TIE03
                lda    #$40
TIE03           cmpa   #$68
                blo    TIE04
                lda    #$68
TIE04           sta    proc.data +9,U
; Y VEL ADJUST
TIE06           lda    proc.data +9,U
                suba   OY16,X
                adda   #$10
                cmpa   #$20
                bls    TIEX
                suba   #$10
                bmi    TIE07
                ldd    #-$10
                bra    TIE08
TIE07           ldd    #$10
TIE08           addd   OYV,X
                std    OYV,X
                bra    TIEX
; ON SCREEN
TIE09           suba   PLAYC                ; CLOSE TO PLAYER
                bmi    TIE20                ; BELOW
                cmpa   #$20
                blo    TIE10                ; CLOSE
                ldd    #$FFF0
                bra    TIE30
TIE10           cmpa   #$10
                bhi    TIE31
                ldd    #$0010
                bra    TIE30
TIE20           cmpa   #-$20
                bgt    TIE21
                ldd    #$0010
                bra    TIE30
TIE21           cmpa   #-$10
                blt    TIE31
                ldd    #$FFF0
TIE30           addd   OYV,X
                std    OYV,X
TIE31           lda    LSEED                ; BOMB?
                anda   #$7
                bne    TIEX                 ; NO
                bsr    BOMBST
TIEX
                NAP (1,TIE)
;
; TIE KILL
;
TIEKIL
                KILO ($125,TIHSND)
                dec    TIECNT
                ldu    OBJID,X
                leay   proc.data,U
TIEK1           cmpx   ,Y++
                bne    TIEK1
                CLRD()
                std    -2,Y
                dec    proc.data +8,U
                bne    TIEKX
                leax   ,U
                jsr    KILL
TIEKX           rts
;
; START BOMB
;
BOMBST          lda    BMBCNT
                cmpa   #10
                bhs    BMBSTX
                SHL (BMBOUT,BMBP1,BKIL)
                beq    BMBSTX
                ldb    HSEED
                sex
                aslb
                rola
                lda    SEED
                anda   #$1F
                inca
                sta    ODATA,X              ; LIFETIME
BMBSTX          rts
KILOFF          jsr    KILLOB
                jmp    OFSHIT
;
; KILL ROUTINE
; X=OBJECT
; RET=SCORE MSB,LSB
; RET+2=SOUND
;
KILP01          pshs   X
                jsr    KILLOP
                puls   X
                bra    KILO01
KILPOS          pshs   X
                jsr    KILLOP
                puls   X
KILOS           jsr    KILLOB
KILO01          pshs   U,D
                ldu    4,S
                pulu   D
                jsr    SCORE
                bsr    EXST
                pulu   D
                stu    4,S
                jsr    SNDLD
                puls   U,D,PC
;
; EXPLODE
; X=OBJECT TO BLOW
;
EXST            pshs   X,U,D,Y
                jsr    OFSHIT
                jsr    XSVCT
                puls   X,U,D,Y,PC
;
; COLOR TIES
; PD =TCTAB INDEX
TIECOL          ldx    #TCTAB
TIECL1          stx    proc.data ,U
                NAP (6,TIECL)
TIECL           ldx    proc.data ,U
                ldd    ,X++
                std    PCRAM+$D
                lda    ,X+
                sta    PCRAM+$F
                cmpx   #TCTAB+9
                blo    TIECL1
                bra    TIECOL
; TIE COLOR TABLE
TCTAB   FCB $81,$81,$2F
        FCB $81,$2F,$07
        FCB $2F,$81,$07
;
; COLOR BOMBS
;
CBOMB           lda    #$FF
                sta    PCRAM+$A
                clr    PCRAM+$C
                NAP (3,CBMB1)
CBMB1           lda    SEED
                anda   #$1F
                ldx    #COLTAB
                lda    A,X
                sta    PCRAM+$A
                sta    PCRAM+$C
                ldx    #BMBD10
                cmpx   BAX
                bne    CBMB2
                ldx    #BMBD20
CBMB2           stx    BAX
                NAP (6,CBOMB)
;
ON75            pshs   X                    ; SAVE X
                sts    TEMP48
                bcc    ON751
                leay   2,Y
ON751           lds    OBJP0,Y              ; SET POINTER TO BEGINNING OF OBJECT
                addb   #5
                tfr    D,U
                bra    ON750

ON65            pshs   X                    ; SAVE X
                sts    TEMP48
                bcc    ON651
                leay   2,Y
ON651           lds    OBJP0,Y              ; SET POINTER TO BEGINNING OF OBJECT
                addb   #5
                tfr    D,U
                bra    ON650

ON85            pshs   X                    ; SAVE X
                sts    TEMP48
                bcc    ON851
                leay   2,Y
ON851           lds    OBJP0,Y              ; SET POINTER TO BEGINNING OF OBJECT
                addb   #5
                tfr    D,U
                puls   X,Y,A
                pshu   X,Y,A
                leau   261,U
ON750           puls   X,Y,A
                pshu   X,Y,A
                leau   261,U
ON650           puls   X,Y,A
                pshu   X,Y,A
                leau   261,U
                puls   X,Y,A
                pshu   X,Y,A
                leau   261,U
                puls   X,Y,A
                pshu   X,Y,A
                leau   261,U
ON350           puls   X,Y,A
                pshu   X,Y,A
                leau   261,U
                puls   X,Y,A
                pshu   X,Y,A
                leau   261,U
                puls   X,Y,A
                pshu   X,Y,A
                lds    TEMP48               ; RESTORE	STACK
                puls   X,PC                 ; AND RETURN
;
; ON 3X5 IMAGE
;
ON35            pshs   X
                sts    TEMP48
                bcc    ON351
                leay   2,Y
ON351           lds    OBJP0,Y
                addb   #5
                tfr    D,U
                bra    ON350
; OFF 3X5
OFF35           pshs   X
                addb   #5
                tfr    D,U
                clra
                ldx    #0
                leay   ,X
                bra    OFF350

OFF75           pshs   X
                addb   #5
                tfr    D,U
                clra
                ldx    #0
                leay   ,X
                bra    OFF750

OFF65           pshs   X
                addb   #5
                tfr    D,U
                clra
                ldx    #0
                leay   ,X
                bra    OFF650

OFF85           pshs   X
                addb   #5
                tfr    D,U
                clra
                ldx    #0
                leay   ,X
                pshu   X,Y,A
                leau   261,U
OFF750          pshu   X,Y,A
                leau   261,U
OFF650          pshu   X,Y,A
                leau   261,U
                pshu   X,Y,A
                leau   261,U
OFF450          pshu   X,Y,A
                leau   261,U
OFF350          pshu   X,Y,A
                leau   261,U
                pshu   X,Y,A
                leau   261,U
                pshu   X,Y,A
                puls   X,PC                 ; GET OUT OF HERE

OFF45           pshs   X
                addb   #5
                tfr    D,U
                ldx    #0
                leay   ,X
                clra
                bra    OFF450
;
; DISPLAY YOUR INVISO
IVDISP          pshs   D,X,Y,U
                lda    SAMEXC
                bne    IVDX
                ldx    #P1IVD
                lda    .P1INV
                bsr    IVDSP
                lda    PLRCNT
                deca
                beq    IVDX
                ldx    #P2IVD
                lda    .P2INV
                bsr    IVDSP
IVDX            puls   D,X,Y,U,PC
IVDSP           pshs   A
                ldd    #$2402
                jsr    BLKCLR
                lda    ,S+
                beq    IVDSPX
                cmpa   #180
                bls    IVDSP1
                lda    #180
IVDSP1          ldb    #$FF
                stb    ,X
                stb    1,X
                leax   $100,X
                suba   #5
                bhi    IVDSP1
IVDSPX          rts

;;; DISPLAY LASERS
LDISP           pshs   D,X,Y,U              ; PLAYER 1
                lda    SAMEXC
                bne    !exit
                ldx    #P1LAS
                lda    .P1LAS
                bsr    LDSP
                lda    PLRCNT
                deca
                beq    !exit
                ldx    #P2LAS
                lda    .P2LAS
                bsr    LDSP
!exit           puls   D,X,Y,U,PC


;;; Display a laser
;;; A = 
;;; X -> 
LDSP            pshs   A
                ldd    #$2406
                jsr    BLKCLR
                lda    ,S
                cmpa   #17
                bls    LDSP1
                lda    #17                  ; MAXED
LDSP1           cmpa   #10
                blo    LDSP2                ; >10
                suba   #10
                sta    ,S                   ; SAVE 1S
                ldy    #PLAPIC
                tfr    X,D
                jsr    CWRIT
                ldy    #PL10P               ; 10
                addd   #$0002
                jsr    CWRIT
                leax   $0803,X
LDSP2           lda    ,S
                beq    LDSPX                ; NOBODY LEFT
                tfr    X,D
                ldy    #PL1P
LDSP3           jsr    CWRIT                ; WRITE OUT YOUR SHIPS
                adda   #4
                dec    ,S
                bne    LDSP3
LDSPX           puls   A,PC

;;; DISPLAY TOP OF SCREEN
TDISP           pshs   D,X,Y,U
                ldd    #$4020
                ldx    #SCANER
                jsr    BLKCLR
                ldx    #$0008               ; PLAYER 1 UPPER RIGHT CORNER (NOTE...NO EQUATE)
                ldd    #$2F28               ; PLAYER 1 REGION SIZE
                jsr    BLKCLR
                ldx    #$7108               ; PLAYER 2 UPPER RIGHT
                ldd    #$2B28               ; REGION SIZE
                jsr    BLKCLR
                jsr    BORDER
                jsr    LDISP
                bsr    SBDISP
                jsr    IVDISP
                lda    PLRCNT
!TD1            jsr    SCRTR0
                deca
                bne    !TD1
                jsr    MESINV               ; INITIALIZE MESSAGE REGION
                puls   D,X,Y,U,PC

;;; SMART BOMB DISPLAY
SBDISP          pshs   X,Y,D,U
                lda    SAMEXC               ; CHECK IF OK TO WRITE
                bne    SBDX
                ldx    #P1SBD
                lda    .P1SBC
                bsr    SBDSP
                lda    PLRCNT
                deca
                beq    SBDX
                ldx    #P2SBD
                lda    .P2SBC
                bsr    SBDSP
SBDX            puls   X,Y,D,U,PC
SBDSP           pshs   A
                ldd    #$2405
                jsr    BLKCLR
                lda    ,S
                cmpa   #17
                bls    SBDSP1
                lda    #17
SBDSP1          cmpa   #10
                blo    SBDSP2
                suba   #10
                sta    ,S
                ldy    #SB10P
                tfr    X,D
                jsr    CWRIT
                leax   $0802,X
SBDSP2          lda    ,S
                beq    SBDSPX
                ldy    #SBPIC
                tfr    X,D
SBDSP3          jsr    CWRIT
                adda   #4
                dec    ,S
                bne    SBDSP3
SBDSPX          puls   A,PC

;;; PLAYER SHIP 10DISPLAY
PL10P   FCB 4,3
        FDB PL10D
PL10D   FCB $BB,$0B,$BB
        FCB $9B,$9B,$96
        FCB $99,$90,$99
        FCB $9B,$96,$96

;;; PLAYER SHIP DISPLAY ONES UNITS
PL1P    FCB 4,3
        FDB PL1D00
PL1D00  FCB $BB,$BB,$B6
        FCB $00,$BE,$66
        FCB $00,$F0,$6F
        FCB $00,$00,$F0
;;; SMART BOMB #10# DISP
SB10P   FCB 7,5
        FDB SB10D
SB10D   FCB $99,$00,$00,$00,$99
        FCB $00,$90,$99,$90,$00
        FCB $09,$92,$92,$92,$09
        FCB $99,$92,$92,$92,$99
        FCB $99,$22,$92,$22,$99
        FCB $90,$9C,$9C,$9C,$90
        FCB $00,$00,$C0,$00,$00
;
; DRAW BORDER
;
BORDER          lda    SCNCOL
                ldb    SCNCOL
                ldx    #PLIN
BORD0           sta    ,X
                leax   $100,X
                cmpx   #SCANER
                blo    BORD0
                ldx    #P2ORG+PLIN-$100     ; PLAYER 2 LINE
BORD00          sta    ,X
                leax   $100,X
                cmpx   #$9C00
                blo    BORD00
                sta    SCANER-1             ; SCANNER CORNERS
                sta    SCANER+$FF
                sta    SCANER+28
                sta    SCANER+$100+28
                anda   #$0F
                ldx    #SCANER-$100-1
BORD01          sta    ,X+
                cmpx   #SCANER-$100+35
                bne    BORD01
                lda    SCNCOL               ; RIGHT CORNERS
                ldb    SCNCOL
                sta    SCANER+$3F00-1
                sta    SCANER+$3E00-1
                sta    SCANER+$3E00+28
                sta    SCANER+$3F00+28
                anda   #$F0
                ldx    #SCANER+$4000-1
BORD02          sta    ,X+
                cmpx   #SCANER+$4009+26
                bne    BORD02
                ldd    #$9999
                ldx    #SCANER+$1C00-2
BORD3           std    ,X
                std    30,X
                leax   $100,X
                cmpx   #SCANER+$2400-2
                bne    BORD3
                rts
;
;
;
; START DATA TABLES
;
; SWITCH TABLE

;
; COLOR RAM TABLE
;
CRTAB   FCB 0                           ;  0 SPACED OUT
        FCB $F9                         ;  1 LASER (SPECIAL)
        FCB $07                         ;  2 RED
        FCB $28                         ;  3 GREEN
        FCB $2F                         ;  4 YELLOW
        FCB $00                         ;  5 MESSAGES
        FCB $A4                         ;  6 GRAY
        FCB $15                         ;  7 TERRAIN
        FCB $C7                         ;  8 PURPLE
        FCB $FF                         ;  9 WHITE
        FCB $38                         ;  A BOMB CYCLER
        FCB $17                         ;  B ORANGE
        FCB $CC                         ;  C CYCLER
        FCB $81                         ;  D TIE1
        FCB $81                         ;  E TIE2
        FCB $2F                         ;  F TIE3,MONO

PACTAB  FDB $0205,C5P1                  ; 500 POINTS, 500 PICTURE
        FDB $0210,C10P1                 ; 1000
        FDB $0215,C15P1                 ; 1500
        FDB $0220,C20P1

; *****************************
; G R A P H I C S
; *****************************
; SCHITZO
SCZP1   FCB 5,8
        FDB SCZD10,SCZD11,ON58,OFF58
; ASTRO EXPLODE
ASXP1   FCB 4,8
        FDB ASXD10,ASXD10,ON48,OFF48
; SWARM EXPLODE
SWXP1   FCB 4,8
        FDB SWXD10,SWXD10,ON48,OFF48
; NULL OBJECT
NULOB   FCB 1,1
        FDB NULD10,NULD10,DRTS,DRTS
NULD10  FCB 0
; PROBE
PRBP1   FCB 4,8
        FDB PRBD10,PRBD11,ON48,OFF48
; ASTRONAUTS
; FACING LEFT
ASTP1   FCB 2,8
        FDB ASTD10,ASTD11,ON28,OFF28
ASTP2   FCB 2,8
        FDB ASTD20,ASTD21,ON28,OFF28
; FACING RT.
ASTP3   FCB 2,8
        FDB ASTD30,ASTD31,ON28,OFF28
ASTP4   FCB 2,8
        FDB ASTD40,ASTD41,ON28,OFF28
; TIE PICTS
TIEP1   FCB 4,8
        FDB TIED10,TIED11,ON48,OFF48
TIEP2   FCB 4,8
        FDB TIED20,TIED21,ON48,OFF48
TIEP3   FCB 4,8
        FDB TIED30,TIED31,ON48,OFF48
TIEP4   FCB 4,8
        FDB TIED40,TIED41,ON48,OFF48
; BOMB EXPLOSION PICT
BXPIC   FCB 4,8
        FDB BXD10,BXD10,ON48,OFF48
; BOMB PICT
BMBP1   FCB 2,3
        FDB BMBD10,BMBD11,ON23,OFF23
BMBP2   FCB 2,3
        FDB BMBD20,BMBD21,ON23,OFF23
; LASER PICT
LASP1   FCB 8,1
        FDB LASD10
LASD10  FDB $FFFF,$FFFF,$FFFF,$FFFF
; SWARM PICTS
SWPIC1  FCB 3,4
        FDB SWMD10,SWMD11,ON34,OFF34
; PLAYER PICTURE
PLAPIC  FCB 8,6
        FDB PLD10,PLD11,ON86,OFF86
PLBPIC  FCB 8,6
        FDB PLD20,PLD21,ON86,OFF86
; SMART BOMB
SBPIC   FCB 3,3
        FDB SBD10
;  DANCING 2
; 250 SCORE
C25P1   FCB 6,5
        FDB C25D10,C25D11,ON65,OFF65

; 500 SCORE

C5P1    FCB 6,5
        FDB C5D10,C5D11,ON65,OFF65
C10P1   FCB 7,5
        FDB C10EVN,C10ODD
        FDB ON75,OFF75

C15P1   FCB 7,5
        FDB C15EVN,C15ODD
        FDB ON75,OFF75

C20P1   FCB 8,5
        FDB C20EVN,C20ODD
        FDB ON85,OFF85

; 250 SCORE
C25D10  FCB $11,$00,$11,$10,$11
        FCB $10,$10,$10,$00,$10
        FCB $11,$10,$11,$00,$11
        FCB $10,$00,$10,$10,$10
        FCB $11,$10,$10,$10,$11
        FCB $10,$10,$10,$10,$10
C25D11  FCB $01,$00,$01,$01,$01
        FCB $11,$01,$11,$00,$11
        FCB $01,$01,$01,$00,$01
        FCB $11,$00,$11,$01,$11
        FCB $01,$01,$01,$01,$01
        FCB $11,$01,$01,$01,$11
; 500 SCORE
C5D10
        FCB $FF,$F0,$FF,$00,$FF
        FCB $F0,$00,$F0,$F0,$F0
        FCB $EE,$E0,$E0,$E0,$EE
        FCB $E0,$E0,$E0,$E0,$E0
        FCB $DD,$D0,$D0,$D0,$DD
        FCB $D0,$D0,$D0,$D0,$D0
C5D11
        FCB $0F,$0F,$0F,$00,$0F
        FCB $FF,$00,$FF,$0F,$FF
        FCB $0E,$0E,$0E,$0E,$0E
        FCB $EE,$0E,$0E,$0E,$EE
        FCB $0D,$0D,$0D,$0D,$0D
        FCB $DD,$0D,$0D,$0D,$DD

C10EVN  FCB $10,$10,$10,$10,$10
        FCB $11,$10,$10,$10,$11
        FCB $10,$10,$10,$10,$10
        FCB $11,$10,$10,$10,$11
        FCB $10,$10,$10,$10,$10
        FCB $11,$10,$10,$10,$11
        FCB $10,$10,$10,$10,$10

C10ODD  FCB $01,$01,$01,$01,$01
        FCB $01,$01,$01,$01,$01
        FCB $11,$01,$01,$01,$11
        FCB $01,$01,$01,$01,$01
        FCB $11,$01,$01,$01,$11
        FCB $01,$01,$01,$01,$01
        FCB $11,$01,$01,$01,$11

C15EVN  FCB $C0,$C0,$C0,$C0,$C0
        FCB $CC,$C0,$CC,$00,$CC
        FCB $C0,$00,$C0,$C0,$C0
        FCB $CC,$C0,$C0,$C0,$CC
        FCB $C0,$C0,$C0,$C0,$C0
        FCB $CC,$C0,$C0,$C0,$CC
        FCB $C0,$C0,$C0,$C0,$C0

C15ODD  FCB $0C,$0C,$0C,$0C,$0C
        FCB $0C,$0C,$0C,$00,$0C
        FCB $CC,$00,$CC,$0C,$CC
        FCB $0C,$0C,$0C,$0C,$0C
        FCB $CC,$0C,$0C,$0C,$CC
        FCB $0C,$0C,$0C,$0C,$0C
        FCB $CC,$0C,$0C,$0C,$CC

C20EVN  FCB $FF,$00,$FF,$F0,$FF
        FCB $F0,$F0,$F0,$00,$F0
        FCB $FF,$F1,$F1,$F1,$FF
        FCB $F0,$F0,$F0,$F0,$F0
        FCB $FF,$F1,$F1,$F1,$FF
        FCB $F0,$F0,$F0,$F0,$F0
        FCB $FF,$F1,$F1,$F1,$FF
        FCB $F0,$F0,$F0,$F0,$F0

C20ODD  FCB $0F,$00,$0F,$0F,$0F
        FCB $FF,$0F,$FF,$00,$FF
        FCB $0F,$0F,$0F,$0F,$0F
        FCB $FF,$1F,$1F,$1F,$FF
        FCB $0F,$0F,$0F,$0F,$0F
        FCB $FF,$1F,$1F,$1F,$FF
        FCB $0F,$0F,$0F,$0F,$0F
        FCB $FF,$1F,$1F,$1F,$FF
;
; SCHITZO
;
SCZD10  FCB $00,$03,$F0,$F0,$F3,$00,$01,$10
        FCB $0C,$0C,$04,$08,$38,$10,$00,$00
        FCB $C0,$C0,$38,$78,$78,$A0,$A0,$A0
        FCB $00,$33,$03,$03,$33,$10,$01,$00
        FCB $00,$00,$F0,$F0,$F0,$00,$00,$10
SCZD11  FCB $00,$00,$0F,$0F,$0F,$00,$00,$01
        FCB $00,$30,$00,$00,$33,$01,$10,$00
        FCB $CC,$CC,$43,$87,$87,$0A,$0A,$0A
        FCB $00,$03,$80,$80,$83,$01,$00,$00
        FCB $00,$30,$3F,$3F,$3F,$00,$10,$01
; ASTRO EXPLODE
ASXD10  FDB $0000,$0D6C,$6C0D,$0000
        FDB $06E6,$C883,$82C8,$EC06
        FDB $606D,$8C28,$288C,$6D60
        FDB $0000,$E0C6,$C6E0,$0000
; SWARM EXPLODE
SWXD10  FDB $0000,$0222,$2402,$0000
        FDB $0222,$4444,$2442,$2200
        FDB $2022,$4444,$2442,$2200
        FDB $0000,$2022,$2220,$0000
; PROBE
PRBD10  FDB $000E,$00D8,$000E,$0000                ; 1
        FDB $0F08,$8CC8,$8C08,$0F00                ; 2
        FDB $000E,$80C8,$800E,$0000                ; 3
        FDB $0000,$00D0,$0000,$0000                ; 4
PRBD11  FDB $0000,$000D,$0000,$0000                ; 1
        FDB $00E0,$088C,$08E0,$0000                ; 2
        FDB $F080,$C88C,$C880,$F000                ; 3
        FDB $00E0,$008D,$00E0,$0000                ; 4
; ASTRONAUTS
ASTD10  FDB $3343,$4387,$8707,$0707                ; 1
        FDB $0000,$8080,$8000,$0000
ASTD11  FDB $0304,$0408,$0800,$0000                ; 1
        FDB $3030,$3878,$7870,$7070                ; 2
ASTD20  FDB $3343,$4387,$8777,$7777                ; 1
        FDB $0000,$8080,$8000,$0000
ASTD21  FDB $0304,$0408,$0807,$0707
        FDB $3030,$3878,$7870,$7070
ASTD30  FDB $0303,$8387,$8707,$0707
        FDB $3040,$4080,$8000,$0000
ASTD31  FDB $0000,$0808,$0800,$0000
        FDB $3334,$3478,$7870,$7070
ASTD40  FDB $0303,$8387,$8707,$0707
        FDB $3040,$4080,$8070,$7070
ASTD41  FDB $0000,$0808,$0800,$0000
        FDB $3334,$3478,$7877,$7777
;
; TIE PICTS
;
TIED10  FDB $0808,$DDDE,$DEDE,$DD00                ; 1
        FDB $8888,$DDEE,$FEEE,$DD00                ; 2
        FDB $8888,$D8D8,$D8D0,$D000                ; 3
        FDB 0,0,0,0                     ; 4
TIED11  FDB $0000,$0D0D,$0D0D,$0D00                ; 1
        FDB $8888,$DDEE,$EFEE,$DD00                ; 2
        FDB $8888,$DDED,$EDED,$DD00                ; 3
        FDB $8080,$8080,$8000,$0000                ; 4
TIED20  FDB $0008,$DDDE,$DEDE,$DD00                ; 1
        FDB $0088,$DDEE,$FEEE,$DD00                ; 2
        FDB $0088,$D8D8,$D8D8,$D000                ; 3
        FDB 0,0,0,0                     ; 4
TIED21  FDB $0000,$0D0D,$0D0D,$0D00                ; 1
        FDB $0088,$DDEE,$EFEE,$DD00                ; 2
        FDB $0088,$DDED,$EDED,$DD00                ; 3
        FDB $0080,$8080,$8080,$0000                ; 4
TIED30  FDB $0000,$DDDE,$DEDE,$DD00                ; 1
        FDB $0000,$DDEE,$FEEE,$DD00                ; 2
        FDB $0000,$D8D8,$D8D8,$D800                ; 3
        FDB 0,0,0,0                     ; 4
TIED31  FDB $0000,$0D0D,$0D0D,$0D00                ; 1
        FDB $0000,$DDEE,$EFEE,$DD00                ; 2
        FDB $0000,$DDED,$EDED,$DD00                ; 3
        FDB $0000,$8080,$8080,$8000                ; 4
TIED40  FDB $0000,$DDDE,$DEDE,$DD00                ; 1
        FDB $0000,$DDEE,$FEEE,$DD88                ; 2
        FDB $0000,$D0D8,$D8D8,$D888                ; 3
        FDB 0,0,0,0
TIED41  FDB $0000,$0D0D,$0D0D,$0D00                ; 1
        FDB $0000,$DDEE,$EFEE,$DD88                ; 2
        FDB $0000,$DDED,$EDED,$DD88                ; 3
        FDB $0000,$0080,$8080,$8080                ; 4
; BOMB EXPLOSION PICT
BXD10   FDB $000C,$CCCC,$CCCC,$0C00
        FDB $CCCC,$CCCC,$CCCC,$CCCC
        FDB $CCCC,$CCCC,$CCCC,$CCCC
        FDB $00C0,$CCCC,$CCCC,$C000
;
; BOMB PICT
;
BMBD10  FDB $A00A,$A0A0,$00A0
BMBD11  FDB $0A00,$0A0A,$A00A
BMBD20  FDB $0AAA,$0A00,$A000
BMBD21  FDB $000A,$00A0,$AAA0
; SWARM PICTS
SWMD10  FDB $0002,$2302
        FDB $2022,$2322
        FDB $0000,$2000
SWMD11  FDB $0000,$0200
        FDB $0222,$3222
        FDB $0020,$3220
;
; PLAYER PICTURE
;
PLD10   FCB $FB,$0B,$BB,$0B,$BB,$06
        FCB $00,$B0,$BB,$B6,$66,$66
        FCB $00,$00,$BB,$66,$66,$FF
        FCB $00,$00,$BB,$66,$66,$F0
        FCB $00,$00,$B0,$6D,$66,$00
        FCB $00,$00,$00,$EF,$66,$00
        FCB $00,$00,$00,$00,$FF,$00
        FCB $00,$00,$00,$00,$F0,$00
PLD11   FCB $0F,$00,$0B,$00,$0B,$00
        FCB $B0,$BB,$BB,$BB,$B6,$66
        FCB $00,$00,$BB,$66,$66,$6F
        FCB $00,$00,$BB,$66,$66,$FF
        FCB $00,$00,$BB,$66,$66,$00
        FCB $00,$00,$00,$DE,$66,$00
        FCB $00,$00,$00,$F0,$6F,$00
        FCB $00,$00,$00,$00,$FF,$00
PLD20   FCB $00,$00,$00,$00,$FF,$00
        FCB $00,$00,$00,$0F,$F6,$00
        FCB $00,$00,$00,$ED,$66,$00
        FCB $00,$00,$BB,$66,$66,$00
        FCB $00,$00,$BB,$66,$66,$FF
        FCB $00,$00,$BB,$66,$66,$F6
        FCB $0B,$BB,$BB,$BB,$6B,$66
        FCB $F0,$00,$B0,$00,$B0,$00
PLD21   FCB $00,$00,$00,$00,$0F,$00
        FCB $00,$00,$00,$00,$FF,$00
        FCB $00,$00,$00,$FE,$66,$00
        FCB $00,$00,$0B,$D6,$66,$00
        FCB $00,$00,$BB,$66,$66,$0F
        FCB $00,$00,$BB,$66,$66,$FF
        FCB $00,$0B,$BB,$6B,$66,$66
        FCB $BF,$B0,$BB,$B0,$BB,$60
; SMART BOMB
SBD10   FCB $90,$09,$90
        FCB $99,$99,$99
        FCB $90,$CC,$90
; ************SCANNER BLIP FIX
PACH1           ldu    CRPROC
                lda    proc.data ,U
                inca
                cmpa   #4
                bls    PACH10
                clra
PACH10          sta    proc.data ,U
                bne    PACH1X
                jsr    WVCHKV
                cmpa   #5
                bhi    PACH1X               ; TOO MANY GUYS
                ldx    #OPTR
                ldu    #SETAB
                bsr    PCH13
                ldx    #IPTR
                bsr    PCH13
                jmp    BOZ2                 ; EXIT BACK
PACH1X          ldx    #OPTR
                jmp    BOZ1

; BLIP SCAN SUBROUTINE
PCH110          ldd    OX16,X               ; FIND OFFSET
                subd   XTEMP                ; SUBTRACT SCANNER LEFT
                lsra                        ; SHIFT	DOWN TO 6 BITS
                lsra
                ldb    OY16,X
                lsrb
                lsrb
                lsrb
                addd   #SCANER-5
                std    ,U
                ldd    OBJCOL,X             ; STORE BLIP
                beq    BLPX
                cmpd   #$9000
                beq    BLPX                 ; HUM
                cmpd   #$4000
                beq    BLPX                 ; FIREBALL
                cmpa   #$33                 ; BAITER
                beq    BLPX
                cmpd   #$6666
                beq    BLPX                 ; HUMANOIDS
                cmpd   #$4433
                beq    BLPX                 ; LANDERS
                cmpa   #$1F                 ; STAR GATE?
                bne    PCH120               ; NO
                ldd    ,U
                cmpa   #( SCANER>>8 )+$3F   ; CLIP GATE AT LIMIT
                bne    PCH12A
                deca
                sta    ,U
PCH12A          ldy    #$FFF0
                sty    [,U++]
                incb
                std    ,U
                ldy    #$F0FF
                sty    [,U++]
                inca
                std    ,U
                ldy    #$F0F0
                sty    [,U++]
                decb
                std    ,U
                sty    [,U++]
                bra    PCH13
PCH120          ldd    #$9999
BLPX            std    [,U++]
PCH13           ldx    ,X                   ; DO THE REST
                bne    PCH110
                rts

;;; NO LOW PRIORITY SOUNDS IN ATTR
SNDPCH          pshs   X,D,CC
                clr    THFLG
                tfr    D,X
                lda    ,X
                tst    STATUS
                lbpl   $705                 ; NOT ATTR
SNDPCX          cmpa   #$F0
                lbhs   $705
                puls   X,D,CC,PC            ; NOGO

;;; ATTR SWITCH SCAN PATCH
SWPCH           ldb    STATUS
                bpl    SWPCHX
                lsrb
                bcs    SWPCHX
                ldb    PIA2
                andb   #$7D
                orb    ATRSW2               ; THRUST+ UP
                stb    PIA21
                ldb    PIA3
                andb   #$FE
                orb    ATRSW3
                jmp ::core::PATCH2
SWPCHX          ldb    PIA2
                jmp ::core::PATCH1

;;; NO SCORE IN ATTR PATCH
SCRPCH          pshs   A,B,X,U,Y
                tst    STATUS
                bmi    SCRPCX               ; ATTR NO SCORE
                orcc   #1
                jmp    SCORE2
SCRPCX          puls   A,B,X,U,Y,PC

;;; SMART BOMB DONT KILL NULLOBS PATCH
SBPCH           ldd    OPICT,X
                cmpd   #NULOB
                lbeq   SBMB2
                jsr    [OCVECT,X]
SBRET           jmp    SBMB00
LASPAT          lda    SAMEXC               ; NO LASER DURING BAD TERRAIN
                bne    NOLAS
                lda    LFLG                 ; HOW MANY
                cmpa   #4
                bhs    NOLAS
                rts
NOLAS           jmp    SUCIDE

            org 0x2b70
SWPACH          beq    SWP_
                jsr    INVSTV
SWP_            lda    INDIAG               ; NO SWITCH PROC IF IN DIAGS
                bne    SWP3_                ; NO SWITCH PROCESSING
                ldx    SWPROC               ; CHECK FIRST GUY
                bne    SWP1_                ; DO HIS WORK
                ldx    SWPROC+4             ; CHECK SECOND GUY
                beq    SWP2_                ; EMPTY....CHECK THIRD
                ldd    SWPROC+6
                clr    SWPROC+4
                clr    SWPROC+5
                jmp    SWP3                 ; AND GO BACK AND DO REST
SWP1_           ldd    SWPROC+2
                clr    SWPROC
                clr    SWPROC+1
                jmp    SWP3                 ; JUMP BACK AND DO IT
SWP2_           ldx    SWPR3_               ; CHECK THIRD SWITCH COUNTER
                beq    SWP4_                ; NOTHING THERE...DONE.
                ldd    SWPR3_+2
                clr    SWPR3_
                clr    SWPR3_+1
                jmp    SWP3
SWP4_           ldx    SWPR4_
                lbeq   SWPX
                ldd    SWPR4_+2
                clr    SWPR4_
                clr    SWPR4_+1
                jmp    SWP3
;
SWP3_           jmp    SWPX                 ; JUMP PAST SWITCH PROCESSING

; IRQ SWITCH PATCH
SWWPC9          bne    SWWPC0               ;  NO ROOM IN FIRST COUNTER
                std    SWPROC
                stx    SWPROC+2
                bra    SWWPC4
SWWPC0          ldu    SWPROC+4
                bne    SWWPC2
                std    SWPROC+4
                stx    SWPROC+6
                bra    SWWPC4
SWWPC2          ldu    SWPR3_
                bne    SWWPC3
                std    SWPR3_
                stx    SWPR3_+2
                bra    SWWPC4
SWWPC3          std    SWPR4_
                stx    SWPR4_+2
SWWPC4          puls   U,D                  ; GET BASE, SHIFTERS BACK
                tsta
                lbne   SW0                  ; BIT STILL IN A NEED PROCESSING
                rts                         ; ELSE RETURN TO INTERRUPT PROCESSOR
HYPACH          lda    SCRFLG               ; ANY SCORE OR IS IT SAFE TO KILL HIM?
                bne    HYPC1                ; YEP...KILL HIM
                jmp    SUCIDE               ; NOPE..DIE
HYPC1           jmp    PLNDV1

VOLPAC          clr    lava.time,X
                clr    lava.x8,X
                jmp    VOLCLX


